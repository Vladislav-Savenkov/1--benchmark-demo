
#Область ОбъявлениеПеременных

&НаКлиенте
Перем ПредыдущаяУслуга;
&НаКлиенте
Перем ПредыдущаяУслугаЖКУ;

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Получаем настройки из хранилища.
	Если Параметры.Свойство("АдресНастроекВХранилище") И Параметры.АдресНастроекВХранилище <> Неопределено Тогда
		
		СтруктураНастроек = ПолучитьИзВременногоХранилища(Параметры.АдресНастроекВХранилище);
		
		Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
			
			// Таблица настроек справочника ОСЗН.
			врТаблицаНастроек = Неопределено;
			Если СтруктураНастроек.Свойство("РостовскаяОбласть_ТаблицаНастроек", врТаблицаНастроек) Тогда
				Если ТипЗнч(врТаблицаНастроек) = Тип("ТаблицаЗначений") Тогда
					Объект.РостовскаяОбласть_ТаблицаНастроек.Загрузить(врТаблицаНастроек);
				КонецЕсли;
			КонецЕсли;
			
			// Таблица услуг справочника ОСЗН в базе.
			врТаблицаУслугВБазе = Неопределено;
			Если СтруктураНастроек.Свойство("РостовскаяОбласть_УслугиВБазе", врТаблицаУслугВБазе) Тогда
				Если ТипЗнч(врТаблицаУслугВБазе) = Тип("ТаблицаЗначений") Тогда
					Объект.РостовскаяОбласть_УслугиВБазе.Загрузить(врТаблицаУслугВБазе);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	// Блокировка командных панелей.
	Элементы.ТП_ЖКУ_ВБазеДобавить.Доступность = Ложь;
	Если Объект.РостовскаяОбласть_ТаблицаНастроек.Количество() <> 0 Тогда
		Элементы.ТП_ЖКУ_ВБазеДобавить.Доступность = Истина;
	КонецЕсли;
	
	Элементы.ТП_ЖКУ_ВБазе_ДолжникиДобавить.Доступность = Ложь;
	Если Объект.РостовскаяОбласть_ТаблицаНастроекДолжники.Количество() <> 0 Тогда
		Элементы.ТП_ЖКУ_ВБазе_ДолжникиДобавить.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти


//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область КоманднаяПанель

&НаКлиенте
// Обработчик команды "КомандаСохранить".
//
Процедура КомандаСохранить(Команда)
	
	АдресНастроекВХранилище = ПоместитьНастройкиВХранилище(); 
	Закрыть(АдресНастроекВХранилище);
	
КонецПроцедуры // КомандаСохранить()

////////////////////
// Работа с временным хранилищем

&НаСервере
// Помещает таблицу услуг во временное хранилище.
//
// Возвращаемое значение:
//  Строка - адрес настроек во временном хранилище.
//
Функция ПоместитьНастройкиВХранилище()
	
	СтруктураНастроек = Новый Структура;
	
	СтруктураНастроек.Вставить("РостовскаяОбласть_ТаблицаНастроек", Объект.РостовскаяОбласть_ТаблицаНастроек.Выгрузить());
	СтруктураНастроек.Вставить("РостовскаяОбласть_УслугиВБазе",     Объект.РостовскаяОбласть_УслугиВБазе.Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураНастроек, Новый УникальныйИдентификатор);
	
КонецФункции // ПоместитьНастройкиВХранилище()

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ УСЛУГИ В ОСЗН

#Область УслугиВОСЗН

&НаКлиенте
// Процедура - обработчик события "ПриАктивизацииСтроки" таблицы "УслугаВОСЗН".
//
Процедура ТаблицаНастроекПриАктивизацииСтроки(Элемент)
	
	ТП_ЖКУ_ВБазе.Очистить();
	
	// При активации строки УслугаВОСЗН, загружаем соответствия в УслугаВБазе. 
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", Элемент.ТекущиеДанные.НомерСтроки);
		
		НайденныеСтроки = Объект.РостовскаяОбласть_УслугиВБазе.НайтиСтроки(Отбор);
		
		Для Каждого ТекСтрока из НайденныеСтроки Цикл
			ЗаполнитьЗначенияСвойств(ТП_ЖКУ_ВБазе.Добавить(), ТекСтрока);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ТаблицаНастроекПриАктивизацииСтроки()

&НаКлиенте
// Процедура - обработчик события "ПередНачаломДобавления" строки таблицы "УслугаВОСЗН". 
//
Процедура ТаблицаНастроекПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// Устанавливаем запрет на добавление строк копированием.
	Если Копирование Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("В таблице ""Услуги в ОСЗН"" добавление строк копированием запрещено!");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // ТаблицаНастроекПередНачаломДобавления()

&НаКлиенте
Процедура РостовскаяОбласть_ТаблицаНастроекПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.СпособОпределенияНачислений = "Совокупный";
	Иначе
		ПредыдущийКодУслугиВОСЗН = Элемент.ТекущиеДанные.КодУслугиВОСЗН;
	КонецЕсли;
	
КонецПроцедуры // РостовскаяОбласть_ТаблицаНастроекПриНачалеРедактирования()

&НаКлиенте
// Процедура - обработчик события "ПередУдалением" таблицы "УслугаВОСЗН".
//
Процедура ТаблицаНастроекПередУдалением(Элемент, Отказ)
	
	// Удаление текущей строки соответствия из правой колонки.
	СтрокиУдаления = Объект.РостовскаяОбласть_УслугиВБазе.НайтиСтроки(Новый Структура("Идентификатор", Элемент.ТекущиеДанные.НомерСтроки));
	Для Каждого СтрокаУдаления ИЗ СтрокиУдаления Цикл
		Объект.РостовскаяОбласть_УслугиВБазе.Удалить(СтрокаУдаления);
	КонецЦикла;
	
КонецПроцедуры // ТаблицаНастроекПередУдалением()

&НаКлиенте
// Процедура - обработчик события "ПослеУдаления" таблицы "УслугаВОСЗН".
//
Процедура ТаблицаНастроекПослеУдаления(Элемент)
	
	// После удаления строки из УслугиВОСЗН переписываем идентификаторы в УслугиВБазе.
	Для Каждого СтрокаОСЗН Из Объект.РостовскаяОбласть_ТаблицаНастроек Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("КодУслугиВОСЗН", СтрокаОСЗН.КодУслугиВОСЗН);
		Отбор.Вставить("УслугаВОСЗН",    СтрокаОСЗН.УслугаВОСЗН);
		
		СтрокиИзменения = Объект.РостовскаяОбласть_УслугиВБазе.НайтиСтроки(Отбор);
		
		Для Каждого Строка ИЗ СтрокиИзменения Цикл
			Строка.Идентификатор = СтрокаОСЗН.НомерСтроки; 
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаНастроекПриАктивизацииСтроки(Элемент);
	
	// Если в левой колонке пользователем удалены все колонки, то блокируем возможность добавления соответствий.
	Если Объект.РостовскаяОбласть_ТаблицаНастроек.Количество() = 0 Тогда
		Элементы.ТП_ЖКУ_ВБазеДобавить.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ТаблицаНастроекПослеУдаления()

&НаКлиенте
// Процедура - обработчик события "ПередОкончаниемРедактирования" таблицы "УслугаВОСЗН".
//
Процедура ТаблицаНастроекПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	// Нужно, если происходит отмена редактирования строки ТЧ.
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	КодОшибки = 0;
	
	// Проверяем на наличие и уникальность код услуги.
	ТекущийКодУслугиВОСЗН = Элемент.ТекущиеДанные.КодУслугиВОСЗН;
	Если ЗначениеЗаполнено(ТекущийКодУслугиВОСЗН) Тогда
		ОтборКод = Новый Структура;
		ОтборКод.Вставить("КодУслугиВОСЗН", ТекущийКодУслугиВОСЗН);
		
		СтрокиСовпаденияКод = Объект.РостовскаяОбласть_ТаблицаНастроек.НайтиСтроки(ОтборКод);
		Если СтрокиСовпаденияКод.Количество() > 1 Тогда
			
			Сообщить("Код услуги в ОСЗН не уникален!");
			КодОшибки = КодОшибки + 1;
			
			Элемент.ТекущиеДанные.КодУслугиВОСЗН = ПредыдущийКодУслугиВОСЗН;
			
		КонецЕсли;
	Иначе
		Сообщить("Код услуги в ОСЗН не заполнен!");
		КодОшибки = КодОшибки + 1;
	КонецЕсли;
	
	// При наличии незаполненных полей или дубликатов опрашиваем пользователя.
	Если КодОшибки > 0 Тогда
		
		Сообщить("Добавление незаполненных полей или дублирующих значений недопустимо!");
		Отказ = Истина;
		
	КонецЕсли;
	
	// Разблокируем кнопку добавления табличного поля "ТП_ЖКУ_ВБазе".
	Если Не Объект.РостовскаяОбласть_ТаблицаНастроек.Количество() = 0 Тогда
		Элементы.ТП_ЖКУ_ВБазеДобавить.Доступность = Истина;
	КонецЕсли;
	
	// Найдем и изменим данные в таблице справочника услуг.
	Если НЕ Отказ Тогда
		
		СтрокиДляКоррекировки = Объект.РостовскаяОбласть_УслугиВБазе.НайтиСтроки(Новый Структура("Идентификатор", Элемент.ТекущиеДанные.НомерСтроки));
		Для Каждого Строка ИЗ СтрокиДляКоррекировки Цикл
			Строка.КодУслугиВОСЗН              = Элемент.ТекущиеДанные.КодУслугиВОСЗН;
			Строка.УслугаВОСЗН                 = Элемент.ТекущиеДанные.УслугаВОСЗН;
			Строка.СпособОпределенияНачислений = Элемент.ТекущиеДанные.СпособОпределенияНачислений;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ТаблицаНастроекПередОкончаниемРедактирования()

#КонецОбласти

//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ УСЛУГИ В БАЗЕ

#Область ТП_УслугиВБазе

&НаКлиенте
// Процедура - обработчик события "ПередНачаломДобавления" таблицы "УслугаВБазе".
//
Процедура ТП_ЖКУ_ВБазеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// Если не выбрана строка в левой колоке или добавление строк копированием.
	Если Элементы.РостовскаяОбласть_ТаблицаНастроек.ТекущиеДанные = Неопределено Или Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // ТП_ЖКУ_ВБазеПередНачаломДобавления()

&НаКлиенте
// Процедура - обработчик события "ПередУдалением" таблицы "УслугаВБазе".
//
Процедура ТП_ЖКУ_ВБазеПередУдалением(Элемент, Отказ)
	
	// При удалении строки удаляем соответствующие записи в РостовскаяОбласть_УслугиВБазе. 
	Идентификатор = Элементы.РостовскаяОбласть_ТаблицаНастроек.ТекущиеДанные.НомерСтроки;
	УслугаВБазе   = Элемент.ТекущиеДанные.УслугаВБазе;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Идентификатор", Идентификатор);
	Отбор.Вставить("УслугаВБазе",   УслугаВБазе);
	
	СтрокиУдаления = Объект.РостовскаяОбласть_УслугиВБазе.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаУдаления ИЗ СтрокиУдаления Цикл
		Объект.РостовскаяОбласть_УслугиВБазе.Удалить(СтрокаУдаления);
	КонецЦикла;
	
КонецПроцедуры // ТП_ЖКУ_ВБазеПередУдалением()

&НаКлиенте
// Процедура - обработчик события "ПриНачалеРедактирования" таблицы "УслугаВБазе".
//
Процедура ТП_ЖКУ_ВБазеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// Запоминаем предыдущее значение перезаписываемой строки.
	Если Не НоваяСтрока Тогда
		ПредыдущаяУслуга = Элемент.ТекущиеДанные.УслугаВБазе;
	КонецЕсли;
	
КонецПроцедуры // ТП_ЖКУ_ВБазеПриНачалеРедактирования()

&НаКлиенте
// Процедура - обработчик события "ПередОкончаниемРедактирования" таблицы "УслугаВБазе".
//
Процедура ТП_ЖКУ_ВБазеПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	// Если изменяем выбранное значение, то удаляем предыдущую запись.
	Если Не НоваяСтрока И ЗначениеЗаполнено(ПредыдущаяУслуга) Тогда
		
		Идентификатор = Элементы.РостовскаяОбласть_ТаблицаНастроек.ТекущиеДанные.НомерСтроки;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", Идентификатор);
		Отбор.Вставить("УслугаВБазе",   ПредыдущаяУслуга);
		
		НайденнаяСтрока = Объект.РостовскаяОбласть_УслугиВБазе.НайтиСтроки(Отбор);
		Объект.РостовскаяОбласть_УслугиВБазе.Удалить(НайденнаяСтрока[0]);
		
	КонецЕсли;
	
КонецПроцедуры // ТП_ЖКУ_ВБазеПередОкончаниемРедактирования()

&НаКлиенте
// Процедура - обработчик события "ПриОкончанииРедактирования" таблицы "УслугаВБазе".
//
Процедура ТП_ЖКУ_ВБазеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Не Элемент.ТекущиеДанные = Неопределено И Не ОтменаРедактирования Тогда
		
		Если Элемент.ТекущиеДанные.УслугаВБазе.Пустая() Тогда
			// Удаляем пустую строку.
			ТП_ЖКУ_ВБазе.Удалить(ТП_ЖКУ_ВБазе.Индекс(Элемент.ТекущиеДанные)); 
		Иначе
			// Добавляем запись в РостовскаяОбласть_УслугиВБазе.
			Идентификатор               = Элементы.РостовскаяОбласть_ТаблицаНастроек.ТекущиеДанные.НомерСтроки;
			КодУслугиВОСЗН              = Элементы.РостовскаяОбласть_ТаблицаНастроек.ТекущиеДанные.КодУслугиВОСЗН;
			УслугаВОСЗН                 = Элементы.РостовскаяОбласть_ТаблицаНастроек.ТекущиеДанные.УслугаВОСЗН;
			СпособОпределенияНачислений = Элементы.РостовскаяОбласть_ТаблицаНастроек.ТекущиеДанные.СпособОпределенияНачислений;
			УслугаВБазе                 = Элемент.ТекущиеДанные.УслугаВБазе;
			
			
			НоваяСтрокаСоответствия = Объект.РостовскаяОбласть_УслугиВБазе.Добавить();
			
			НоваяСтрокаСоответствия.УслугаВБазе                 = УслугаВБазе;
			НоваяСтрокаСоответствия.Идентификатор               = Идентификатор;
			НоваяСтрокаСоответствия.КодУслугиВОСЗН              = КодУслугиВОСЗН;
			НоваяСтрокаСоответствия.УслугаВОСЗН                 = УслугаВОСЗН;
			НоваяСтрокаСоответствия.СпособОпределенияНачислений = СпособОпределенияНачислений;
			
			// Удаляем старую запись при перевыборе.
			Если Не ПредыдущаяУслуга = УслугаВБазе Тогда
				
				Отбор = Новый Структура;
				Отбор.Вставить("Идентификатор", Идентификатор);
				Отбор.Вставить("УслугаВБазе",   ПредыдущаяУслуга);
				
				НайденнаяСтрока = Объект.РостовскаяОбласть_УслугиВБазе.НайтиСтроки(Отбор);
				Если НайденнаяСтрока.Количество() = 1 Тогда
					НайденнаяСтрока[0].УслугаВБазе = УслугаВБазе;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ТП_ЖКУ_ВБазеПриОкончанииРедактирования()

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля ввода "УслугаВБазе" таблицы "УслугаВБазе".
//
Процедура ТП_ЖКУ_ВБазеУслугаВБазеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Устанавливаем отбор на справочник "Услуги".
	СтандартнаяОбработка = Ложь;
	
	// Список уже выбранных услуг по данной услуге в ОСЗН.
	СписокВыбранныхУслуг = Новый СписокЗначений;
	Для Каждого Строка Из Объект.РостовскаяОбласть_УслугиВБазе Цикл
		СписокВыбранныхУслуг.Добавить(Строка.УслугаВБазе);
	КонецЦикла;
	
	Форма = ПолучитьФорму("Справочник.КВП_Услуги.ФормаВыбора", Новый Структура("ТекущаяСтрока", Элементы.ТП_ЖКУ_ВБазе.ТекущиеДанные.УслугаВБазе), Элемент);
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.УстановитьЭлементОтбора(Форма.Список.КомпоновщикНастроек.Настройки.Отбор, "Ссылка", СписокВыбранныхУслуг,
															ВидСравненияКомпоновкиДанных.НеВСписке, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	
	Форма.Открыть();
	
КонецПроцедуры // ТП_ЖКУ_ВБазеУслугаВБазеНачалоВыбора()

#КонецОбласти