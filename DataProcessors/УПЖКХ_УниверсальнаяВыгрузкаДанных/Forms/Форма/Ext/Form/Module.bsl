
////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОБРАБОТЧИКИ_СОБЫТИЙ_ФОРМЫ

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураЗначенийПолейФорматаВыгрузки  = Справочники.КВП_НастройкиЗагрузкиВыгрузки.ПодговитьСтруктуруЗначенийПолей();
	СтруктураЗначенийПолейНастроекВыгрузки = Справочники.УПЖКХ_НастройкиУниверсальнойВыгрузкиДанных.ПодговитьСтруктуруЗначенийПолей();
	
	Объект.Период = ТекущаяДата();
	
	СтруктураПараметров = ХранилищеОбщихНастроек.Загрузить("УПЖКХ_УниверсальнаяВыгрузкаДанных");
	
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		
		Если СтруктураПараметров.Свойство("Организация") Тогда
			СтруктураПараметров.Свойство("Организация", Объект.Организация);
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ФорматВыгрузки") Тогда
			СтруктураПараметров.Свойство("ФорматВыгрузки", Объект.ФорматВыгрузки);
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("ИмяФайлаРеестра") Тогда
			СтруктураПараметров.Свойство("ИмяФайлаРеестра", Объект.ИмяФайлаРеестра);
		КонецЕсли;
		
		Если СтруктураПараметров.Свойство("НастройкиВыгрузки") Тогда
			СтруктураПараметров.Свойство("НастройкиВыгрузки", Объект.НастройкиВыгрузки);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.Организация.Пустая() Тогда
		Объект.Организация = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	// ОбщиеМеханизмыИКоманды
	ОТР_ПодключаемыеОбщиеМеханизмыИКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ОбщиеМеханизмыИКоманды
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриОткрытии" формы.
Процедура ПриОткрытии(Отказ)
	
	ПериодСтрока = УПЖКХ_РаботаСДиалогамиКлиентСервер.ДатаКакМесяцПредставление(Объект.Период);
	
	СформироватьИмяФайла();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриЗакрытии" формы.
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СохранитьПараметрыНаСервере();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ОбработкаОповещения" формы.
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновленыНастройкиЗагрузкиВыгрузки"
	   И Источник = ЭтаФорма
	   И Параметр.Свойство("Ключ") Тогда
		Объект.ФорматВыгрузки = Параметр.Ключ;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД ФОРМЫ

#Область ОБРАБОТЧИКИ_СОБЫТИЙ_КОМАНД_ФОРМЫ

&НаКлиенте
// Процедура-обработчик команды "Выгрузить" формы.
Процедура Выгрузить(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнение() Тогда
		
		ФайлВыгрузки         = Новый Файл(Объект.ИмяФайлаРеестра);
		КаталогФайлаВыгрузки = Новый Файл(ФайлВыгрузки.Путь);
		
		КаталогФайлаВыгрузки.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ВыгрузитьПродолжение", ЭтаФорма));
		
	КонецЕсли;
	
КонецПроцедуры

// ЧастоЗадаваемыеВопросы
&НаКлиенте
//Подключаемый обработчик команды перехода к часто задаваемым вопросам.
Процедура Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда)
	
	ОТР_ЧастоЗадаваемыеВопросыКлиент.Подключаемый_ЧастоЗадаваемыеВопросыОткрытьСсылку(Команда);
	
КонецПроцедуры
// Конец ЧастоЗадаваемыеВопросы

#КонецОбласти

////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОБРАБОТЧИКИ_СОБЫТИЙ_ЭЛЕМЕНТОВ_ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "АвтоПодбор" реквизита "ПериодСтрока".
//
Процедура ПериодСтрокаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	КВП_РаботаСДиалогами.ПериодРегистрацииАвтоПодборТекста(ЭтаФорма, "Объект.Период", "ПериодСтрока", Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ОкончаниеВводаТекста" реквизита "ПериодСтрока".
//
Процедура ПериодСтрокаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	КВП_РаботаСДиалогами.ПериодРегистрацииОкончаниеВводаТекста(ЭтаФорма, "Объект.Период", "ПериодСтрока", Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" реквизита "ПериодСтрока".
//
Процедура ПериодСтрокаПриИзменении(Элемент)
	
	КВП_РаботаСДиалогами.ПериодРегистрацииПриИзменении(ЭтаФорма, "Объект.Период", "ПериодСтрока", Элемент);
	
	СформироватьИмяФайла();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "Регулирование" реквизита "ПериодСтрока".
//
Процедура ПериодСтрокаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	КВП_РаботаСДиалогами.ПериодРегистрацииРегулирование(ЭтаФорма, "Объект.Период", "ПериодСтрока", Элемент, Направление, СтандартнаяОбработка);
	
	СформироватьИмяФайла();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "Очистка" реквизита "ПериодСтрока".
//
Процедура ПериодСтрокаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" реквизита "ПериодСтрока".
//
Процедура ПериодСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	КВП_РаботаСДиалогами.ПериодРегистрацииНачалоВыбора(ЭтаФорма, "Объект.Период", "ПериодСтрока", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
// Процедура-обработчик события "НачалоВыбора" поля "ИмяФайлаРеестра".
//
Процедура ИмяФайлаРеестраНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Период = Дата(1, 1, 1) Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Укажите период.");
		Возврат;
	КонецЕсли;
	
	Если Объект.ФорматВыгрузки.Пустая() Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Укажите настройку выгрузки.");
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Заголовок               = "Выберите каталог";
	ДиалогВыбораФайла.МножественныйВыбор      = Ложь;
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ПолноеИмяФайла          = Строка(Объект.ФорматВыгрузки) + " " + Формат(Объект.Период, "ДФ=ММ") + Формат(Объект.Период, "ДФ=гггг");
	
	ФорматФайла = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Объект.ФорматВыгрузки, "ФорматФайла");
	
	Если ФорматФайла = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла[0].Значение Тогда // "TXT"
		
		ДиалогВыбораФайла.Фильтр = "Формат выгрузки(*.txt)|*.txt";
		
	ИначеЕсли ФорматФайла = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла[1].Значение Тогда // "XLS"
		
		ДиалогВыбораФайла.Фильтр = "Формат выгрузки(*.xls)|*.xls";
		
	ИначеЕсли ФорматФайла = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла[4].Значение Тогда // "DBF"
		
		ДиалогВыбораФайла.Фильтр = "Формат выгрузки(*.dbf)|*.dbf";
		
	КонецЕсли;
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ИмяФайлаРеестраНачалоВыбораЗавершение", ЭтаФорма, Новый Структура("ДиалогВыбораФайла", ДиалогВыбораФайла)));
	
КонецПроцедуры

&НаКлиенте
// Процедура-обработчик события "Открытие" поля "ИмяФайлаРеестра".
//
Процедура ИмяФайлаРеестраОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяФайлаРеестра      = ПолучитьИмяФайлаРеестра();
	
	Если НЕ ПустаяСтрока(ИмяФайлаРеестра) Тогда
		
		ФайлРеестра         = Новый Файл(ИмяФайлаРеестра);
		ПараметрыОповещения = Новый Структура("ИмяФайлаРеестра", ИмяФайлаРеестра);
		
		ФайлРеестра.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ИмяФайлаРеестраОткрытиеЗавершение", ЭтаФорма, ПараметрыОповещения));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура-обработчик события "ПриИзменении" поля "Организация".
//
Процедура ОрганизацияПриИзменении(Элемент)
	
	СформироватьИмяФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматВыгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения   = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСправочникаНастройкиЗагрузкиВыгрузки", ЭтаФорма);
	СтруктураПараметров  = Новый Структура("Ключ, ТипОбъектаОткрытияФормы", Объект.ФорматВыгрузки, ТипЗнч(Объект));
	
	ОткрытьФорму("Справочник.КВП_НастройкиЗагрузкиВыгрузки.Форма.ФормаЭлемента", СтруктураПараметров, ЭтаФорма,,,, ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик создания нового элемента справочника "Настройки загрузки / выгрузки".
//
Процедура ФорматВыгрузкиСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения   = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСправочникаНастройкиЗагрузкиВыгрузки", ЭтаФорма);
	ЗначенияЗаполнения   = Новый Структура("ВидОперации", ПредопределенноеЗначение("Перечисление.КВП_ВидыНастроекЗагрузкиИзАС.УниверсальнаяВыгрузкаДанных"));
	
	СтруктураПараметров  = Новый Структура;
	СтруктураПараметров.Вставить("Ключ",                    Неопределено);
	СтруктураПараметров.Вставить("ЗначенияЗаполнения",      ЗначенияЗаполнения);
	СтруктураПараметров.Вставить("ТипОбъектаОткрытияФормы", ТипЗнч(Объект));
	
	ОткрытьФорму("Справочник.КВП_НастройкиЗагрузкиВыгрузки.Форма.ФормаЭлемента", СтруктураПараметров, ЭтаФорма,,,, ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "Показать" объекта "ДиалогВыбораФайла".
Процедура ИмяФайлаРеестраНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		Объект.ИмяФайлаРеестра = ДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "НачатьПроверкуСуществования".
Процедура ИмяФайлаРеестраОткрытиеЗавершение(Существует, ДополнительныеПараметры) Экспорт
	
	ИмяФайлаРеестра = ДополнительныеПараметры.ИмяФайлаРеестра;
	
	Если Существует Тогда
		
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ИмяФайлаРеестраОткрытиеЗавершениеЗавершение", ЭтаФорма), ИмяФайлаРеестра);
		
	Иначе
		
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Файл реестра не существует.");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Обработчик события "НачатьЗапускПриложения".
Процедура ИмяФайлаРеестраОткрытиеЗавершениеЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

&НаКлиенте
// Процедура-обработчик события "ПриИзменении" поля "ФорматВыгрузки".
//
Процедура ФорматВыгрузкиПриИзменении(Элемент)
	
	СформироватьИмяФайла();
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ВСПОМОГАТЕЛЬНЫЕ_ПРОЦЕДУРЫ_И_ФУНКЦИИ

&НаСервере
// Процедура сохраняет параметры выгрузки.
Процедура СохранитьПараметрыНаСервере()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",       Объект.Организация);
	СтруктураПараметров.Вставить("ФорматВыгрузки",    Объект.ФорматВыгрузки);
	СтруктураПараметров.Вставить("ИмяФайлаРеестра",   Объект.ИмяФайлаРеестра);
	СтруктураПараметров.Вставить("НастройкиВыгрузки", Объект.НастройкиВыгрузки);
	
	ХранилищеОбщихНастроек.Сохранить("УПЖКХ_УниверсальнаяВыгрузкаДанных", , СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
// Процедура формирует имя файла рестра.
Процедура СформироватьИмяФайла()
	
	ПапкаСохранения = "";
	ФорматФайла     = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Объект.ФорматВыгрузки, "ФорматФайла");
	
	Если НЕ ПустаяСтрока(Объект.ИмяФайлаРеестра) Тогда
		
		Файл              = Новый Файл(Объект.ИмяФайлаРеестра);
		Папка             = Новый Файл(Файл.Путь);
		ПараметрыОписания = Новый Структура("Папка, ФорматФайла", Файл.Путь, ФорматФайла);
		
		Папка.НачатьПроверкуСуществования(Новый ОписаниеОповещения("СформироватьИмяФайлаЗавершение", ЭтаФорма, ПараметрыОписания));
		Возврат;
		
	КонецЕсли;
	
	СформироватьИмяФайлаФрагмент(ПапкаСохранения, ФорматФайла);
	
КонецПроцедуры

&НаСервереБезКонтекста
// Возвращает таблицу лицевых счетов.
//
// Параметры:
//  Запрос - результат запроса менеджера временных таблиц.
//
Функция ПолучитьТаблицуЛицевыхСчетов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Здание,
	|	КодПомещения";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИмяКолонки(ИмяПараметра, Префикс, Постфикс, ПараметрыВыгрузки, ДополнительноеИмяПараметра = "")
	
	ИмяКолонки = ИмяПараметра;
	
	Если ПараметрыВыгрузки.ЭтоDBF Тогда
		ИмяКолонки = ДополнительноеИмяПараметра;
	Иначе
		ИмяКолонки = Префикс + ИмяКолонки;
	КонецЕсли;
	
	ИмяКолонки = ИмяКолонки + Постфикс;
	
	Возврат ИмяКолонки;
	
КонецФункции

&НаСервереБезКонтекста
// Возвращает таблицу долгов лицевых счетов на конец периода.
//
// Параметры:
//  Запрос	 - результат запроса менеджера временных таблиц.
// 
// Возвращаемое значение:
//   Таблица значений.
//
Функция ПолучитьТаблицуДолговПоЛицевымСчетам(Запрос, ПараметрыВыгрузки)
	
	НайденнаяСтрокаПараметраСуммаДолга = ПараметрыВыгрузки.ТаблицаПараметров.Найти(Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.СуммаДолга, "Параметр");
	Если НЕ (НайденнаяСтрокаПараметраСуммаДолга <> Неопределено
	 ИЛИ ПараметрыВыгрузки.ТипОтбораПоСуммеДолга <> 0
	 ИЛИ (ПараметрыВыгрузки.ЭтоTXT И ПараметрыВыгрузки.ВыводитьЗаголовок)
	 ИЛИ ПараметрыВыгрузки.ВыгружатьСведенияПоУслугам) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НайденнаяСтрокаПараметраСуммаДолга = Неопределено Тогда
		ТипПериодаДолга = ПараметрыВыгрузки.СтруктураЗначенийПолейФорматаВыгрузки.СпТипПериодаДолга[1].Значение// 1; // поиск долга на конец периода.
	Иначе
		ТипПериодаДолга = НайденнаяСтрокаПараметраСуммаДолга.ТипПериодаДолга; // поиск долга на выбранный период в настройках выгрузки.
	КонецЕсли;
	
	ТипОтбораПоСуммеДолга = ПараметрыВыгрузки.ТипОтбораПоСуммеДолга;
	МинимальнаяСуммаДолга = ПараметрыВыгрузки.МинимальнаяСуммаДолга;
	ВыбранныеУслуги       = ПараметрыВыгрузки.ВыбранныеУслуги;
	ВыборУслуг            = ПараметрыВыгрузки.ВыборУслуг;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КВП_ВзаиморасчетыПоЛицевымСчетамОстатки.ЛицевойСчет КАК ЛицевойСчет,
	|	КВП_ВзаиморасчетыПоЛицевымСчетамОстатки.Услуга КАК Услуга,
	|	СУММА(КВП_ВзаиморасчетыПоЛицевымСчетамОстатки.СуммаНачисленияОстаток) КАК СуммаДолга
	|ПОМЕСТИТЬ втОстаткиПоЛицевымСчетамИУслугам
	|ИЗ
	|	РегистрНакопления.КВП_ВзаиморасчетыПоЛицевымСчетам.Остатки(
	|			&ПериодПоТипу,
	|			Организация = &Организация
	|				И ЛицевойСчет В
	|					(ВЫБРАТЬ
	|						врТаблицаЛицевыеСчета.ЛицевойСчет
	|					ИЗ
	|						врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)
	|				И &УсловиеПоУслугам) КАК КВП_ВзаиморасчетыПоЛицевымСчетамОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	КВП_ВзаиморасчетыПоЛицевымСчетамОстатки.ЛицевойСчет,
	|	КВП_ВзаиморасчетыПоЛицевымСчетамОстатки.Услуга
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиПоЛицевымСчетамИУслугам.ЛицевойСчет КАК ЛицевойСчет,
	|	СУММА(втОстаткиПоЛицевымСчетамИУслугам.СуммаДолга) КАК СуммаДолга
	|ПОМЕСТИТЬ втОстаткиПоЛицевымСчетам
	|ИЗ
	|	втОстаткиПоЛицевымСчетамИУслугам КАК втОстаткиПоЛицевымСчетамИУслугам
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстаткиПоЛицевымСчетамИУслугам.ЛицевойСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет,
	|	ЕСТЬNULL(втОстаткиПоЛицевымСчетам.СуммаДолга, 0) КАК СуммаДолга
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоЛицевымСчетам КАК втОстаткиПоЛицевымСчетам
	|		ПО врТаблицаЛицевыеСчета.ЛицевойСчет = втОстаткиПоЛицевымСчетам.ЛицевойСчет
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ТипОтбораПоСуммеДолга = 1
	|				ТОГДА ЕСТЬNULL(втОстаткиПоЛицевымСчетам.СуммаДолга, 0) >= &МинимальнаяСуммаДолга
	|			КОГДА &ТипОтбораПоСуммеДолга = 2
	|				ТОГДА ЕСТЬNULL(втОстаткиПоЛицевымСчетам.СуммаДолга, 0) > 0
	|			КОГДА &ТипОтбораПоСуммеДолга = 3
	|				ТОГДА ЕСТЬNULL(втОстаткиПоЛицевымСчетам.СуммаДолга, 0) < 0
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОстаткиПоЛицевымСчетам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.ЛицевойСчет КАК ЛицевойСчет,
	|	втОстатки.СуммаДолга КАК СуммаДолга
	|ИЗ
	|	втОстатки КАК втОстатки";
	
	// Значения параметра "ТипОтбораПоСуммеДолга":
	// 0 - нет отбора по сумме долга.
	// 1 - выгружаются лицевые счета с суммой долга, больший или равный Минимальной сумме долга.
	// 2 - выгружаются лицевые счета только с "положительным" долгом.
	// 3 - выгружаются лицевые счета только с "отрицательным" долгом (переплатами).
	
	Запрос.УстановитьПараметр("ТипОтбораПоСуммеДолга", ТипОтбораПоСуммеДолга);
	Запрос.УстановитьПараметр("МинимальнаяСуммаДолга", МинимальнаяСуммаДолга);
	
	Если ТипПериодаДолга = ПараметрыВыгрузки.СтруктураЗначенийПолейФорматаВыгрузки.СпТипПериодаДолга[0].Значение Тогда // 0
		Период = Запрос.Параметры.НачалоПериода;
	Иначе
		Период = Запрос.Параметры.КонецПериода + 1;
	КонецЕсли;
	Запрос.УстановитьПараметр("ПериодПоТипу", Период);
	
	//Параметр "ВыборУслуг" принимает следующие значения:
	//	0	- "Всем услугам";
	//	1	- "Выбранным услугам";
	//	2	- "Всем услугам, кроме выбранных".
	
	Если ВыборУслуг = 0 Тогда // Все услуги.
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("СписокУслуг", ВыбранныеУслуги);
		Если ВыборУслуг = 1 Тогда // По выбранным услугам.
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "Услуга В (&СписокУслуг)");
		ИначеЕсли ВыборУслуг = 2 Тогда // Все услуги, кроме выбранных.
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "НЕ Услуга В (&СписокУслуг)");
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаДолгов = Запрос.Выполнить().Выгрузить();
	
	// Скорректируем временную таблицу лицевых счетов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ врТаблицаЛицевыеСчетаСОтборомПоСуммеДолга
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|ГДЕ
	|	ВЫБОР
	|		КОГДА &ТипОтбораПоСуммеДолга <> 0
	|			ТОГДА врТаблицаЛицевыеСчета.ЛицевойСчет В
	|				(ВЫБРАТЬ
	|					втОстатки.ЛицевойСчет
	|				ИЗ
	|					втОстатки КАК втОстатки)
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаЛицевыеСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ врТаблицаЛицевыеСчета
	|ИЗ
	|	врТаблицаЛицевыеСчетаСОтборомПоСуммеДолга КАК врТаблицаЛицевыеСчетаСОтборомПоСуммеДолга
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаЛицевыеСчетаСОтборомПоСуммеДолга;";
	
	Запрос.Выполнить();
	
	Возврат ТаблицаДолгов;
	
КонецФункции

&НаСервереБезКонтекста
// Возвращает таблицу площадей лицевых счетов.
//
// Параметры:
//  Запрос						 - результат запроса менеджера временных таблиц.
//  УчитыватьДолюЛицевогоСчета	 - Булево - параметр определяет: учитывать долю лицевого счета или нет.
// 
// Возвращаемое значение:
//   Таблица значений.
//
Функция ПолучитьТаблицуПлощадей(Запрос, ПараметрыВыгрузки)
	
	НайденнаяСтрокаПараметраПлощадь = ПараметрыВыгрузки.ТаблицаПараметров.Найти(Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Площадь, "Параметр");
	Если НайденнаяСтрокаПараметраПлощадь = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидПлощади", НайденнаяСтрокаПараметраПлощадь.ВидПлощади);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КВП_ПлощадьПомещенияСрезПоследних.Объект КАК Помещение,
	|	КВП_ПлощадьПомещенияСрезПоследних.Площадь
	|ПОМЕСТИТЬ втПлощадиПомещений
	|ИЗ
	|	РегистрСведений.КВП_ПлощадьПомещения.СрезПоследних(
	|			&КонецПериода,
	|			Объект В
	|					(ВЫБРАТЬ
	|						врТаблицаЛицевыеСчета.Помещение
	|					ИЗ
	|						врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)
	|				И ВидПлощади = &ВидПлощади) КАК КВП_ПлощадьПомещенияСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаЛицевыеСчета.ЛицевойСчет,
	|	врТаблицаЛицевыеСчета.Помещение,
	|	ЕСТЬNULL(втПлощадиПомещений.Площадь, 0) КАК ПлощадьПомещения
	|ПОМЕСТИТЬ втПлощадиПомещенийПоЛицевымСчетам
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПлощадиПомещений КАК втПлощадиПомещений
	|		ПО врТаблицаЛицевыеСчета.Помещение = втПлощадиПомещений.Помещение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втПлощадиПомещений";
	
	Если НайденнаяСтрокаПараметраПлощадь.УчитыватьДолюЛицевогоСчета Тогда
		Запрос.Текст = Запрос.Текст +
		"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КВП_РазмерыДолейЛицевыхСчетовСрезПоследних.ЛицевойСчет,
		|	КВП_РазмерыДолейЛицевыхСчетовСрезПоследних.Объект КАК Помещение,
		|	ВЫБОР
		|		КОГДА КВП_РазмерыДолейЛицевыхСчетовСрезПоследних.ДоляЗнаменатель = 0
		|			ТОГДА 0
		|		ИНАЧЕ КВП_РазмерыДолейЛицевыхСчетовСрезПоследних.ДоляЧислитель / КВП_РазмерыДолейЛицевыхСчетовСрезПоследних.ДоляЗнаменатель
		|	КОНЕЦ КАК РазмерДоли
		|ПОМЕСТИТЬ втРазмерыДолейЛицевыхСчетов
		|ИЗ
		|	РегистрСведений.КВП_РазмерыДолейЛицевыхСчетов.СрезПоследних(
		|			&КонецПериода,
		|			ЛицевойСчет В
		|				(ВЫБРАТЬ
		|					врТаблицаЛицевыеСчета.ЛицевойСчет
		|				ИЗ
		|					врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)) КАК КВП_РазмерыДолейЛицевыхСчетовСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПлощадиПомещенийПоЛицевымСчетам.ЛицевойСчет,
		|	ВЫБОР
		|		КОГДА втРазмерыДолейЛицевыхСчетов.РазмерДоли ЕСТЬ NULL 
		|			ТОГДА втПлощадиПомещенийПоЛицевымСчетам.ПлощадьПомещения
		|		ИНАЧЕ втПлощадиПомещенийПоЛицевымСчетам.ПлощадьПомещения * ЕСТЬNULL(втРазмерыДолейЛицевыхСчетов.РазмерДоли, 0)
		|	КОНЕЦ КАК Площадь
		|ИЗ
		|	втПлощадиПомещенийПоЛицевымСчетам КАК втПлощадиПомещенийПоЛицевымСчетам
		|		ЛЕВОЕ СОЕДИНЕНИЕ втРазмерыДолейЛицевыхСчетов КАК втРазмерыДолейЛицевыхСчетов
		|		ПО втПлощадиПомещенийПоЛицевымСчетам.ЛицевойСчет = втРазмерыДолейЛицевыхСчетов.ЛицевойСчет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втРазмерыДолейЛицевыхСчетов";
	Иначе
		Запрос.Текст = Запрос.Текст +
		"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПлощадиПомещенийПоЛицевымСчетам.ЛицевойСчет,
		|	втПлощадиПомещенийПоЛицевымСчетам.ПлощадьПомещения КАК Площадь
		|ИЗ
		|	втПлощадиПомещенийПоЛицевымСчетам КАК втПлощадиПомещенийПоЛицевымСчетам";
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
// Возвращает таблицу контактных данных лицевых счетов.
//
// Параметры:
//  Запрос	 - результат запроса менеджера временных таблиц.
// 
// Возвращаемое значение:
//   Таблица значений.
//
Функция ПолучитьТаблицуКонтактныхДанных(Запрос, ПараметрыВыгрузки)
	
	НайденнаяСтрокаПараметраНомерТелефона = ПараметрыВыгрузки.ТаблицаПараметров.Найти(Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.НомерТелефона, "Параметр");
	
	Если НайденнаяСтрокаПараметраНомерТелефона = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыгружатьТолькоСНомерамиТелефонов = ПараметрыВыгрузки.ВыгружатьТолькоСНомерамиТелефонов;
	
	Запрос.УстановитьПараметр("ВыгружатьТолькоСНомерамиТелефонов", ВыгружатьТолькоСНомерамиТелефонов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТелефоновЖильцов.Жилец КАК Жилец,
	|	ТаблицаТелефоновЖильцов.СписокТелефонов КАК СписокТелефонов
	|ПОМЕСТИТЬ втТелефоныЖильцов
	|ИЗ
	|	&ТаблицаТелефоновЖильцов КАК ТаблицаТелефоновЖильцов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Ссылка КАК ОтветственныйВладелец,
	|	КонтактнаяИнформация.Представление КАК НомерТелефонаКонтрагента
	|ПОМЕСТИТЬ врТаблицаКонтактнаяИнформацияКонтрагента
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				врТаблицаЛицевыеСчета.ОтветственныйВладелец
	|			ИЗ
	|				врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента)
	|	И ВЫБОР
	|			КОГДА &ВыгружатьТолькоСНомерамиТелефонов
	|				ТОГДА КонтактнаяИнформация.Представление <> """"
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет,
	|	врТаблицаЛицевыеСчета.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	врТаблицаЛицевыеСчета.ОтветственныйВладелецПредставление КАК ОтветственныйВладелецПредставление,
	|	ВЫБОР
	|		КОГДА врТаблицаЛицевыеСчета.НомерТелефонаЛС <> """"
	|			ТОГДА врТаблицаЛицевыеСчета.НомерТелефонаЛС
	|		КОГДА врТаблицаЛицевыеСчета.ОтветственныйВладелец ССЫЛКА Справочник.УПЖКХ_Жильцы
	|			ТОГДА ЕСТЬNULL(втТелефоныЖильцов.СписокТелефонов, """")
	|		КОГДА врТаблицаЛицевыеСчета.ОтветственныйВладелец ССЫЛКА Справочник.Контрагенты
	|			ТОГДА ЕСТЬNULL(врТаблицаКонтактнаяИнформацияКонтрагента.НомерТелефонаКонтрагента, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерТелефона
	|ПОМЕСТИТЬ врТаблицаНомераТелефонов
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ врТаблицаКонтактнаяИнформацияКонтрагента КАК врТаблицаКонтактнаяИнформацияКонтрагента
	|		ПО врТаблицаЛицевыеСчета.ОтветственныйВладелец = врТаблицаКонтактнаяИнформацияКонтрагента.ОтветственныйВладелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТелефоныЖильцов КАК втТелефоныЖильцов
	|		ПО врТаблицаЛицевыеСчета.ОтветственныйВладелец = втТелефоныЖильцов.Жилец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаНомераТелефонов.ЛицевойСчет КАК ЛицевойСчет,
	|	врТаблицаНомераТелефонов.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	врТаблицаНомераТелефонов.ОтветственныйВладелецПредставление КАК ОтветственныйВладелецПредставление,
	|	врТаблицаНомераТелефонов.НомерТелефона КАК НомерТелефона
	|ПОМЕСТИТЬ ТаблицаКонтактныеДанные
	|ИЗ
	|	врТаблицаНомераТелефонов КАК врТаблицаНомераТелефонов
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ВыгружатьТолькоСНомерамиТелефонов
	|				ТОГДА врТаблицаНомераТелефонов.НомерТелефона <> """"
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаНомераТелефонов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаКонтактнаяИнформацияКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКонтактныеДанные.ЛицевойСчет КАК ЛицевойСчет,
	|	ТаблицаКонтактныеДанные.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	ТаблицаКонтактныеДанные.ОтветственныйВладелецПредставление КАК ОтветственныйВладелецПредставление,
	|	ТаблицаКонтактныеДанные.НомерТелефона КАК НомерТелефона
	|ИЗ
	|	ТаблицаКонтактныеДанные КАК ТаблицаКонтактныеДанные";
	
	ТаблицаКонтактныеДанные = Запрос.Выполнить().Выгрузить();
	
	Если ВыгружатьТолькоСНомерамиТелефонов Тогда // обновляем таблицу лицевых счетов.
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ врТаблицаЛицевыеСчетаСОтбором
		|ИЗ
		|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКонтактныеДанные КАК ТаблицаКонтактныеДанные
		|		ПО врТаблицаЛицевыеСчета.ЛицевойСчет = ТаблицаКонтактныеДанные.ЛицевойСчет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ врТаблицаЛицевыеСчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ врТаблицаЛицевыеСчета
		|ИЗ
		|	врТаблицаЛицевыеСчетаСОтбором КАК врТаблицаЛицевыеСчетаСОтбором";
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Возврат ТаблицаКонтактныеДанные;
	
КонецФункции

&НаСервереБезКонтекста
// Возвращает таблицу начислений по лицевым счетам.
//
// Параметры:
//  Запрос	 - результат запроса менеджера временных таблиц.
// 
// Возвращаемое значение:
//   Таблица значений.
//
Функция ПолучитьТаблицуНачислений(Запрос, ПараметрыВыгрузки)
	
	НайденнаяСтрокаСуммаНачислений = ПараметрыВыгрузки.ТаблицаПараметров.Найти(Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.СуммаНачислений, "Параметр");
	
	Если НЕ (НайденнаяСтрокаСуммаНачислений <> Неопределено
	 ИЛИ (ПараметрыВыгрузки.ЭтоTXT И ПараметрыВыгрузки.ВыводитьЗаголовок))Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборУслуг      = ПараметрыВыгрузки.ВыборУслуг;
	ВыбранныеУслуги = ПараметрыВыгрузки.ВыбранныеУслуги;
	
	Если НайденнаяСтрокаСуммаНачислений = Неопределено Тогда
		УчитыватьЛьготыДляСуммыНачислений = Истина;
	Иначе
		УчитыватьЛьготыДляСуммыНачислений = НайденнаяСтрокаСуммаНачислений.УчитыватьЛьготы;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_НачисленияОбороты.ЛицевойСчет КАК ЛицевойСчет,
	|	УПЖКХ_НачисленияОбороты.СуммаНачисленияОборот КАК СуммаНачисления
	|ПОМЕСТИТЬ Начисление
	|ИЗ
	|	РегистрНакопления.УПЖКХ_Начисления.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И ЛицевойСчет В
	|					(ВЫБРАТЬ
	|						врТаблицаЛицевыеСчета.ЛицевойСчет
	|					ИЗ
	|						врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)
	|				И НЕ РазделУчета В (&ИсключаемыеРазделыУчета)
	|				И НЕ ВидНачисления В (ЗНАЧЕНИЕ(Перечисление.УПЖКХ_ВидыНачислений.Оплата), ЗНАЧЕНИЕ(Перечисление.УПЖКХ_ВидыНачислений.ЗачетДолговИПереплат))
	|				И &УсловиеПоУслугам) КАК УПЖКХ_НачисленияОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УПЖКХ_НачисленияПоДобровольномуСтрахованиюОбороты.ЛицевойСчет,
	|	УПЖКХ_НачисленияПоДобровольномуСтрахованиюОбороты.СуммаНачисленияОборот
	|ИЗ
	|	РегистрНакопления.УПЖКХ_НачисленияПоДобровольномуСтрахованию.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И ЛицевойСчет В
	|					(ВЫБРАТЬ
	|						врТаблицаЛицевыеСчета.ЛицевойСчет
	|					ИЗ
	|						врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)) КАК УПЖКХ_НачисленияПоДобровольномуСтрахованиюОбороты
	|ГДЕ
	|	НЕ УПЖКХ_НачисленияПоДобровольномуСтрахованиюОбороты.Регистратор ССЫЛКА Документ.КВП_РегистрацияОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисление.ЛицевойСчет КАК ЛицевойСчет,
	|	СУММА(Начисление.СуммаНачисления) КАК СуммаНачисления
	|ИЗ
	|	Начисление КАК Начисление
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисление.ЛицевойСчет";
	
	ИсключаемыеРазделыУчета = Новый СписокЗначений;
	ИсключаемыеРазделыУчета.Добавить(Перечисления.УПЖКХ_РазделыУчета.ДобровольноеСтрахование);
	Если НЕ УчитыватьЛьготыДляСуммыНачислений Тогда
		ИсключаемыеРазделыУчета.Добавить(Перечисления.УПЖКХ_РазделыУчета.Льготы);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИсключаемыеРазделыУчета", ИсключаемыеРазделыУчета);
	
	//Параметр "ВыборУслуг" принимает следующие значения:
	//	0	- "Всем услугам";
	//	1	- "Выбранным услугам";
	//	2	- "Всем услугам, кроме выбранных".
	
	Если ВыборУслуг = 0 Тогда // Все услуги.
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "ИСТИНА");
		
	Иначе
		
		Запрос.УстановитьПараметр("СписокУслуг", ВыбранныеУслуги);
		
		Если ВыборУслуг = 1 Тогда
			
			// По выбранным услугам.
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "Услуга В (&СписокУслуг)");
			
		ИначеЕсли ВыборУслуг = 2 Тогда
			
			// Все услуги, кроме выбранных.
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "НЕ Услуга В (&СписокУслуг)");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
// Функция формирует файл и заполняет его данными.
//
// Параметры:
//  ПараметрыВыгрузки	 - Структура	 - настройки выгрузки.
//  ТаблицаДанных		 - ТаблицаЗначений	 - таблица данных.
// 
// Возвращаемое значение:
//  ТекстовыйДокумент, ТабличныйДокумент, XBase.
//
Функция ПолучитьФайлРеестра(ПараметрыВыгрузки, ТаблицаДанных)
	
	ФайлРеестра = Неопределено;
	
	ФорматФайла                             = ПараметрыВыгрузки.ФорматФайла;
	ВыгружатьСведенияПоСчетчикам            = ПараметрыВыгрузки.ВыгружатьСведенияПоСчетчикам;
	Кодировка                               = ПараметрыВыгрузки.Кодировка;
	КолонкиДанных                           = ТаблицаДанных.Колонки;
	
	Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
		
		ФайлРеестра = Новый ТекстовыйДокумент;
		ФайлРеестра.УстановитьТипФайла(?(Кодировка = "DOS", КодировкаТекста.OEM, КодировкаТекста.ANSI));
		
		Если ПараметрыВыгрузки.ЭтоTXT И ПараметрыВыгрузки.ВыводитьЗаголовок Тогда
			
			ТекстЗаголовка = СтрЗаменить(ПараметрыВыгрузки.ТекстЗаголовка, "[КоличествоЗаписей]", Формат(ТаблицаДанных.Количество(), "ЧГ="));
			ФайлРеестра.ДобавитьСтроку(ТекстЗаголовка);
			
		КонецЕсли;
		
		Для Каждого ТекущаяСтрока Из ТаблицаДанных Цикл
			
			СтрокаВывода = "";
			
			Для Каждого ТекущаяКолонка Из КолонкиДанных Цикл
				
				Значение = ТекущаяСтрока[ТекущаяКолонка.Имя];
				
				СтрокаВывода = СтрокаВывода + Значение;
				
			КонецЦикла;
			
			Если Не ПустаяСтрока(СтрокаВывода) Тогда
				
				ФайлРеестра.ДобавитьСтроку(СтрокаВывода);
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ПараметрыВыгрузки.ЭтоXLS Тогда
		
		ФайлРеестра = Новый ТабличныйДокумент;
		
		Для Каждого ТекущаяСтрока Из ТаблицаДанных Цикл
			
			ТаблицаСчетчикиИПоказания = Неопределено;
			
			НомерСтроки = ТаблицаДанных.Индекс(ТекущаяСтрока) + 1;
			
			Для Каждого ТекущаяКолонка Из КолонкиДанных Цикл
				
				Значение = ТекущаяСтрока[ТекущаяКолонка.Имя];
				
				НомерКолонки = КолонкиДанных.Индекс(ТекущаяКолонка);
				
				Если ВыгружатьСведенияПоСчетчикам И НЕ ТаблицаСчетчикиИПоказания = Неопределено Тогда
					НомерКолонки = НомерКолонки + ТаблицаСчетчикиИПоказания.Колонки.Количество() - 1;
				Иначе
					НомерКолонки = НомерКолонки + 1;
				КонецЕсли;
				
				ОбластьЯчейка       = ФайлРеестра.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
				ОбластьЯчейка.Текст = Значение;
				
			КонецЦикла;
			
		КонецЦикла;
		
	ИначеЕсли ПараметрыВыгрузки.ЭтоDBF Тогда
		
		ИмяФайлаРеестра = ПолучитьИмяВременногоФайла("DBF");
		
		Файл     = Новый Файл(ИмяФайлаРеестра);
		ВремФайл = Файл.Путь + Прав(Файл.Имя, 8 + 4);
		
		ФайлDBF = Новый XBase(ВремФайл);
		ФайлDBF.АвтоСохранение = Истина;
		ФайлDBF.Кодировка      = ?(Кодировка = "DOS", КодировкаXBase.OEM, КодировкаXBase.ANSI);
		
		Для Каждого ТекущаяКолонка Из КолонкиДанных Цикл
			
			ФайлDBF.Поля.Добавить(ТекущаяКолонка.Имя, "S", 50);
			
		КонецЦикла;
		
		ФайлDBF.СоздатьФайл(ВремФайл);
		
		Для Каждого ТекущаяСтрока Из ТаблицаДанных Цикл
			
			ФайлDBF.Добавить();
			
			Для Каждого ТекущаяКолонка Из КолонкиДанных Цикл
				
				Значение = ТекущаяСтрока[ТекущаяКолонка.Имя];
				
				ФайлDBF[ТекущаяКолонка.Имя] = Значение;
				
			КонецЦикла;
			
			ФайлDBF.Записать();
			
		КонецЦикла;
		
		ЗакрытьФайлDBF(ФайлDBF);
		
		ФайлРеестра = Новый Файл(ВремФайл);
		
	КонецЕсли;
	
	Возврат ФайлРеестра;
	
КонецФункции

&НаКлиенте
// Заменяет параметры в наименовании файла реестра.
Функция ПолучитьИмяФайлаРеестра()
	
	ИмяФайлаРеестра = "";
	
	Если Найти(Объект.ИмяФайлаРеестра, "[ИНН]") > 0
	 ИЛИ Найти(Объект.ИмяФайлаРеестра, "[Расчетный счет]") > 0 Тогда
		
		СведенияОбОрганизации = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.Период, "ИННЮЛ, БанкСчетНомер");
		ИмяФайлаРеестра       = СтрЗаменить(Объект.ИмяФайлаРеестра, "[ИНН]",            СведенияОбОрганизации.ИННЮЛ);
		ИмяФайлаРеестра       = СтрЗаменить(ИмяФайлаРеестра,        "[Расчетный счет]", СведенияОбОрганизации.БанкСчетНомер);
		
	Иначе
		
		ИмяФайлаРеестра = Объект.ИмяФайлаРеестра
		
	КонецЕсли;
	
	Файл = Новый Файл(ИмяФайлаРеестра);
	
	Если ПустаяСтрока(Файл.Расширение) Тогда
		
		ФорматФайла = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Объект.ФорматВыгрузки, "ФорматФайла");
		
		Если ФорматФайла = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла[0].Значение Тогда // "TXT"
			
			ИмяФайлаРеестра = ИмяФайлаРеестра + ".txt";
			
		ИначеЕсли ФорматФайла = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла[2].Значение Тогда // "CSV"
			
			ИмяФайлаРеестра = ИмяФайлаРеестра + ".csv";
			
		ИначеЕсли ФорматФайла = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла[1].Значение Тогда // "XLS"
			
			ИмяФайлаРеестра = ИмяФайлаРеестра + ".xlsx";
			
		ИначеЕсли ФорматФайла = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла[4].Значение Тогда // "DBF"
			
			ИмяФайлаРеестра = ИмяФайлаРеестра + ".dbf";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяФайлаРеестра;
	
КонецФункции

&НаКлиенте
// Обработчик события "НачатьПроверкуСуществования" в процедуре "Выгрузить".
Процедура ВыгрузитьПродолжение(Существует, ДополнительныеПараметры) Экспорт
	
	Если НЕ Существует Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Путь файла реестра не существует.");
		Возврат;
	КонецЕсли;
	
	Попытка
		
		АдресХранилищаРеестра = СформироватьФайлРеестра();
		
		Если НЕ ПустаяСтрока(АдресХранилищаРеестра) Тогда
			
			ИмяФайлаРеестра = ПолучитьИмяФайлаРеестра();
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПослеЗаписиФайлаНаКлиент", ЭтаФорма, ИмяФайлаРеестра);
			
			ПолучаемыеФайлы = Новый Массив;
			ПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(ИмяФайлаРеестра, АдресХранилищаРеестра));
			
			НачатьПолучениеФайлов(ОписаниеОповещения, ПолучаемыеФайлы, ИмяФайлаРеестра, Ложь);
			
		Иначе
			
			УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Файл реестра не создан.");
			
		КонецЕсли;
		
	Исключение
		
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Произошли ошибки в механизме Универсальной выгрузки данных. Обратитесь за помощью к разработчикам: otr@rarus.ru
		|Техническая информация для разработчиков:
		|"
		+ ОписаниеОшибки());
		
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
// Процедура-обработчик описания оповещения.
Процедура ВыполнитьПослеЗаписиФайлаНаКлиент(ПолученныеФайлы, ИмяФайлаРеестра) Экспорт
	
	УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьПользователю("Выгрузка данных в файл """ + ИмяФайлаРеестра + """ завершена.");
	
КонецПроцедуры

&НаКлиенте
// Процедура-обработчик результата закрытия формы "НастройкиЗагрузкиВыгрузки".
Процедура ОбработатьЗакрытиеФормыСправочникаНастройкиЗагрузкиВыгрузки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СформироватьИмяФайла();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "НачатьПроверкуСуществования" в процедуре "СформироватьИмяФайла".
Процедура СформироватьИмяФайлаЗавершение(Существует, ДополнительныеПараметры) Экспорт
	
	ПапкаСохранения = ДополнительныеПараметры.Папка;
	ФорматФайла     = ДополнительныеПараметры.ФорматФайла;
	
	Если Существует Тогда
		Папка             = Новый Файл(ПапкаСохранения);
		ПараметрыОписания = Новый Структура("Папка, ФорматФайла", Папка, ФорматФайла);
		Папка.НачатьПроверкуЭтоКаталог(Новый ОписаниеОповещения("СформироватьИмяФайлаЗавершениеЗавершение", ЭтаФорма, ПараметрыОписания));
		Возврат;
	КонецЕсли;
	
	СформироватьИмяФайлаФрагмент(ПапкаСохранения, ФорматФайла);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "НачатьПроверкуЭтоКаталог" в процедуре "СформироватьИмяФайлаЗавершение".
Процедура СформироватьИмяФайлаЗавершениеЗавершение(ЭтоКаталог, ДополнительныеПараметры) Экспорт
	
	Папка       = ДополнительныеПараметры.Папка;
	ФорматФайла = ДополнительныеПараметры.ФорматФайла;
	
	Если ЭтоКаталог Тогда
		ПапкаСохранения = Папка.ПолноеИмя;
	КонецЕсли;
	
	СформироватьИмяФайлаФрагмент(ПапкаСохранения, ФорматФайла);

КонецПроцедуры

&НаКлиенте
// Формирует имя файла реестра.
Процедура СформироватьИмяФайлаФрагмент(ПапкаСохранения, ФорматФайла)
	
	СокращенияВИмениФайлаНайдены = Ложь;
	
	Если Найти(Объект.ИмяФайлаРеестра, "[ИНН]") > 0 ИЛИ Найти(Объект.ИмяФайлаРеестра, "[Расчетный счет]") > 0 Тогда
		СокращенияВИмениФайлаНайдены = Истина;
	КонецЕсли;
	
	Если НЕ СокращенияВИмениФайлаНайдены Тогда
		
		Если НЕ СтрЗаканчиваетсяНа(ПапкаСохранения, "\") Тогда
			ПапкаСохранения = ПапкаСохранения + "\";
		КонецЕсли;
		
		Объект.ИмяФайлаРеестра = ПапкаСохранения
								+ Строка(Объект.ФорматВыгрузки)
								+ " " + Формат(Объект.Период, "ДФ=ММ")
								+ Формат(Объект.Период, "ДФ=гггг")
								+ "." + ФорматФайла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
// Заполняет данными файл реестра.
//
// Возвращаемое значение:
//   Строка - адрес хранилища значений.
//
Функция СформироватьФайлРеестра()
	
	ВозвращаемыйАдресХранилища = "";
	
	ПараметрыВыгрузки = ПолучитьПараметрыВыгрузки();
	
	Если НЕ ПараметрыВыгрузки = Неопределено Тогда
		
		ТаблицаДанных = ПолучитьОбщуюТаблицуСДаннымиДляВыгрузки(ПараметрыВыгрузки);
		
		Если НЕ ТаблицаДанных.Количество() = 0 Тогда
			
			ФайлРеестра = ПолучитьФайлРеестра(ПараметрыВыгрузки, ТаблицаДанных);
			
			Если НЕ ФайлРеестра = Неопределено Тогда
				
				ВозвращаемыйАдресХранилища = ЗаписатьФайлРеестра(ФайлРеестра, ПараметрыВыгрузки);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемыйАдресХранилища;
	
КонецФункции

&НаСервере
// Создает и заполняет файл реестра.
//
// Возвращаемое значение:
//   Структура - структура данных для формирования и заполнения файла реестра.
//
Функция ПолучитьПараметрыВыгрузки()
	
	ПараметрыВыгрузки = Справочники.УПЖКХ_НастройкиУниверсальнойВыгрузкиДанных.ПолучитьНастройки(Объект.НастройкиВыгрузки, Объект.Организация, Объект.Период);
	
	ФорматВыгрузки = Объект.ФорматВыгрузки;
	ФорматФайла    = ФорматВыгрузки.ФорматФайла;
	
	ПараметрыВыгрузки.Вставить("СтруктураЗначенийПолейФорматаВыгрузки",    СтруктураЗначенийПолейФорматаВыгрузки);
	ПараметрыВыгрузки.Вставить("НачалоПериода",                            НачалоМесяца(ПараметрыВыгрузки.Период));
	ПараметрыВыгрузки.Вставить("КонецПериода",                             КонецМесяца(ПараметрыВыгрузки.Период));
	ПараметрыВыгрузки.Вставить("ФорматФайла",                              ФорматФайла);
	ПараметрыВыгрузки.Вставить("ФорматВыгрузки",                           ФорматВыгрузки);
	ПараметрыВыгрузки.Вставить("Кодировка",                                ФорматВыгрузки.Кодировка);
	ПараметрыВыгрузки.Вставить("ВыгружатьСведенияПоЗакрытымЛицевымСчетам", ПараметрыВыгрузки.ВыгружатьСведенияПоЗакрытымЛицевымСчетам);
	
	СпФорматовФайла   = СтруктураЗначенийПолейФорматаВыгрузки.СпФорматовФайла;
	
	ПараметрыВыгрузки.Вставить("ЭтоTXT", ФорматФайла = СпФорматовФайла[0].Значение); // "TXT"
	ПараметрыВыгрузки.Вставить("ЭтоXLS", ФорматФайла = СпФорматовФайла[1].Значение); // "XLS"
	ПараметрыВыгрузки.Вставить("ЭтоCSV", ФорматФайла = СпФорматовФайла[2].Значение); // "CSV"
	ПараметрыВыгрузки.Вставить("ЭтоDBF", ФорматФайла = СпФорматовФайла[4].Значение); // "DBF"
	
	// Настройки разделителя колонок.
	Если ПараметрыВыгрузки.ЭтоTXT ИЛИ ПараметрыВыгрузки.ЭтоCSV Тогда
		ПараметрыВыгрузки.Вставить("ИспользоватьРезделителиКолонок", Истина);
		Если ПараметрыВыгрузки.ЭтоTXT Тогда
			ПараметрыВыгрузки.Вставить("РазделительКолонок", ПолучитьРазделительКолонок(ПараметрыВыгрузки));
		ИначеЕсли ПараметрыВыгрузки.ЭтоCSV Тогда
			ПараметрыВыгрузки.Вставить("РазделительКолонок", ";");
		КонецЕсли;
	Иначе
		ПараметрыВыгрузки.Вставить("ИспользоватьРезделителиКолонок", Ложь);
		ПараметрыВыгрузки.Вставить("РазделительКолонок",             "");
	КонецЕсли;
	
	ТаблицаПараметров = ПолучитьТаблицуПараметров(ПараметрыВыгрузки);
	
	Если ТаблицаПараметров.Количество() = 0 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Нет параметров для выгрузки.");
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ТаблицаПараметров", ТаблицаПараметров);
	ПараметрыВыгрузки.Вставить("ВыводитьЗаголовок", ПолучитьНастройкуВыгрузкиВыводитьЗаголовок(ФорматВыгрузки, ПараметрыВыгрузки));
	
	ПараметрыБлоковСведений      = Перечисления.УПЖКХ_ПараметрыБлоковСведенийУниверсальнойВыгрузкиДанных;
	ТипыПараметровБлоковСведений = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных;
	
	// Параметры выгрузки сведений по счетчикам.
	
	ЗаполнитьПараметрыВыгрузкиСведенийПоСчетчикам(ПараметрыВыгрузки);
	
	// Сведения по услугам.
	
	ЗаполнитьПараметрыВыгрузкиСведенийПоУслугам(ПараметрыВыгрузки);
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

&НаСервере
// Создает и заполняет файл реестра.
//
// Возвращаемое значение:
//   Структура - структура данных для формирования и заполнения файла реестра.
//
Функция ПолучитьТаблицуПараметров(ПараметрыВыгрузки)
	
	ФорматВыгрузки    = ПараметрыВыгрузки.ФорматВыгрузки;
	ТаблицаПараметров = ФорматВыгрузки.НастройкиСоответствияПолей.Выгрузить();
	
	врТаблицыПараметров = ТаблицаПараметров.СкопироватьКолонки();
	Для Каждого ТекущаяКолонка Из врТаблицыПараметров.Колонки Цикл
		Если СтрНачинаетсяС(ТекущаяКолонка.Имя, "Удалить") Тогда
			ТаблицаПараметров.Колонки.Удалить(ТекущаяКолонка.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаПараметровБлокаСведенийПоСчетчикам = ТаблицаПараметров.СкопироватьКолонки();
	ТаблицаПараметровБлокаСведенийПоУслугам   = ТаблицаПараметров.СкопироватьКолонки();
	
	Для Каждого ТекущаяСтрока Из ТаблицаПараметров Цикл
		Если ТекущаяСтрока.ТипПараметраБлокаСведений = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных.ОсновнойПоСчетчикам
		 ИЛИ ТекущаяСтрока.ТипПараметраБлокаСведений = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных.Счетчики Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаПараметровБлокаСведенийПоСчетчикам.Добавить(), ТекущаяСтрока);
		ИначеЕсли ТекущаяСтрока.ТипПараметраБлокаСведений = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных.ОсновнойПоУслугам
		 ИЛИ ТекущаяСтрока.ТипПараметраБлокаСведений = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных.Услуги Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаПараметровБлокаСведенийПоУслугам.Добавить(), ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыВыгрузки.Вставить("ТаблицаПараметровБлокаСведенийПоСчетчикам", ТаблицаПараметровБлокаСведенийПоСчетчикам);
	ПараметрыВыгрузки.Вставить("ТаблицаПараметровБлокаСведенийПоУслугам",   ТаблицаПараметровБлокаСведенийПоУслугам);
	
	мСтрокиДляУдаления = Новый Массив;
	Для Каждого ТекущаяСтрока Из ТаблицаПараметров Цикл
		Если ТекущаяСтрока.Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.СчетчикиИУслуги
		   И ЗначениеЗаполнено(ТекущаяСтрока.ПараметрБлокаСведений) Тогда
			мСтрокиДляУдаления.Добавить(ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из мСтрокиДляУдаления Цикл
		ТаблицаПараметров.Удалить(ТекущаяСтрока);
	КонецЦикла;
	
	Если НЕ ПараметрыВыгрузки.ЭтоDBF Тогда
		// Не используем строки с нулевым номером колонки.
		НайденныеСтроки = ТаблицаПараметров.НайтиСтроки(Новый Структура("НомерКолонки", 0));
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			ТаблицаПараметров.Удалить(ТекущаяСтрока);
		КонецЦикла;
	КонецЕсли;
	
	// Перезаполним номера строк, по которым будем идентифицировать соответствие параметров выгрузки и колонок для значений параметров.
	КоличествоПараметров = ТаблицаПараметров.Количество();
	Для Сч = 1 По КоличествоПараметров Цикл
		ТаблицаПараметров[Сч - 1].НомерСтроки = Сч;
	КонецЦикла;
	
	// Компенсируем поля с пустым параметром по порядку номеров колонок.
	Если НЕ ПараметрыВыгрузки.ЭтоDBF Тогда
		врТаблицаПараметров = ТаблицаПараметров.Скопировать(, "НомерКолонки");
		врТаблицаПараметров.Сортировать("НомерКолонки Убыв");
		МаксНомерКолонки = врТаблицаПараметров[0].НомерКолонки;
		
		Для Сч = 1 По МаксНомерКолонки Цикл
			Если врТаблицаПараметров.Найти(Сч, "НомерКолонки") = Неопределено Тогда
				НоваяСтрока = ТаблицаПараметров.Добавить();
				
				НоваяСтрока.Параметр     = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.ПустоеПоле;
				НоваяСтрока.НомерКолонки = Сч;
				Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
					НоваяСтрока.РазделительПослеЗначения = ПараметрыВыгрузки.РазделительКолонок;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаПараметров.Сортировать("НомерКолонки Возр");
	КонецЕсли;
	
	Если ТаблицаПараметров.Количество() = 0 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Нет параметров для выгрузки.");
		Возврат ТаблицаПараметров.СкопироватьКолонки();
	КонецЕсли;
	
	Возврат ТаблицаПараметров;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыВыгрузкиСведенийПоСчетчикам(ПараметрыВыгрузки)
	
	ПараметрыУниверсальнойВыгрузкиДанных      = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных;
	ПараметрыБлоковСведений                   = Перечисления.УПЖКХ_ПараметрыБлоковСведенийУниверсальнойВыгрузкиДанных;
	ТипыПараметровБлоковСведений              = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных;
	ТаблицаПараметровБлокаСведенийПоСчетчикам = ПараметрыВыгрузки.ТаблицаПараметровБлокаСведенийПоСчетчикам;
	ИспользоватьРезделителиКолонок            = ПараметрыВыгрузки.ИспользоватьРезделителиКолонок;
	ОбщРазделительКолонок                     = ПараметрыВыгрузки.РазделительКолонок;
	
	ВыгружатьСведенияПоСчетчикам           = Ложь;
	ТаблицаПараметровСведенийПоСчетчикам   = Неопределено;
	ВыгружатьПоказания                     = Ложь;
	МаксКоличествоСведенийВБлокеСчетчиков  = 0;
	ФлагЗавершенияБлокаСведенийПоСчетчикам = "";
	
	ОтборПоТаблицеПараметров = Новый Структура("Параметр, ТипПараметраБлокаСведений");
	
	ОтборПоТаблицеПараметров.Параметр                  = ПараметрыУниверсальнойВыгрузкиДанных.СчетчикиИУслуги;
	ОтборПоТаблицеПараметров.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.ОсновнойПоСчетчикам;
	НайденныеСтрокиОсновногоПараметраСчетчики          = ПараметрыВыгрузки.ТаблицаПараметровБлокаСведенийПоСчетчикам.НайтиСтроки(ОтборПоТаблицеПараметров);
	ВыгружатьРазделительПослеБлокаСчетчики             = Ложь;
	
	Если НайденныеСтрокиОсновногоПараметраСчетчики.Количество() = 1 Тогда
		
		СтрокаОсновногоПараметраСчетчики = НайденныеСтрокиОсновногоПараметраСчетчики[0];
		
		ФлагЗавершенияБлокаСведенийПоСчетчикам = СтрокаОсновногоПараметраСчетчики.ФлагЗавершенияБлокаСведений;
		МаксКоличествоСведенийВБлокеСчетчиков  = СтрокаОсновногоПараметраСчетчики.МаксимальноеКоличествоСведенийВБлоке;
		
		// Получаем строки параметров сведений по счетчикам.
		ОтборПоТаблицеПараметров.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.Счетчики;
		ТаблицаПараметровСведенийПоСчетчикам = ТаблицаПараметровБлокаСведенийПоСчетчикам.Скопировать(ОтборПоТаблицеПараметров);
		
		Если НЕ ТаблицаПараметровСведенийПоСчетчикам.Количество() = 0 Тогда
			
			ВыгружатьСведенияПоСчетчикам = Истина;
			
			Если ИспользоватьРезделителиКолонок Тогда
				Для Каждого ТекущаяСтрока Из ТаблицаПараметровСведенийПоСчетчикам Цикл
					Если ПустаяСтрока(ТекущаяСтрока.РазделительПослеЗначения) Тогда
						ТекущаяСтрока.РазделительПослеЗначения = ОбщРазделительКолонок;
					Иначе
						ТекущаяСтрока.РазделительПослеЗначения = ПолучитьРазделительКолонок(ПараметрыВыгрузки, ТекущаяСтрока.РазделительПослеЗначения);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если НЕ ТаблицаПараметровСведенийПоСчетчикам.Найти(ПараметрыБлоковСведений.Показание, "ПараметрБлокаСведений") = Неопределено Тогда
				ВыгружатьПоказания = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок И ВыгружатьСведенияПоСчетчикам Тогда
			НомерКолонкиБлокаСчетчики = СтрокаОсновногоПараметраСчетчики.НомерКолонки;
			Для Каждого ТекущаяСтрокаПараметра Из ПараметрыВыгрузки.ТаблицаПараметров Цикл
				Если НомерКолонкиБлокаСчетчики < ТекущаяСтрокаПараметра.НомерКолонки
				   И НЕ ТекущаяСтрокаПараметра.Параметр = ПараметрыУниверсальнойВыгрузкиДанных.ПустоеПоле Тогда
					ВыгружатьРазделительПослеБлокаСчетчики = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ВыгружатьСведенияПоСчетчикам",           ВыгружатьСведенияПоСчетчикам);
	ПараметрыВыгрузки.Вставить("ТаблицаПараметровСведенийПоСчетчикам",   ТаблицаПараметровСведенийПоСчетчикам);
	ПараметрыВыгрузки.Вставить("ВыгружатьПоказания",                     ВыгружатьПоказания);
	ПараметрыВыгрузки.Вставить("МаксКоличествоСведенийВБлокеСчетчиков",  МаксКоличествоСведенийВБлокеСчетчиков);
	ПараметрыВыгрузки.Вставить("ФлагЗавершенияБлокаСведенийПоСчетчикам", ФлагЗавершенияБлокаСведенийПоСчетчикам);
	ПараметрыВыгрузки.Вставить("ВыгружатьРазделительПослеБлокаСчетчики", ВыгружатьРазделительПослеБлокаСчетчики);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыВыгрузкиСведенийПоУслугам(ПараметрыВыгрузки)
	
	ПараметрыУниверсальнойВыгрузкиДанных    = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных;
	ПараметрыБлоковСведений                 = Перечисления.УПЖКХ_ПараметрыБлоковСведенийУниверсальнойВыгрузкиДанных;
	ТипыПараметровБлоковСведений            = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных;
	ТаблицаПараметровБлокаСведенийПоУслугам = ПараметрыВыгрузки.ТаблицаПараметровБлокаСведенийПоУслугам;
	ИспользоватьРезделителиКолонок          = ПараметрыВыгрузки.ИспользоватьРезделителиКолонок;
	ОбщРазделительКолонок                   = ПараметрыВыгрузки.РазделительКолонок;
	
	ВыгружатьСведенияПоУслугам           = Ложь;
	ТаблицаПараметровСведенийПоУслугам   = Неопределено;
	МаксКоличествоСведенийВБлокеУслуг    = 0;
	ВыгружатьСуммыДолгаПоУслугам         = Ложь;
	ФлагЗавершенияБлокаСведенийПоУслугам = "";
	
	ОтборПоТаблицеПараметров = Новый Структура("Параметр, ТипПараметраБлокаСведений");
	
	ОтборПоТаблицеПараметров.Параметр                  = ПараметрыУниверсальнойВыгрузкиДанных.СчетчикиИУслуги;
	ОтборПоТаблицеПараметров.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.ОсновнойПоУслугам;
	НайденныеСтрокиОсновногоПараметраУслуги            = ТаблицаПараметровБлокаСведенийПоУслугам.НайтиСтроки(ОтборПоТаблицеПараметров);
	ВыгружатьРазделительПослеБлокаУслуги               = Ложь;
	
	Если НайденныеСтрокиОсновногоПараметраУслуги.Количество() = 1 Тогда
		
		СтрокаОсновногоПараметраУслуги = НайденныеСтрокиОсновногоПараметраУслуги[0];
		
		ФлагЗавершенияБлокаСведенийПоУслугам = СтрокаОсновногоПараметраУслуги.ФлагЗавершенияБлокаСведений;
		МаксКоличествоСведенийВБлокеУслуг    = СтрокаОсновногоПараметраУслуги.МаксимальноеКоличествоСведенийВБлоке;
		
		// Получаем строки параметров сведений по услугам.
		ОтборПоТаблицеПараметров.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.Услуги;
		ТаблицаПараметровСведенийПоУслугам         = ТаблицаПараметровБлокаСведенийПоУслугам.Скопировать(ОтборПоТаблицеПараметров);
		
		Если НЕ ТаблицаПараметровСведенийПоУслугам.Количество() = 0 Тогда
			
			ВыгружатьСведенияПоУслугам = Истина;
			
			Если ИспользоватьРезделителиКолонок Тогда
				Для Каждого ТекущаяСтрока Из ТаблицаПараметровСведенийПоУслугам Цикл
					Если ПустаяСтрока(ТекущаяСтрока.РазделительПослеЗначения) Тогда
						ТекущаяСтрока.РазделительПослеЗначения = ОбщРазделительКолонок;
					Иначе
						ТекущаяСтрока.РазделительПослеЗначения = ПолучитьРазделительКолонок(ПараметрыВыгрузки, ТекущаяСтрока.РазделительПослеЗначения);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если НЕ ТаблицаПараметровСведенийПоУслугам.Найти(ПараметрыБлоковСведений.СуммаДолга, "ПараметрБлокаСведений") = Неопределено Тогда
				ВыгружатьСуммыДолгаПоУслугам = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок И ВыгружатьСведенияПоУслугам Тогда
			НомерКолонкиБлокаУслуги = СтрокаОсновногоПараметраУслуги.НомерКолонки;
			Для Каждого ТекущаяСтрокаПараметра Из ПараметрыВыгрузки.ТаблицаПараметров Цикл
				Если НомерКолонкиБлокаУслуги < ТекущаяСтрокаПараметра.НомерКолонки
				   И НЕ ТекущаяСтрокаПараметра.Параметр = ПараметрыУниверсальнойВыгрузкиДанных.ПустоеПоле Тогда
					ВыгружатьРазделительПослеБлокаУслуги = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ВыгружатьСведенияПоУслугам",           ВыгружатьСведенияПоУслугам);
	ПараметрыВыгрузки.Вставить("ТаблицаПараметровСведенийПоУслугам",   ТаблицаПараметровСведенийПоУслугам);
	ПараметрыВыгрузки.Вставить("МаксКоличествоСведенийВБлокеУслуг",    МаксКоличествоСведенийВБлокеУслуг);
	ПараметрыВыгрузки.Вставить("ВыгружатьСуммыДолгаПоУслугам",         ВыгружатьСуммыДолгаПоУслугам);
	ПараметрыВыгрузки.Вставить("ФлагЗавершенияБлокаСведенийПоУслугам", ФлагЗавершенияБлокаСведенийПоУслугам);
	ПараметрыВыгрузки.Вставить("ВыгружатьРазделительПослеБлокаУслуги", ВыгружатьРазделительПослеБлокаУслуги);
	
КонецПроцедуры

&НаСервереБезКонтекста
// Получает общую сумму долга по всем лицевым счетам таблицы "ТаблицаДолгов".
//
// Параметры:
//  ПараметрыВыгрузки - Структура - настройки обработки.
//
Функция ПолучитьОбщуюСуммуДолга(СтруктураОбщихТаблиц)
	
	Сумма = СтруктураОбщихТаблиц.ТаблицаДолги.Итог("СуммаДолга");
	
	Возврат Сумма;
	
КонецФункции

&НаСервереБезКонтекста
// Получает общую сумму начислений по всем лицевым счетам таблицы "ТаблицаНачислений".
//
// Параметры:
//  ПараметрыВыгрузки - Структура - настройки обработки.
//
Функция ПолучитьОбщуюСуммуНачислений(СтруктураОбщихТаблиц)
	
	Сумма = СтруктураОбщихТаблиц.ТаблицаНачислений.Итог("СуммаНачисления");
	
	Возврат Сумма;
	
КонецФункции

&НаСервереБезКонтекста
// Получает период выгрузки для заголовка файла TXT.
//
// Параметры:
//  ПараметрыВыгрузки - Структура - настройки обработки.
//
Функция ПолучитьПериодВыгрузки(ПараметрыВыгрузки)
	
	Период = Формат(ПараметрыВыгрузки.Период, "ДФ=dd.MM.yyyy");
	
	Возврат Период;
	
КонецФункции

&НаСервереБезКонтекста
// Получает факт вывода заголовка в файле выгрузки.
//
// Параметры:
//  ФорматВыгрузки	 - Ссылка - настройки загрузки / выгрузки.
//
Функция ПолучитьНастройкуВыгрузкиВыводитьЗаголовок(ФорматВыгрузки, ПараметрыВыгрузки)
	
	Если ПараметрыВыгрузки.ЭтоTXT И ФорматВыгрузки.ВыводитьЗаголовок Тогда
		ВыводитьЗаголовок = Истина;
	Иначе
		ВыводитьЗаголовок = Ложь;
	КонецЕсли;
	
	Возврат ВыводитьЗаголовок;
	
КонецФункции

&НаСервереБезКонтекста
// Получает текст комментария для заголовка в файле.
//
// Параметры:
//  ПараметрыВыгрузки - Структура - настройки обработки.
//
Функция ПолучитьНастройкуВыгрузкиТекстЗаголовка(ПараметрыВыгрузки, ФорматВыгрузки, СтруктураОбщихТаблиц)
	
	Если НЕ (ПараметрыВыгрузки.ЭтоTXT И ПараметрыВыгрузки.ВыводитьЗаголовок) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗаголовка     = ФорматВыгрузки.ТекстЗаголовкаВТекстовомФайле;
	СписокПараметров   = Справочники.КВП_НастройкиЗагрузкиВыгрузки.ПолучитьСписокПараметровДляЗаголовкаВТекстовомФайле();
	
	Для Каждого ТекущийПараметрСписка Из СписокПараметров Цикл
		ТекущийПараметр = ТекущийПараметрСписка.Значение;
		
		ПредставлениеПараметра = ПредставлениеТекущегоПараметра(ТекущийПараметр);
		
		Если ТекущийПараметр = "КоличествоЗаписей" Тогда
			// Значение получаем после заполнения файла TXT данными в процедуре "СформироватьФайлРеестра()".
		ИначеЕсли ТекущийПараметр = "ОбщаяСуммаДолга" Тогда
			ОбщаяСуммаДолга = ПолучитьОбщуюСуммуДолга(СтруктураОбщихТаблиц);
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, ПредставлениеПараметра, Формат(ОбщаяСуммаДолга, "ЧДЦ=2; ЧН=0,00; ЧГ="));
		ИначеЕсли ТекущийПараметр = "ОбщаяСуммаНачислений" Тогда
			ОбщаяСуммаНачислений = ПолучитьОбщуюСуммуНачислений(СтруктураОбщихТаблиц);
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, ПредставлениеПараметра, Формат(ОбщаяСуммаНачислений, "ЧДЦ=2; ЧН=0,00; ЧГ="));
		ИначеЕсли ТекущийПараметр = "ПериодВыгрузки" Тогда
			ПериодВыгрузки = ПолучитьПериодВыгрузки(ПараметрыВыгрузки);
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, ПредставлениеПараметра, ПериодВыгрузки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТекстЗаголовка;
	
КонецФункции

&НаСервереБезКонтекста
// Получает представление параметра для текста заголовка TXT файла.
//
// Параметры:
//  ТекущийПараметр	 - Строка - имя параметра.
//
Функция ПредставлениеТекущегоПараметра(ТекущийПараметр)
	
	ПредставлениеПараметра = "[" + ТекущийПараметр + "]";
	
	Возврат ПредставлениеПараметра;
	
КонецФункции

&НаСервереБезКонтекста
// Получает разделитель колонок.
//
// Параметры:
//  ПараметрыВыгрузки	 - Структура - настройки обработки.
//
Функция ПолучитьРазделительКолонок(ПараметрыВыгрузки, РазделительКолонок = Неопределено)
	
	Если РазделительКолонок = Неопределено Тогда
		РазделительКолонок = ПараметрыВыгрузки.ФорматВыгрузки.РазделительКолонок;
	КонецЕсли;
	
	Если ПараметрыВыгрузки.ЭтоTXT Тогда
		Если РазделительКолонок = "Таб" Тогда
			Разделитель = Символы.Таб;
		ИначеЕсли ЗначениеЗаполнено(РазделительКолонок) Тогда
			Разделитель = РазделительКолонок;
		Иначе
			Разделитель = ":";
		КонецЕсли;
	ИначеЕсли ПараметрыВыгрузки.ЭтоCSV Тогда
		Разделитель = ";";
	Иначе
		Разделитель = "";
	КонецЕсли;
	
	Возврат Разделитель;
	
КонецФункции

&НаСервереБезКонтекста
// Подготавливает сведения о лицевых счетах в запросе.
//
// Параметры:
//  Запрос				 - результат запроса менеджера временных таблиц.
//  ПараметрыВыгрузки	 - Структура - настройки обработки.
//
Функция ПодготовитьОсновныеДанныеПоЛицевымСчетамВЗапросе(Запрос, ПараметрыВыгрузки)
	
	Запрос.УстановитьПараметр("ОтборПоОтветственномуСобственнику",        ПараметрыВыгрузки.ОтборПоОтветственномуСобственнику);
	Запрос.УстановитьПараметр("ВыгружатьСведенияПоЗакрытымЛицевымСчетам", ПараметрыВыгрузки.ВыгружатьСведенияПоЗакрытымЛицевымСчетам);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_СведенияДляВзаиморасчетовПоЛССрезПоследних.ЛицевойСчет
	|ПОМЕСТИТЬ втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией
	|ИЗ
	|	РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС.СрезПоследних(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И &УсловиеОтбораЛицевыхСчетов) КАК УПЖКХ_СведенияДляВзаиморасчетовПоЛССрезПоследних";
	
	ВыборЛицевыхСчетов = ПараметрыВыгрузки.ВыборЛицевыхСчетов;
	
	Если ВыборЛицевыхСчетов = 1 ИЛИ ВыборЛицевыхСчетов = 2 Тогда
		
		Запрос.УстановитьПараметр("СписокЛицевыхСчетов", ПараметрыВыгрузки.ВыбранныеЛицевыеСчета);
		
		Если ВыборЛицевыхСчетов = 1 Тогда
			УсловиеОтбораЛицевыхСчетов  = "ЛицевойСчет В (&СписокЛицевыхСчетов)";
			// Для дальнейшего получения телефонов жильцов подготовим таблицу заранее.
			ТаблицаТелефоновЖильцов = Справочники.УПЖКХ_Жильцы.ПолучитьТаблицуТелефоновФизЛицЖильцов(ПараметрыВыгрузки.ВыбранныеЛицевыеСчета);
		ИначеЕсли ВыборЛицевыхСчетов = 2 Тогда
			УсловиеОтбораЛицевыхСчетов  = "НЕ ЛицевойСчет В (&СписокЛицевыхСчетов)";
			// Для дальнейшего получения телефонов жильцов подготовим таблицу заранее.
			ТаблицаТелефоновЖильцов = Справочники.УПЖКХ_Жильцы.ПолучитьТаблицуТелефоновФизЛицЖильцов(ПараметрыВыгрузки.ВыбранныеЛицевыеСчета, Истина);
		КонецЕсли;
		
	Иначе // Выбор по лицевым счетам не используется.
		УсловиеОтбораЛицевыхСчетов = "ИСТИНА";
		// Для дальнейшего получения телефонов жильцов подготовим таблицу заранее.
		ТаблицаТелефоновЖильцов = Справочники.УПЖКХ_Жильцы.ПолучитьТаблицуТелефоновФизЛицЖильцов();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТаблицаТелефоновЖильцов", ТаблицаТелефоновЖильцов);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбораЛицевыхСчетов", УсловиеОтбораЛицевыхСчетов);
	
	Запрос.Текст = Запрос.Текст +
	"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" +
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	КВП_ЛицевыеСчетаСрезПоследних.ЛицевойСчет КАК ЛицевойСчет
	|ПОМЕСТИТЬ врТаблицаДействующиеЛицевыеСчета
	|ИЗ
	|	РегистрСведений.КВП_ЛицевыеСчета.СрезПоследних(
	|			&НачалоПериода,
	|			ЛицевойСчет В
	|				(ВЫБРАТЬ
	|					втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией.ЛицевойСчет
	|				ИЗ
	|					втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией КАК втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией)) КАК КВП_ЛицевыеСчетаСрезПоследних
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ВыгружатьСведенияПоЗакрытымЛицевымСчетам
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ КВП_ЛицевыеСчетаСрезПоследних.Действует
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КВП_ЛицевыеСчета.ЛицевойСчет
	|ИЗ
	|	РегистрСведений.КВП_ЛицевыеСчета КАК КВП_ЛицевыеСчета
	|ГДЕ
	|	КВП_ЛицевыеСчета.Период >= &НачалоПериода
	|	И КВП_ЛицевыеСчета.Период <= &КонецПериода
	|	И КВП_ЛицевыеСчета.ЛицевойСчет В
	|			(ВЫБРАТЬ
	|				втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией.ЛицевойСчет
	|			ИЗ
	|				втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией КАК втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втЛицевыеСчетаСоВзаиморасчетамиСОрганизацией
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет КАК ЛицевойСчет,
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет.Адрес КАК Помещение,
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет.Адрес.Код КАК КодПомещения,
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет.Адрес.Владелец КАК Здание,
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет.Идентификатор КАК ИдентификаторЛС,
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет.Код КАК КодЛС,
	|	УПЖКХ_ОтветственныйСобственник.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	ПРЕДСТАВЛЕНИЕ(УПЖКХ_ОтветственныйСобственник.ОтветственныйВладелец) КАК ОтветственныйВладелецПредставление,
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет.Наименование КАК НаименованиеЛС,
	|	УПЖКХ_ОтветственныйСобственник.ЛицевойСчет.Телефон КАК НомерТелефонаЛС,
	|	ВЫБОР
	|		КОГДА УПЖКХ_ОтветственныйСобственник.ОтветственныйВладелец ССЫЛКА Справочник.Контрагенты
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоЖилец,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(УПЖКХ_ОтветственныйСобственник.ОтветственныйВладелец) = ТИП(Справочник.УПЖКХ_Жильцы)
	|			ТОГДА УПЖКХ_ОтветственныйСобственник.ОтветственныйВладелец.ФизЛицо
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УПЖКХ_Жильцы.ПустаяСсылка)
	|	КОНЕЦ КАК ФизЛицоЖильца
	|ПОМЕСТИТЬ врТаблицаОтветственныеЛица
	|ИЗ
	|	РегистрСведений.УПЖКХ_ОтветственныйСобственникНанимательЛицевогоСчета.СрезПоследних(
	|			&КонецПериода,
	|			ЛицевойСчет В
	|				(ВЫБРАТЬ
	|					врТаблицаДействующиеЛицевыеСчета.ЛицевойСчет
	|				ИЗ
	|					врТаблицаДействующиеЛицевыеСчета КАК врТаблицаДействующиеЛицевыеСчета)) КАК УПЖКХ_ОтветственныйСобственник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаДействующиеЛицевыеСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаОтветственныеЛица.ЛицевойСчет КАК ЛицевойСчет,
	|	врТаблицаОтветственныеЛица.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	врТаблицаОтветственныеЛица.ОтветственныйВладелец.Наименование КАК НаименованиеКонтрагента,
	|	ВЫБОР
	|		КОГДА врТаблицаОтветственныеЛица.ОтветственныйВладелец.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РазбиватьНаименованиеКонтрагента
	|ПОМЕСТИТЬ врТаблицаКонтрагентов
	|ИЗ
	|	врТаблицаОтветственныеЛица КАК врТаблицаОтветственныеЛица
	|ГДЕ
	|	НЕ врТаблицаОтветственныеЛица.ЭтоЖилец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФИОФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ФИОФизическихЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизическихЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизическихЛицСрезПоследних.Отчество КАК Отчество
	|ПОМЕСТИТЬ врТаблицаФИОФизЛиц
	|ИЗ
	|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(
	|			&КонецПериода,
	|			ФизическоеЛицо В
	|				(ВЫБРАТЬ
	|					врТаблицаОтветственныеЛица.ФизЛицоЖильца КАК ФизЛицо
	|				ИЗ
	|					врТаблицаОтветственныеЛица КАК врТаблицаОтветственныеЛица
	|				ГДЕ
	|					врТаблицаОтветственныеЛица.ЭтоЖилец)) КАК ФИОФизическихЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаОтветственныеЛица.ЛицевойСчет КАК ЛицевойСчет,
	|	врТаблицаОтветственныеЛица.Помещение КАК Помещение,
	|	врТаблицаОтветственныеЛица.КодПомещения КАК КодПомещения,
	|	врТаблицаОтветственныеЛица.Здание КАК Здание,
	|	врТаблицаОтветственныеЛица.ИдентификаторЛС КАК ИдентификаторЛС,
	|	врТаблицаОтветственныеЛица.КодЛС КАК КодЛС,
	|	врТаблицаОтветственныеЛица.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	врТаблицаОтветственныеЛица.ОтветственныйВладелецПредставление КАК ОтветственныйВладелецПредставление,
	|	врТаблицаОтветственныеЛица.НаименованиеЛС КАК НаименованиеЛС,
	|	врТаблицаОтветственныеЛица.НомерТелефонаЛС КАК НомерТелефонаЛС,
	|	врТаблицаОтветственныеЛица.ЭтоЖилец КАК ЭтоЖилец,
	|	ЕСТЬNULL(врТаблицаКонтрагентов.НаименованиеКонтрагента, """") КАК НаименованиеКонтрагента,
	|	ЕСТЬNULL(врТаблицаКонтрагентов.РазбиватьНаименованиеКонтрагента, ЛОЖЬ) КАК РазбиватьНаименованиеКонтрагента,
	|	ЕСТЬNULL(врТаблицаФИОФизЛиц.Фамилия, """") КАК Фамилия,
	|	ЕСТЬNULL(врТаблицаФИОФизЛиц.Имя, """") КАК Имя,
	|	ЕСТЬNULL(врТаблицаФИОФизЛиц.Отчество, """") КАК Отчество
	|ПОМЕСТИТЬ врТаблицаОтветственныхСобственниковСФИО
	|ИЗ
	|	врТаблицаОтветственныеЛица КАК врТаблицаОтветственныеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ врТаблицаКонтрагентов КАК врТаблицаКонтрагентов
	|		ПО врТаблицаОтветственныеЛица.ЛицевойСчет = врТаблицаКонтрагентов.ЛицевойСчет
	|			И врТаблицаОтветственныеЛица.ОтветственныйВладелец = врТаблицаКонтрагентов.ОтветственныйВладелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ врТаблицаФИОФизЛиц КАК врТаблицаФИОФизЛиц
	|		ПО врТаблицаОтветственныеЛица.ФизЛицоЖильца = врТаблицаФИОФизЛиц.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаОтветственныхСобственниковСФИО.ЛицевойСчет КАК ЛицевойСчет,
	|	врТаблицаОтветственныхСобственниковСФИО.Помещение КАК Помещение,
	|	врТаблицаОтветственныхСобственниковСФИО.КодПомещения КАК КодПомещения,
	|	врТаблицаОтветственныхСобственниковСФИО.Здание КАК Здание,
	|	врТаблицаОтветственныхСобственниковСФИО.НаименованиеЛС КАК НаименованиеЛС,
	|	врТаблицаОтветственныхСобственниковСФИО.ИдентификаторЛС КАК ИдентификаторЛС,
	|	врТаблицаОтветственныхСобственниковСФИО.КодЛС КАК КодЛС,
	|	врТаблицаОтветственныхСобственниковСФИО.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	врТаблицаОтветственныхСобственниковСФИО.ОтветственныйВладелецПредставление КАК ОтветственныйВладелецПредставление,
	|	врТаблицаОтветственныхСобственниковСФИО.НомерТелефонаЛС КАК НомерТелефонаЛС,
	|	врТаблицаОтветственныхСобственниковСФИО.ЭтоЖилец КАК ЭтоЖилец,
	|	врТаблицаОтветственныхСобственниковСФИО.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
	|	врТаблицаОтветственныхСобственниковСФИО.РазбиватьНаименованиеКонтрагента КАК РазбиватьНаименованиеКонтрагента,
	|	врТаблицаОтветственныхСобственниковСФИО.Фамилия КАК Фамилия,
	|	врТаблицаОтветственныхСобственниковСФИО.Имя КАК Имя,
	|	врТаблицаОтветственныхСобственниковСФИО.Отчество КАК Отчество
	|ПОМЕСТИТЬ врТаблицаЛицевыеСчета
	|ИЗ
	|	врТаблицаОтветственныхСобственниковСФИО КАК врТаблицаОтветственныхСобственниковСФИО
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтборПоОтветственномуСобственнику = 0
	|				ТОГДА ИСТИНА
	|			КОГДА &ОтборПоОтветственномуСобственнику = 1
	|				ТОГДА врТаблицаОтветственныхСобственниковСФИО.ОтветственныйВладелец ССЫЛКА Справочник.УПЖКХ_Жильцы
	|						ИЛИ врТаблицаОтветственныхСобственниковСФИО.ОтветственныйВладелец ССЫЛКА Справочник.Контрагенты
	|							И врТаблицаОтветственныхСобственниковСФИО.ОтветственныйВладелец.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			КОГДА &ОтборПоОтветственномуСобственнику = 2
	|				ТОГДА врТаблицаОтветственныхСобственниковСФИО.ОтветственныйВладелец ССЫЛКА Справочник.Контрагенты
	|						И врТаблицаОтветственныхСобственниковСФИО.ОтветственныйВладелец.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаОтветственныхСобственниковСФИО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаОтветственныеЛица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаКонтрагентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаФИОФизЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет,
	|	врТаблицаЛицевыеСчета.Помещение КАК Помещение,
	|	врТаблицаЛицевыеСчета.Помещение КАК КодПомещения,
	|	врТаблицаЛицевыеСчета.Здание КАК Здание,
	|	врТаблицаЛицевыеСчета.НаименованиеЛС КАК НаименованиеЛС,
	|	врТаблицаЛицевыеСчета.ИдентификаторЛС КАК ИдентификаторЛС,
	|	врТаблицаЛицевыеСчета.КодЛС КАК КодЛС,
	|	врТаблицаЛицевыеСчета.ОтветственныйВладелец КАК ОтветственныйВладелец,
	|	врТаблицаЛицевыеСчета.ОтветственныйВладелецПредставление КАК ОтветственныйВладелецПредставление,
	|	врТаблицаЛицевыеСчета.НомерТелефонаЛС КАК НомерТелефонаЛС,
	|	врТаблицаЛицевыеСчета.ЭтоЖилец КАК ЭтоЖилец,
	|	врТаблицаЛицевыеСчета.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
	|	врТаблицаЛицевыеСчета.РазбиватьНаименованиеКонтрагента КАК РазбиватьНаименованиеКонтрагента,
	|	врТаблицаЛицевыеСчета.Фамилия КАК Фамилия,
	|	врТаблицаЛицевыеСчета.Имя КАК Имя,
	|	врТаблицаЛицевыеСчета.Отчество КАК Отчество
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета";
	
	Запрос.Выполнить();
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует таблицу значений для заполнения выгружаемых данных.
//
// Параметры:
//  ПараметрыВыгрузки	 - Структура	 - структура настроек выгрузки.
// 
// Возвращаемое значение:
//  Таблица значений.
//
&НаСервереБезКонтекста
Функция ПолучитьОбщуюТаблицуСДаннымиДляВыгрузки(ПараметрыВыгрузки)
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	
	СтруктураОбщихТаблиц = ПолучитьСтруктуруОбщихТаблиц(ПараметрыВыгрузки);
	
	Если СтруктураОбщихТаблиц = Неопределено Тогда
		Возврат ТаблицаДанных;
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ТекстЗаголовка", ПолучитьНастройкуВыгрузкиТекстЗаголовка(ПараметрыВыгрузки, ПараметрыВыгрузки.ФорматВыгрузки, СтруктураОбщихТаблиц));
	
	ПараметрыУниверсальнойВыгрузкиДанных = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных;
	ТипыПараметровБлоковСведений         = Перечисления.УПЖКХ_ТипыПараметровБлоковСведенийУниверсальнойВыгрузкиДанных;
	
	ТаблицаСведенийПоСчетчикам     = СтруктураОбщихТаблиц.ТаблицаСведенийПоСчетчикам;
	ТаблицаСведенийПоУслугам       = СтруктураОбщихТаблиц.ТаблицаСведенийПоУслугам;
	ТаблицаЛицевыеСчета            = СтруктураОбщихТаблиц.ТаблицаЛицевыеСчета;
	РазделительКолонок             = ПараметрыВыгрузки.РазделительКолонок;
	ВыгружатьСведенияПоСчетчикам   = ПараметрыВыгрузки.ВыгружатьСведенияПоСчетчикам;
	ВыгружатьСведенияПоУслугам     = ПараметрыВыгрузки.ВыгружатьСведенияПоУслугам;
	ИспользоватьРезделителиКолонок = ПараметрыВыгрузки.ИспользоватьРезделителиКолонок;
	ТаблицаПараметров              = ПараметрыВыгрузки.ТаблицаПараметров;
	
	мСтрокиДляУдаления = Новый Массив;
	Для Каждого ТекущаяСтрока Из ТаблицаПараметров Цикл
		Если ТекущаяСтрока.Параметр = ПараметрыУниверсальнойВыгрузкиДанных.СчетчикиИУслуги
		   И ЗначениеЗаполнено(ТекущаяСтрока.ПараметрБлокаСведений) Тогда
			мСтрокиДляУдаления.Добавить(ТекущаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из мСтрокиДляУдаления Цикл
		ТаблицаПараметров.Удалить(ТекущаяСтрока);
	КонецЦикла;
	
	// 1. Получаем количество всех колонок по максимальному Номеру колонки или Номеру строки в таблице.
	
	Если ВыгружатьСведенияПоСчетчикам Тогда
		врТаблицаСведенийПоСчетчикам = ТаблицаСведенийПоСчетчикам.СкопироватьКолонки();
		врТаблицаСведенийПоСчетчикам.Колонки.Удалить("ЛицевойСчет");
	КонецЕсли;
	
	Если ВыгружатьСведенияПоУслугам Тогда
		врТаблицаСведенийПоУслугам = ТаблицаСведенийПоУслугам.СкопироватьКолонки();
		врТаблицаСведенийПоУслугам.Колонки.Удалить("ЛицевойСчет");
	КонецЕсли;
	
	// Формируем структуру таблицы данных.
	СчПараметров   = 1;
	ИмяРазделителя = "OsBorder";
	СчРазделителя  = 1;
	
	Для Каждого ТекущаяСтрока Из ТаблицаПараметров Цикл
		
		Если ТекущаяСтрока.Параметр = ПараметрыУниверсальнойВыгрузкиДанных.СчетчикиИУслуги Тогда
			
			Если ВыгружатьСведенияПоСчетчикам И ТекущаяСтрока.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.ОсновнойПоСчетчикам Тогда
				
				Для Каждого ТекущаяКолонка Из врТаблицаСведенийПоСчетчикам.Колонки Цикл
					ТаблицаДанных.Колонки.Добавить(ТекущаяКолонка.Имя);
				КонецЦикла;
				
			ИначеЕсли ВыгружатьСведенияПоУслугам И ТекущаяСтрока.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.ОсновнойПоУслугам Тогда
				
				Для Каждого ТекущаяКолонка Из врТаблицаСведенийПоУслугам.Колонки Цикл
					ТаблицаДанных.Колонки.Добавить(ТекущаяКолонка.Имя);
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			ИмяПараметра = XMLстрока(ТекущаяСтрока.Параметр);
			
			Если ТекущаяСтрока.Параметр = ПараметрыУниверсальнойВыгрузкиДанных.ПустоеПоле
			   И ((ПараметрыВыгрузки.ЭтоDBF И ТаблицаПараметров.НайтиСтроки(Новый Структура("Параметр, ИмяПоляDBF", ПараметрыУниверсальнойВыгрузкиДанных.ПустоеПоле, ТекущаяСтрока.ИмяПоляDBF)).Количество() > 1)
			 ИЛИ (НЕ ПараметрыВыгрузки.ЭтоDBF И ТаблицаПараметров.НайтиСтроки(Новый Структура("Параметр", ПараметрыУниверсальнойВыгрузкиДанных.ПустоеПоле)).Количество() > 1)) Тогда
				Постфикс     = СчПараметров;
				СчПараметров = СчПараметров + 1;
			Иначе
				Постфикс = "";
			КонецЕсли;
			
			ИмяКолонки = ПолучитьИмяКолонки(ИмяПараметра, "", Постфикс, ПараметрыВыгрузки, ТекущаяСтрока.ИмяПоляDBF);
			
			ТаблицаДанных.Колонки.Добавить(ИмяКолонки);
			
			Если ИспользоватьРезделителиКолонок Тогда
				
				ИмяКолонкиРазделителя = ИмяРазделителя + СчРазделителя;
				
				ТаблицаДанных.Колонки.Добавить(ИмяКолонкиРазделителя, Новый ОписаниеТипов("Строка"));
				
				СчРазделителя = СчРазделителя + 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Заполним таблицу данными.
	
	Отбор = Новый Структура("ЛицевойСчет");
	
	Для Каждого ТекущаяСтрокаЛицевогоСчета Из ТаблицаЛицевыеСчета Цикл
		
		КоличествоПараметров = ТаблицаПараметров.Количество();
		
		Отбор.ЛицевойСчет = ТекущаяСтрокаЛицевогоСчета.ЛицевойСчет;
		
		НоваяСтрока = ТаблицаДанных.Добавить();
		
		СчПараметров = 1;
		
		Для Каждого ТекущаяСтрокаПараметра Из ТаблицаПараметров Цикл
			
			Если ТекущаяСтрокаПараметра.Параметр = ПараметрыУниверсальнойВыгрузкиДанных.СчетчикиИУслуги Тогда
				
				Если ВыгружатьСведенияПоСчетчикам И ТекущаяСтрокаПараметра.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.ОсновнойПоСчетчикам Тогда
					
					НайденныеСтрокиПоПУ = ТаблицаСведенийПоСчетчикам.НайтиСтроки(Отбор);
					
					Если НЕ НайденныеСтрокиПоПУ.Количество() = 0 Тогда
						ТекущаяСтрокаСведенийПУ = НайденныеСтрокиПоПУ[0];
						Для Каждого ТекущаяКолонкаПУ Из врТаблицаСведенийПоСчетчикам.Колонки Цикл
							НоваяСтрока[ТекущаяКолонкаПУ.Имя] = ТекущаяСтрокаСведенийПУ[ТекущаяКолонкаПУ.Имя];
						КонецЦикла;
					КонецЕсли;
					
					КоличествоПараметров = КоличествоПараметров - 1;
					
				ИначеЕсли ТекущаяСтрокаПараметра.ТипПараметраБлокаСведений = ТипыПараметровБлоковСведений.ОсновнойПоУслугам Тогда
					
					НайденныеСтрокиПоУслугам = ТаблицаСведенийПоУслугам.НайтиСтроки(Отбор);
					
					Если НЕ НайденныеСтрокиПоУслугам.Количество() = 0 Тогда
						ТекущаяСтрокаСведенийПоУслугам = НайденныеСтрокиПоУслугам[0];
						Для Каждого ТекущаяКолонкаУслуг Из врТаблицаСведенийПоУслугам.Колонки Цикл
							НоваяСтрока[ТекущаяКолонкаУслуг.Имя] = ТекущаяСтрокаСведенийПоУслугам[ТекущаяКолонкаУслуг.Имя];
						КонецЦикла;
					КонецЕсли;
					
					КоличествоПараметров = КоличествоПараметров - 1;
					
				КонецЕсли;
				
			Иначе
				
				ИмяПараметра = XMLстрока(ТекущаяСтрокаПараметра.Параметр);
				
				ИмяКолонки = ПолучитьИмяКолонки(ИмяПараметра, "", "", ПараметрыВыгрузки, ТекущаяСтрокаПараметра.ИмяПоляDBF);
				
				Если НЕ ТекущаяСтрокаПараметра.Параметр = ПараметрыУниверсальнойВыгрузкиДанных.ПустоеПоле Тогда
					
					НоваяСтрока[ИмяКолонки] = ПолучитьЗначениеКолонки(ПараметрыВыгрузки, ТекущаяСтрокаЛицевогоСчета, ТекущаяСтрокаПараметра, СтруктураОбщихТаблиц);
					
				КонецЕсли;
				
				Если ИспользоватьРезделителиКолонок И НЕ СчПараметров = КоличествоПараметров Тогда
					
					ИмяКолонкиРазделителя = ИмяРазделителя + СчПараметров;
					
					НоваяСтрока[ИмяКолонкиРазделителя] = РазделительКолонок;
					
					СчПараметров = СчПараметров + 1;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруОбщихТаблиц(ПараметрыВыгрузки)
	
	СтруктураОбщихТаблиц = Новый Структура;
	
	// Основные данные в базе по настройкам выгрузки.
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачалоПериода", ПараметрыВыгрузки.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  ПараметрыВыгрузки.КонецПериода);
	Запрос.УстановитьПараметр("Организация",   ПараметрыВыгрузки.Организация);
	
	Запрос = ПодготовитьОсновныеДанныеПоЛицевымСчетамВЗапросе(Запрос, ПараметрыВыгрузки);
	
	СтруктураОбщихТаблиц.Вставить("ТаблицаДолги",             ПолучитьТаблицуДолговПоЛицевымСчетам(Запрос, ПараметрыВыгрузки));
	СтруктураОбщихТаблиц.Вставить("ТаблицаКонтактныеДанные",  ПолучитьТаблицуКонтактныхДанных(Запрос, ПараметрыВыгрузки));
	
	// Проверяем наличие данных по лицевым счетам после установок отборов.
	ТаблицаЛицевыеСчета = ПолучитьТаблицуЛицевыхСчетов(Запрос);
	Если ТаблицаЛицевыеСчета.Количество() = 0 Тогда
		УПЖКХ_ТиповыеМетодыКлиентСервер.СообщитьОбОшибке("Нет данных для выгрузки.");
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураОбщихТаблиц.Вставить("ТаблицаЛицевыеСчета",        ТаблицаЛицевыеСчета);
	СтруктураОбщихТаблиц.Вставить("ТаблицаНачислений",          ПолучитьТаблицуНачислений(Запрос, ПараметрыВыгрузки));
	СтруктураОбщихТаблиц.Вставить("ТаблицаПлощадей",            ПолучитьТаблицуПлощадей(Запрос, ПараметрыВыгрузки));
	
	СтруктураОбщихТаблиц.Вставить("ТаблицаСведенийПоСчетчикам", ПолучитьТаблицуПоСчетчикам(Запрос, ПараметрыВыгрузки));
	СтруктураОбщихТаблиц.Вставить("ТаблицаСведенийПоУслугам",   ПолучитьТаблицуДолговПоЛицевымСчетамВРазрезеУслуг(Запрос, ПараметрыВыгрузки, СтруктураОбщихТаблиц.ТаблицаДолги));
	
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
	Возврат СтруктураОбщихТаблиц;
	
КонецФункции

&НаСервереБезКонтекста
// Возвращает таблицу показаний счетчиков.
//
// Параметры:
//  Запрос	 - результат запроса менеджера временных таблиц.
// 
// Возвращаемое значение:
//   Таблица значений.
//
Функция ПолучитьТаблицуПоСчетчикам(Запрос, ПараметрыВыгрузки)
	
	Таблица = Неопределено;
	
	Если НЕ ПараметрыВыгрузки.ВыгружатьСведенияПоСчетчикам Тогда
		Возврат Таблица;
	КонецЕсли;
	
	//Параметр "ТипВыгрузкиДанныхПоПоказаниямСчетчиков" принимает следующие значения:
	//	0	- "Только по счетчикам с показаниями";
	//	1	- "По всем счетчикам".
	
	ТипВыгрузкиДанныхПоПоказаниямСчетчиков = ПараметрыВыгрузки.ТипВыгрузкиДанныхПоПоказаниямСчетчиков;
	ВыгружатьПоказания                     = ПараметрыВыгрузки.ВыгружатьПоказания;
	ВыборУслуг                             = ПараметрыВыгрузки.ВыборУслуг;
	ВыбранныеУслуги                        = ПараметрыВыгрузки.ВыбранныеУслуги;
	ПараметрыБлоковСведений                = Перечисления.УПЖКХ_ПараметрыБлоковСведенийУниверсальнойВыгрузкиДанных;
	ТаблицаПараметровСведенийПоСчетчикам   = ПараметрыВыгрузки.ТаблицаПараметровСведенийПоСчетчикам;
	
	// Сведения по счетчикам из базы.
	
	СведенияПоДанным = ПолучитьСведенияПоСчетчикамИзБазы(Запрос, ПараметрыВыгрузки);
	
	// Получим сведения по количеству полей в блоке по счетчикам.
	КоличествоПолейПоПараметрам = 0;
	
	Если СведенияПоДанным.Количество() = 1 Тогда
		КоличествоПолейПоПараметрам           = СведенияПоДанным[0].КоличествоПолейПоПараметрам;
		МаксКоличествоСведенийВБлокеСчетчиков = ПараметрыВыгрузки.МаксКоличествоСведенийВБлокеСчетчиков;
		Если НЕ МаксКоличествоСведенийВБлокеСчетчиков = 0 И КоличествоПолейПоПараметрам >= МаксКоличествоСведенийВБлокеСчетчиков Тогда
			КоличествоПолейПоПараметрам = МаксКоличествоСведенийВБлокеСчетчиков;
		КонецЕсли;
	КонецЕсли;
	
	// Производим подбор параметров согласно составу параметров в блоке по счетчикам.
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	
	ЗапросВыбора    = СхемаЗапроса.ПакетЗапросов.Добавить();
	ОператорВыбрать = ЗапросВыбора.Операторы[0];
	
	ОператорВыбрать.ВыбиратьРазличные = Истина;
	ОператорВыбрать.Источники.Добавить("врПоказания","врПоказания");
	
	ОператорВыбрать.ВыбираемыеПоля.Добавить("ЛицевойСчет");
	ОператорВыбрать.ВыбираемыеПоля.Добавить("Счетчик");
	
	Для Каждого ТекущаяСтрока Из ТаблицаПараметровСведенийПоСчетчикам Цикл
		
		ТекущийПараметр = ТекущаяСтрока.ПараметрБлокаСведений;
		Псевдоним       = XMLстрока(ТекущийПараметр);
		
		Если ТекущийПараметр = ПараметрыБлоковСведений.Код Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("Счетчик.Код")).Псевдоним = Псевдоним;
			
		ИначеЕсли ТекущийПараметр = ПараметрыБлоковСведений.Наименование Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("Счетчик.Наименование")).Псевдоним = Псевдоним;
			
		ИначеЕсли ТекущийПараметр = ПараметрыБлоковСведений.ЗаводскойНомер Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("Счетчик.ЗаводскойНомер")).Псевдоним = Псевдоним;
			
		ИначеЕсли ТекущийПараметр = ПараметрыБлоковСведений.Идентификатор Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("Счетчик.Идентификатор")).Псевдоним = Псевдоним;
			
		ИначеЕсли ТекущийПараметр = ПараметрыБлоковСведений.НомерСчетчикаВГИСЖКХ Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("Счетчик.НомерВГИСЖКХ")).Псевдоним = Псевдоним;
			
		ИначеЕсли ТекущийПараметр = ПараметрыБлоковСведений.Показание Тогда
			
			ОператорВыбрать.ВыбираемыеПоля.Добавить("Тарифность");
			ОператорВыбрать.ВыбираемыеПоля.Добавить("ДневноеПоказание");
			ОператорВыбрать.ВыбираемыеПоля.Добавить("НочноеПоказание");
			ОператорВыбрать.ВыбираемыеПоля.Добавить("ПиковоеПоказание");
			
			ЗапросВыбора.Порядок.Добавить("Тарифность");
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОператорВыбрать.ВыбиратьРазличные = Истина;
	
	// Упорядочикание по ссылке на лицевой счет.
	ЗапросВыбора.Порядок.Добавить("ЛицевойСчет");
	
	Запрос.Текст = ЗапросВыбора.ПолучитьТекстЗапроса();
	
	ТаблицаПоСчетчикам = Запрос.Выполнить().Выгрузить();
	
	Если НЕ ТаблицаПоСчетчикам.Количество() = 0 Тогда
		
		ЗаполнитьТаблицуСведенийПоСчетчикам(Таблица, ПараметрыВыгрузки, ТаблицаПоСчетчикам, КоличествоПолейПоПараметрам);
		
		// Удалим ненужные временные таблицы в общем запросе.
		Запрос.Текст =
		"УНИЧТОЖИТЬ врПоказания";
		Запрос.Выполнить();
		
	Иначе
		
		ПараметрыВыгрузки.ВыгружатьСведенияПоСчетчикам = Ложь;
		
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСведенияПоСчетчикамИзБазы(Запрос, ПараметрыВыгрузки)
	
	ВыгружатьПоказания = ПараметрыВыгрузки.ВыгружатьПоказания;
	ВыборУслуг         = ПараметрыВыгрузки.ВыборУслуг;
	ВыбранныеУслуги    = ПараметрыВыгрузки.ВыбранныеУслуги;
	
	Запрос.УстановитьПараметр("ВыгружатьПоказания",                     ПараметрыВыгрузки.ВыгружатьПоказания);
	Запрос.УстановитьПараметр("ТипВыгрузкиДанныхПоПоказаниямСчетчиков", ПараметрыВыгрузки.ТипВыгрузкиДанныхПоПоказаниямСчетчиков);
	
	// Закрепленные счетчики.
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	КВП_ЗакрепленныеСчетчикиСрезПоследних.Объект КАК Объект,
	|	КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик КАК Счетчик,
	|	ВЫБОР
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность = ЗНАЧЕНИЕ(Перечисление.КВП_ТарифностьСчетчиков.Однотарифный)
	|			ТОГДА 1
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность = ЗНАЧЕНИЕ(Перечисление.КВП_ТарифностьСчетчиков.Двухтарифный)
	|			ТОГДА 2
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность = ЗНАЧЕНИЕ(Перечисление.КВП_ТарифностьСчетчиков.Трехтарифный)
	|			ТОГДА 3
	|	КОНЕЦ КАК Тарифность,
	|	0 КАК ДневноеПоказание,
	|	0 КАК НочноеПоказание,
	|	0 КАК ПиковоеПоказание
	|ПОМЕСТИТЬ врТаблицаСчетчиков
	|ИЗ
	|	РегистрСведений.КВП_ЗакрепленныеСчетчики.СрезПоследних(
	|			&КонецПериода,
	|			Объект ССЫЛКА Справочник.КВП_ЛицевыеСчета
	|				И Объект В
	|					(ВЫБРАТЬ
	|						врТаблицаЛицевыеСчета.ЛицевойСчет
	|					ИЗ
	|						врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)
	|				И ДатаИзменения < &КонецПериода
	|				И &УсловиеПоУслугам) КАК КВП_ЗакрепленныеСчетчикиСрезПоследних
	|ГДЕ
	|	КВП_ЗакрепленныеСчетчикиСрезПоследних.Действует
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КВП_ЗакрепленныеСчетчикиСрезПоследних.Объект,
	|	КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик,
	|	ВЫБОР
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность = ЗНАЧЕНИЕ(Перечисление.КВП_ТарифностьСчетчиков.Однотарифный)
	|			ТОГДА 1
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность = ЗНАЧЕНИЕ(Перечисление.КВП_ТарифностьСчетчиков.Двухтарифный)
	|			ТОГДА 2
	|		КОГДА КВП_ЗакрепленныеСчетчикиСрезПоследних.Счетчик.Тарифность = ЗНАЧЕНИЕ(Перечисление.КВП_ТарифностьСчетчиков.Трехтарифный)
	|			ТОГДА 3
	|	КОНЕЦ,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	РегистрСведений.КВП_ЗакрепленныеСчетчики.СрезПоследних(
	|			&КонецПериода,
	|			Объект ССЫЛКА Справочник.УПЖКХ_Помещения
	|				И Объект В
	|					(ВЫБРАТЬ
	|						врТаблицаЛицевыеСчета.Помещение
	|					ИЗ
	|						врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета)
	|				И ДатаИзменения < &КонецПериода
	|				И &УсловиеПоУслугам) КАК КВП_ЗакрепленныеСчетчикиСрезПоследних
	|ГДЕ
	|	КВП_ЗакрепленныеСчетчикиСрезПоследних.Действует
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	Счетчик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	врТаблицаЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет,
	|	врТаблицаСчетчиков.Счетчик КАК Счетчик,
	|	врТаблицаСчетчиков.ДневноеПоказание КАК ДневноеПоказание,
	|	врТаблицаСчетчиков.НочноеПоказание КАК НочноеПоказание,
	|	врТаблицаСчетчиков.ПиковоеПоказание КАК ПиковоеПоказание,
	|	врТаблицаСчетчиков.Тарифность КАК Тарифность
	|ПОМЕСТИТЬ врТаблицаЗакрепленныхСчетчиков
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врТаблицаСчетчиков КАК врТаблицаСчетчиков
	|		ПО врТаблицаЛицевыеСчета.ЛицевойСчет = врТаблицаСчетчиков.Объект
	|ГДЕ
	|	врТаблицаСчетчиков.Объект ССЫЛКА Справочник.КВП_ЛицевыеСчета
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	врТаблицаЛицевыеСчета.ЛицевойСчет,
	|	врТаблицаСчетчиков.Счетчик,
	|	врТаблицаСчетчиков.ДневноеПоказание,
	|	врТаблицаСчетчиков.НочноеПоказание,
	|	врТаблицаСчетчиков.ПиковоеПоказание,
	|	врТаблицаСчетчиков.Тарифность
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врТаблицаСчетчиков КАК врТаблицаСчетчиков
	|		ПО врТаблицаЛицевыеСчета.Помещение = врТаблицаСчетчиков.Объект
	|ГДЕ
	|	врТаблицаСчетчиков.Объект ССЫЛКА Справочник.УПЖКХ_Помещения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаСчетчиков";
	
	//Параметр "ВыборУслуг" принимает следующие значения:
	//	0	- "Всем услугам";
	//	1	- "Выбранным услугам";
	//	2	- "Всем услугам, кроме выбранных".
	
	Если ВыборУслуг = 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("СписокУслуг", ВыбранныеУслуги);
		
		Если ВыборУслуг = 1 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "Счетчик.ВидУслуги В (&СписокУслуг)");
		ИначеЕсли ВыборУслуг = 2 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоУслугам", "НЕ Счетчик.ВидУслуги В (&СписокУслуг)");
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Выполнить();
	
	// Показания.
	
	Если ВыгружатьПоказания Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КВП_ПоказанияСчетчиковСрезПоследних.Счетчик КАК Счетчик,
		|	КВП_ПоказанияСчетчиковСрезПоследних.ДневноеПоказание КАК ДневноеПоказание,
		|	КВП_ПоказанияСчетчиковСрезПоследних.НочноеПоказание КАК НочноеПоказание,
		|	КВП_ПоказанияСчетчиковСрезПоследних.ПиковоеПоказание КАК ПиковоеПоказание
		|ПОМЕСТИТЬ ПоказанияСчетчиков
		|ИЗ
		|	РегистрСведений.КВП_ПоказанияСчетчиков.СрезПоследних(
		|			&КонецПериода,
		|			Счетчик В
		|				(ВЫБРАТЬ
		|					врТаблицаЗакрепленныхСчетчиков.Счетчик
		|				ИЗ
		|					врТаблицаЗакрепленныхСчетчиков КАК врТаблицаЗакрепленныхСчетчиков)) КАК КВП_ПоказанияСчетчиковСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счетчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	врТаблицаЗакрепленныхСчетчиков.ЛицевойСчет КАК ЛицевойСчет,
		|	врТаблицаЗакрепленныхСчетчиков.Счетчик КАК Счетчик,
		|	врТаблицаЗакрепленныхСчетчиков.Тарифность КАК Тарифность,
		|	ЕСТЬNULL(ПоказанияСчетчиков.ДневноеПоказание, 0) КАК ДневноеПоказание,
		|	ЕСТЬNULL(ПоказанияСчетчиков.НочноеПоказание, 0) КАК НочноеПоказание,
		|	ЕСТЬNULL(ПоказанияСчетчиков.ПиковоеПоказание, 0) КАК ПиковоеПоказание
		|ПОМЕСТИТЬ втОбъектыСПоказаниямиПУ
		|ИЗ
		|	врТаблицаЗакрепленныхСчетчиков КАК врТаблицаЗакрепленныхСчетчиков
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПоказанияСчетчиков КАК ПоказанияСчетчиков
		|		ПО врТаблицаЗакрепленныхСчетчиков.Счетчик = ПоказанияСчетчиков.Счетчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ врТаблицаЗакрепленныхСчетчиков
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПоказанияСчетчиков
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОбъектыСПоказаниямиПУ.ЛицевойСчет КАК ЛицевойСчет,
		|	втОбъектыСПоказаниямиПУ.Счетчик КАК Счетчик,
		|	втОбъектыСПоказаниямиПУ.Тарифность КАК Тарифность,
		|	втОбъектыСПоказаниямиПУ.ДневноеПоказание КАК ДневноеПоказание,
		|	втОбъектыСПоказаниямиПУ.НочноеПоказание КАК НочноеПоказание,
		|	втОбъектыСПоказаниямиПУ.ПиковоеПоказание КАК ПиковоеПоказание
		|ПОМЕСТИТЬ врТаблицаЗакрепленныхСчетчиков
		|ИЗ
		|	втОбъектыСПоказаниямиПУ КАК втОбъектыСПоказаниямиПУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втОбъектыСПоказаниямиПУ";
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	врТаблицаЗакрепленныхСчетчиков.ЛицевойСчет КАК ЛицевойСчет,
	|	ВЫРАЗИТЬ(врТаблицаЗакрепленныхСчетчиков.Счетчик КАК Справочник.КВП_Счетчики) КАК Счетчик,
	|	врТаблицаЗакрепленныхСчетчиков.ДневноеПоказание КАК ДневноеПоказание,
	|	врТаблицаЗакрепленныхСчетчиков.НочноеПоказание КАК НочноеПоказание,
	|	врТаблицаЗакрепленныхСчетчиков.ПиковоеПоказание КАК ПиковоеПоказание,
	|	врТаблицаЗакрепленныхСчетчиков.Тарифность КАК Тарифность
	|ПОМЕСТИТЬ врПоказания
	|ИЗ
	|	врТаблицаЗакрепленныхСчетчиков КАК врТаблицаЗакрепленныхСчетчиков
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ТипВыгрузкиДанныхПоПоказаниямСчетчиков = 0
	|				ТОГДА НЕ(врТаблицаЗакрепленныхСчетчиков.ДневноеПоказание = 0
	|							И врТаблицаЗакрепленныхСчетчиков.НочноеПоказание = 0
	|							И врТаблицаЗакрепленныхСчетчиков.ПиковоеПоказание = 0)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаЗакрепленныхСчетчиков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоПолейПоПараметрам) КАК КоличествоПолейПоПараметрам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.ЛицевойСчет КАК ЛицевойСчет,
	|		ВЫБОР
	|			КОГДА &ВыгружатьПоказания
	|				ТОГДА СУММА(ВложенныйЗапрос.Тарифность)
	|			ИНАЧЕ КОЛИЧЕСТВО(ВложенныйЗапрос.Счетчик)
	|		КОНЕЦ КАК КоличествоПолейПоПараметрам
	|	ИЗ
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			врПоказания.ЛицевойСчет КАК ЛицевойСчет,
	|			врПоказания.Счетчик КАК Счетчик,
	|			врПоказания.Тарифность КАК Тарифность
	|		ИЗ
	|			врПоказания КАК врПоказания) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.ЛицевойСчет) КАК ВложенныйЗапрос";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьТаблицуСведенийПоСчетчикам(Таблица, ПараметрыВыгрузки, ТаблицаПоСчетчикам, КоличествоПолейПоПараметрам)
	
	ПараметрыБлоковСведений                = Перечисления.УПЖКХ_ПараметрыБлоковСведенийУниверсальнойВыгрузкиДанных;
	ВыгружатьПоказания                     = ПараметрыВыгрузки.ВыгружатьПоказания;
	ТаблицаПараметровСведенийПоСчетчикам   = ПараметрыВыгрузки.ТаблицаПараметровСведенийПоСчетчикам;
	КоличествоПараметров                   = ТаблицаПараметровСведенийПоСчетчикам.Количество();
	ЭтоDBF                                 = ПараметрыВыгрузки.ЭтоDBF;
	
	// Создаем структуру таблицы по счетчикам.
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ЛицевойСчет", Новый ОписаниеТипов("СправочникСсылка.КВП_ЛицевыеСчета"));
	
	СчBorder = 0;
	
	ИмяРазделителя = "PuBorder";
	
	Для Сч = 1 По КоличествоПолейПоПараметрам Цикл
		Для Каждого ТекущаяСтрока Из ТаблицаПараметровСведенийПоСчетчикам Цикл
			ИмяПараметра = XMLстрока(ТекущаяСтрока.ПараметрБлокаСведений);
			ИмяКолонки   = ПолучитьИмяКолонки(ИмяПараметра, "Pu", Сч, ПараметрыВыгрузки, ТекущаяСтрока.ИмяПоляDBF);
			
			Таблица.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
			
			Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
				СчBorder = СчBorder + 1;
				ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
				Таблица.Колонки.Добавить(ИмяКолонкиРазделителя, Новый ОписаниеТипов("Строка"));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	КолонкиТаблицы    = Таблица.Колонки;
	КолонкиДанныхПоЛС = ТаблицаПоСчетчикам.Колонки;
	
	ТаблицаЛС = ТаблицаПоСчетчикам.Скопировать(, "ЛицевойСчет");
	ТаблицаЛС.Свернуть("ЛицевойСчет");
	массивЛС = ТаблицаЛС.ВыгрузитьКолонку("ЛицевойСчет");
	
	Для Каждого ТекущийЛС Из массивЛС Цикл
		
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.ЛицевойСчет = ТекущийЛС;
		
		ДанныеПоЛС = ТаблицаПоСчетчикам.НайтиСтроки(Новый Структура("ЛицевойСчет", ТекущийЛС));
		
		Если ВыгружатьПоказания Тогда
			врДанныеПоЛС = ТаблицаПоСчетчикам.Скопировать(ДанныеПоЛС);
			КоличествоПолейПоДаннымВБазе = врДанныеПоЛС.Итог("Тарифность");
			Если КоличествоПолейПоДаннымВБазе > КоличествоПолейПоПараметрам Тогда
				КоличествоПолейПоДаннымВБазе = КоличествоПолейПоПараметрам;
			КонецЕсли;
		Иначе
			КоличествоПолейПоДаннымВБазе = КоличествоПолейПоПараметрам;
		КонецЕсли;
		
		СчBorder = 0;
		СчДанных = 0;
		
		Для Каждого ТекущаяСтрокаДанных Из ДанныеПоЛС Цикл
			Если ВыгружатьПоказания Тогда
				ТекущаяТарифность = ТекущаяСтрокаДанных.Тарифность;
				
				Для Тарифность = 1 По ТекущаяТарифность Цикл
					
					СчДанных = СчДанных + 1;
					
					Если СчДанных > КоличествоПолейПоДаннымВБазе Тогда
						Прервать;
					КонецЕсли;
					
					СчПараметров = 0;
					
					Для Каждого ТекущаяСтрокаПараметра Из ТаблицаПараметровСведенийПоСчетчикам Цикл
						СчПараметров    = СчПараметров + 1;
						ТекущийПараметр = ТекущаяСтрокаПараметра.ПараметрБлокаСведений;
						ИмяПараметра    = XMLстрока(ТекущийПараметр);
						
						Если ТекущийПараметр = ПараметрыБлоковСведений.Показание Тогда
							
							Если Тарифность = 1 Тогда
								// Дневное показание.
								ИмяКолонки = ПолучитьИмяКолонки(ИмяПараметра, "Pu", СчДанных, ПараметрыВыгрузки, ТекущаяСтрокаПараметра.ИмяПоляDBF);
								КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонки, КолонкиДанныхПоЛС, "ДневноеПоказание");
								НоваяСтрока[ИмяКолонки] = Формат(ТекущаяСтрокаДанных["ДневноеПоказание"], "ЧГ=");
								
								Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
									СчBorder              = СчBorder + 1;
									ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
									КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонкиРазделителя);
									РазделительПослеЗначения = ТекущаяСтрокаПараметра.РазделительПослеЗначения;
									
									Если НЕ ПараметрыВыгрузки.ЭтоCSV
									   И СчПараметров = КоличествоПараметров И СчДанных = КоличествоПолейПоДаннымВБазе И СчПараметров = КоличествоПараметров Тогда
										Если НЕ ПараметрыВыгрузки.ВыгружатьРазделительПослеБлокаСчетчики Тогда
											РазделительПослеЗначения = "";
										ИначеЕсли НЕ ПустаяСтрока(ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам) Тогда
											РазделительПослеЗначения = ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам;
										КонецЕсли;
									КонецЕсли;
									
									НоваяСтрока[ИмяКолонкиРазделителя] = РазделительПослеЗначения;
								КонецЕсли;
								
							ИначеЕсли Тарифность = 2 Тогда
								// Ночное показание.
								ИмяКолонки = ПолучитьИмяКолонки(ИмяПараметра, "Pu", СчДанных, ПараметрыВыгрузки, ТекущаяСтрокаПараметра.ИмяПоляDBF);
								КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонки, КолонкиДанныхПоЛС, "НочноеПоказание");
								НоваяСтрока[ИмяКолонки] = Формат(ТекущаяСтрокаДанных["НочноеПоказание"], "ЧГ=");
								
								Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
									СчBorder = СчBorder + 1;
									ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
									КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонкиРазделителя);
									РазделительПослеЗначения = ТекущаяСтрокаПараметра.РазделительПослеЗначения;
									
									Если НЕ ПараметрыВыгрузки.ЭтоCSV
									   И СчПараметров = КоличествоПараметров И СчДанных = КоличествоПолейПоДаннымВБазе И СчПараметров = КоличествоПараметров Тогда
										Если НЕ ПараметрыВыгрузки.ВыгружатьРазделительПослеБлокаСчетчики Тогда
											РазделительПослеЗначения = "";
										ИначеЕсли НЕ ПустаяСтрока(ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам) Тогда
											РазделительПослеЗначения = ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам;
										КонецЕсли;
									КонецЕсли;
									
									НоваяСтрока[ИмяКолонкиРазделителя] = РазделительПослеЗначения;
								КонецЕсли;
								
							ИначеЕсли Тарифность = 3 Тогда
								// Пиковое показание.
								ИмяКолонки = ПолучитьИмяКолонки(ИмяПараметра, "Pu", СчДанных, ПараметрыВыгрузки, ТекущаяСтрокаПараметра.ИмяПоляDBF);
								КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонки, КолонкиДанныхПоЛС, "ПиковоеПоказание");
								НоваяСтрока[ИмяКолонки] = Формат(ТекущаяСтрокаДанных["ПиковоеПоказание"], "ЧГ=");
								
								Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
									СчBorder = СчBorder + 1;
									ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
									КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонкиРазделителя);
									РазделительПослеЗначения = ТекущаяСтрокаПараметра.РазделительПослеЗначения;
									
									Если НЕ ПараметрыВыгрузки.ЭтоCSV
									   И СчПараметров = КоличествоПараметров И СчДанных = КоличествоПолейПоДаннымВБазе И СчПараметров = КоличествоПараметров Тогда
										Если НЕ ПараметрыВыгрузки.ВыгружатьРазделительПослеБлокаСчетчики Тогда
											РазделительПослеЗначения = "";
										ИначеЕсли НЕ ПустаяСтрока(ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам) Тогда
											РазделительПослеЗначения = ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам;
										КонецЕсли;
									КонецЕсли;
									
									НоваяСтрока[ИмяКолонкиРазделителя] = РазделительПослеЗначения;
								КонецЕсли;
								
							КонецЕсли;
							
						Иначе
							
							Если НЕ ТекущийПараметр = ПараметрыБлоковСведений.ПустоеПоле Тогда
								ИмяКолонки = ПолучитьИмяКолонки(ИмяПараметра, "Pu", СчДанных, ПараметрыВыгрузки, ТекущаяСтрокаПараметра.ИмяПоляDBF);
								КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонки, КолонкиДанныхПоЛС, ИмяПараметра);
								НоваяСтрока[ИмяКолонки] = ТекущаяСтрокаДанных[ИмяПараметра];
								
								Постфикс = "";
								Если ТекущийПараметр = ПараметрыБлоковСведений.Наименование Тогда
									Если НЕ ТекущаяТарифность = 1 Тогда
										Если Тарифность = 1 Тогда
											Постфикс = " ДЕНЬ";
										ИначеЕсли Тарифность = 2 Тогда
											Постфикс = " НОЧЬ";
										ИначеЕсли Тарифность = 3 Тогда
											Постфикс = " ПИК";
										КонецЕсли;
									КонецЕсли;
								КонецЕсли;
								
								НоваяСтрока[ИмяКолонки] = НоваяСтрока[ИмяКолонки] + Постфикс;
							КонецЕсли;
							
							Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
								СчBorder = СчBorder + 1;
								ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
								КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонкиРазделителя);
								РазделительПослеЗначения = ТекущаяСтрокаПараметра.РазделительПослеЗначения;
								
								Если НЕ ПараметрыВыгрузки.ЭтоCSV И СчПараметров = КоличествоПараметров И СчДанных = КоличествоПолейПоДаннымВБазе Тогда
									//Если НЕ ПараметрыВыгрузки.ВыгружатьРазделительПослеБлокаСчетчики Тогда
									//	РазделительПослеЗначения = "";
									//Иначе
									Если НЕ ПустаяСтрока(ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам) Тогда
										РазделительПослеЗначения = ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам;
									КонецЕсли;
								КонецЕсли;
								
								НоваяСтрока[ИмяКолонкиРазделителя] = РазделительПослеЗначения;
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			Иначе
				
				СчПараметров = 0;
				СчДанных     = СчДанных + 1;
				
				Если СчДанных > КоличествоПолейПоДаннымВБазе Тогда
					Прервать;
				КонецЕсли;
				
				Для Каждого ТекущаяСтрокаПараметра Из ТаблицаПараметровСведенийПоСчетчикам Цикл
					
					СчПараметров    = СчПараметров + 1;
					ТекущийПараметр = ТекущаяСтрокаПараметра.ПараметрБлокаСведений;
					
					Если НЕ ТекущийПараметр = ПараметрыБлоковСведений.ПустоеПоле Тогда
						ИмяПараметра = XMLстрока(ТекущийПараметр);
						ИмяКолонки = ПолучитьИмяКолонки(ИмяПараметра, "Pu", СчДанных, ПараметрыВыгрузки, ТекущаяСтрокаПараметра.ИмяПоляDBF);
						КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонки, КолонкиДанныхПоЛС, ИмяПараметра);
						НоваяСтрока[ИмяКолонки] = ТекущаяСтрокаДанных[ИмяПараметра];
					КонецЕсли;
					
					Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
						СчBorder = СчBorder + 1;
						ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
						КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонкиРазделителя);
						РазделительПослеЗначения = ТекущаяСтрокаПараметра.РазделительПослеЗначения;
						
						Если НЕ ПараметрыВыгрузки.ЭтоCSV И СчПараметров = КоличествоПараметров И СчДанных = КоличествоПолейПоДаннымВБазе Тогда
							Если НЕ ПараметрыВыгрузки.ВыгружатьРазделительПослеБлокаСчетчики Тогда
								РазделительПослеЗначения = "";
							ИначеЕсли НЕ ПустаяСтрока(ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам) Тогда
								РазделительПослеЗначения = ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоСчетчикам;
							КонецЕсли;
						КонецЕсли;
						
						НоваяСтрока[ИмяКолонкиРазделителя] = РазделительПослеЗначения;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
// Возвращает таблицу долгов лицевых счетов на конец периода.
//
// Параметры:
//  Запрос	 - результат запроса менеджера временных таблиц.
// 
// Возвращаемое значение:
//   Таблица значений.
//
Функция ПолучитьТаблицуДолговПоЛицевымСчетамВРазрезеУслуг(Запрос, ПараметрыВыгрузки, ТаблицаДолги)
	
	Таблица = Неопределено;
	
	Если НЕ ПараметрыВыгрузки.ВыгружатьСведенияПоУслугам
	 ИЛИ ТаблицаДолги = Неопределено Тогда
		Возврат Таблица;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	втОстаткиПоЛицевымСчетамИУслугам.ЛицевойСчет КАК ЛицевойСчет,
	|	ВЫРАЗИТЬ(втОстаткиПоЛицевымСчетамИУслугам.Услуга КАК Справочник.КВП_Услуги) КАК Услуга,
	|	втОстаткиПоЛицевымСчетамИУслугам.СуммаДолга КАК СуммаДолга
	|ПОМЕСТИТЬ врТаблицаДолгов
	|ИЗ
	|	втОстаткиПоЛицевымСчетамИУслугам КАК втОстаткиПоЛицевымСчетамИУслугам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО втОстаткиПоЛицевымСчетамИУслугам.ЛицевойСчет = втОстатки.ЛицевойСчет
	|ГДЕ
	|	НЕ втОстаткиПоЛицевымСчетамИУслугам.СуммаДолга = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет,
	|	ЕСТЬNULL(врТаблицаДолгов.Услуга, ЗНАЧЕНИЕ(Справочник.КВП_Услуги.ПустаяСсылка)) КАК Услуга,
	|	ЕСТЬNULL(врТаблицаДолгов.СуммаДолга, 0) КАК СуммаДолга
	|ПОМЕСТИТЬ врТаблицаДолговПоУслугам
	|ИЗ
	|	врТаблицаЛицевыеСчета КАК врТаблицаЛицевыеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ врТаблицаДолгов КАК врТаблицаДолгов
	|		ПО врТаблицаЛицевыеСчета.ЛицевойСчет = врТаблицаДолгов.ЛицевойСчет
	|ГДЕ
	|	НЕ врТаблицаДолгов.СуммаДолга = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врТаблицаДолгов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОстаткиПоЛицевымСчетамИУслугам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоПолейПоПараметрам) КАК КоличествоПолейПоПараметрам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.ЛицевойСчет КАК ЛицевойСчет,
	|		КОЛИЧЕСТВО(ВложенныйЗапрос.Услуга) КАК КоличествоПолейПоПараметрам
	|	ИЗ
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			врТаблицаДолговПоУслугам.ЛицевойСчет КАК ЛицевойСчет,
	|			врТаблицаДолговПоУслугам.Услуга КАК Услуга
	|		ИЗ
	|			врТаблицаДолговПоУслугам КАК врТаблицаДолговПоУслугам) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.ЛицевойСчет) КАК ВложенныйЗапрос";
	
	СведенияПоДанным = Запрос.Выполнить().Выгрузить();
	
	ПараметрыБлоковСведений            = Перечисления.УПЖКХ_ПараметрыБлоковСведенийУниверсальнойВыгрузкиДанных;
	ТаблицаПараметровСведенийПоУслугам = ПараметрыВыгрузки.ТаблицаПараметровСведенийПоУслугам;
	КоличествоПолейПоПараметрам        = 0;
	МаксКоличествоСведенийВБлокеУслуг  = ПараметрыВыгрузки.МаксКоличествоСведенийВБлокеУслуг;
	
	Если СведенияПоДанным.Количество() = 1 Тогда
		КоличествоПолейПоПараметрам = СведенияПоДанным[0].КоличествоПолейПоПараметрам;
		
		Если КоличествоПолейПоПараметрам >= МаксКоличествоСведенийВБлокеУслуг И НЕ МаксКоличествоСведенийВБлокеУслуг = 0 Тогда
			КоличествоПолейПоПараметрам = МаксКоличествоСведенийВБлокеУслуг;
		КонецЕсли;
	КонецЕсли;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	
	ЗапросВыбора    = СхемаЗапроса.ПакетЗапросов.Добавить();
	ОператорВыбрать = ЗапросВыбора.Операторы[0];
	ОператорВыбрать.Источники.Добавить("врТаблицаДолговПоУслугам","врТаблицаДолговПоУслугам");
	
	ОператорВыбрать.ВыбираемыеПоля.Добавить("врТаблицаДолговПоУслугам.ЛицевойСчет");
	
	Для Каждого ТекущаяСтрока Из ТаблицаПараметровСведенийПоУслугам Цикл
		
		ТекущийПараметр = ТекущаяСтрока.ПараметрБлокаСведений;
		Псевдоним       = XMLстрока(ТекущийПараметр);
		
		Если ТекущийПараметр = ПараметрыБлоковСведений.СуммаДолга Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("СуммаДолга")).Псевдоним = Псевдоним;
			
		ИначеЕсли ТекущийПараметр = ПараметрыБлоковСведений.Код Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("Услуга.Код")).Псевдоним = Псевдоним;
			
		ИначеЕсли ТекущийПараметр = ПараметрыБлоковСведений.Наименование Тогда
			
			ЗапросВыбора.Колонки.Найти(ОператорВыбрать.ВыбираемыеПоля.Добавить("Услуга.Наименование")).Псевдоним = Псевдоним;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОператорВыбрать.ВыбиратьРазличные = Истина;
	
	// Упорядочикание по ссылке на лицевой счет.
	ЗапросВыбора.Порядок.Добавить("ЛицевойСчет");
	
	Запрос.Текст = ЗапросВыбора.ПолучитьТекстЗапроса();
	
	ТаблицаПоУслугам = Запрос.Выполнить().Выгрузить();
	
	Если НЕ ТаблицаПараметровСведенийПоУслугам.Найти(ПараметрыБлоковСведений.ПустоеПоле, "ПараметрБлокаСведений") = Неопределено Тогда
		ТаблицаПоУслугам.Колонки.Добавить("ПустоеПоле", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	
	Если ТаблицаПоУслугам.Количество() > 0 Тогда
		
		ЗаполнитьТаблицуСведенийПоУслугам(Таблица, ПараметрыВыгрузки, ТаблицаПоУслугам, КоличествоПолейПоПараметрам);
		
		// Удалим ненужные временные таблицы в общем запросе.
		Запрос.Текст =
		"УНИЧТОЖИТЬ врТаблицаДолговПоУслугам";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьТаблицуСведенийПоУслугам(Таблица, ПараметрыВыгрузки, ТаблицаПоУслугам, КоличествоПолейПоПараметрам)
	
	ПараметрыБлоковСведений            = Перечисления.УПЖКХ_ПараметрыБлоковСведенийУниверсальнойВыгрузкиДанных;
	ВыгружатьСуммыДолгаПоУслугам       = ПараметрыВыгрузки.ВыгружатьСуммыДолгаПоУслугам;
	ТаблицаПараметровСведенийПоУслугам = ПараметрыВыгрузки.ТаблицаПараметровСведенийПоУслугам;
	КоличествоПараметров               = ТаблицаПараметровСведенийПоУслугам.Количество();
	
	// Создаем структуру таблицы по услугам.
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ЛицевойСчет", Новый ОписаниеТипов("СправочникСсылка.КВП_ЛицевыеСчета"));
	
	ИмяРазделителя = "UsBorder";
	
	СчBorder = 0;
	ЭтоDBF   = ПараметрыВыгрузки.ЭтоDBF;
	
	Для Сч = 1 По КоличествоПолейПоПараметрам Цикл
		
		Для Каждого ТекущаяСтрока Из ТаблицаПараметровСведенийПоУслугам Цикл
			
			ИмяПараметра = XMLстрока(ТекущаяСтрока.ПараметрБлокаСведений);
			
			ИмяКолонки   = ПолучитьИмяКолонки(ИмяПараметра, "Us", Сч, ПараметрыВыгрузки, ТекущаяСтрока.ИмяПоляDBF);
			
			Таблица.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
			
			Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
				СчBorder = СчBorder + 1;
				ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
				Таблица.Колонки.Добавить(ИмяКолонкиРазделителя, Новый ОписаниеТипов("Строка"));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	КолонкиТаблицы    = Таблица.Колонки;
	КолонкиДанныхПоЛС = ТаблицаПоУслугам.Колонки;
	
	ТаблицаЛС = ТаблицаПоУслугам.Скопировать(, "ЛицевойСчет");
	ТаблицаЛС.Свернуть("ЛицевойСчет");
	массивЛС = ТаблицаЛС.ВыгрузитьКолонку("ЛицевойСчет");
	
	Для Каждого ТекущийЛС Из массивЛС Цикл
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.ЛицевойСчет = ТекущийЛС;
		
		ДанныеПоЛС = ТаблицаПоУслугам.НайтиСтроки(Новый Структура("ЛицевойСчет", ТекущийЛС));
		
		КоличествоПолейПоДаннымВБазе = КоличествоПолейПоПараметрам;
		
		СчBorder = 0;
		СчДанных = 0;
		
		Для Каждого ТекущаяСтрокаДанных Из ДанныеПоЛС Цикл
			СчДанных     = СчДанных + 1;
			СчПараметров = 0;
			
			Если СчДанных > КоличествоПолейПоПараметрам Тогда
				Прервать;
			КонецЕсли;
			
			Для Каждого ТекущаяСтрокаПараметра Из ТаблицаПараметровСведенийПоУслугам Цикл
				СчПараметров    = СчПараметров + 1;
				ТекущийПараметр = ТекущаяСтрокаПараметра.ПараметрБлокаСведений;
				ИмяПараметра    = XMLстрока(ТекущийПараметр);
				ИмяКолонки      = ПолучитьИмяКолонки(ИмяПараметра, "Us", СчДанных, ПараметрыВыгрузки, ТекущаяСтрокаПараметра.ИмяПоляDBF);
				
				КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонки, КолонкиДанныхПоЛС, ИмяПараметра);
				
				Значение = ТекущаяСтрокаДанных[ИмяПараметра];
				
				Если ТекущийПараметр = ПараметрыБлоковСведений.СуммаДолга Тогда
					Значение = Формат(Значение, "ЧДЦ=2; ЧГ=");
				КонецЕсли;
				
				НоваяСтрока[ИмяКолонки] = Значение;
				
				Если ПараметрыВыгрузки.ИспользоватьРезделителиКолонок Тогда
					СчBorder              = СчBorder + 1;
					ИмяКолонкиРазделителя = ИмяРазделителя + СчBorder;
					КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонкиРазделителя);
					
					РазделительПослеЗначения = ТекущаяСтрокаПараметра.РазделительПослеЗначения;
					
					Если НЕ ПараметрыВыгрузки.ЭтоCSV И СчДанных = КоличествоПолейПоДаннымВБазе И СчПараметров = КоличествоПараметров Тогда
						Если НЕ ПараметрыВыгрузки.ВыгружатьРазделительПослеБлокаУслуги Тогда
							РазделительПослеЗначения = "";
						ИначеЕсли НЕ ПустаяСтрока(ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоУслугам) Тогда
							РазделительПослеЗначения = ПараметрыВыгрузки.ФлагЗавершенияБлокаСведенийПоУслугам;
						КонецЕсли;
					КонецЕсли;
					
					НоваяСтрока[ИмяКолонкиРазделителя] = РазделительПослеЗначения;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КонтрольНаличияКолонок(КолонкиТаблицы, ИмяКолонки, КолонкиДанныхПоЛС = Неопределено, ИмяПараметра = "")
	
	Если КолонкиТаблицы.Найти(ИмяКолонки) = Неопределено
	 ИЛИ (НЕ КолонкиДанныхПоЛС = Неопределено И КолонкиДанныхПоЛС.Найти(ИмяПараметра) = Неопределено) Тогда
		ВызватьИсключение ОписаниеОшибки();
	КонецЕсли;
	
КонецПроцедуры

// Функция - Получить значение колонки
//
// Параметры:
//  Параметр					 - Ссылка	 - параметр универсальной выгрузки данных.
//  ПараметрыВыгрузки			 - Структура	 - структура настроек выгрузки.
//  ТекущаяСтрокаЛицевогоСчета	 - Строка таблицы значений	 - данные по лицевому счету.
//  ТекущаяСтрокаПараметра		 - Строка таблицы значений	 - настройки параметра универсальной выгрузки данных.
// 
// Возвращаемое значение:
//  Строка, Число.
//
&НаСервереБезКонтекста
Функция ПолучитьЗначениеКолонки(ПараметрыВыгрузки, ТекущаяСтрокаЛицевогоСчета, ТекущаяСтрокаПараметра, СтруктураОбщихТаблиц)
	
	Параметр = ТекущаяСтрокаПараметра.Параметр;
	
	СтруктураЗначенийПолейФорматаВыгрузки = ПараметрыВыгрузки.СтруктураЗначенийПолейФорматаВыгрузки;
	
	Если Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.ЛицевойСчет Тогда
		
		Если ТекущаяСтрокаПараметра.ИдентификацияЛС = СтруктураЗначенийПолейФорматаВыгрузки.СпИдентификацияЛС[0].Значение Тогда // "наименование"
			
			// Берем наименование лицевого счета.
			Значение = СтрЗаменить(ТекущаяСтрокаЛицевогоСчета.НаименованиеЛС, "л/с №", "");
			
		ИначеЕсли ТекущаяСтрокаПараметра.ИдентификацияЛС = СтруктураЗначенийПолейФорматаВыгрузки.СпИдентификацияЛС[2].Значение Тогда // "идентификатор"
			
			// Берем идентификатор лицевого счета.
			Значение = ТекущаяСтрокаЛицевогоСчета.ИдентификаторЛС;
			
		ИначеЕсли ТекущаяСтрокаПараметра.ИдентификацияЛС = СтруктураЗначенийПолейФорматаВыгрузки.СпИдентификацияЛС[1].Значение Тогда // "код"
			
			// Берем код лицевого счета
			Значение = ТекущаяСтрокаЛицевогоСчета.КодЛС;
			
		КонецЕсли;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.ФИООтветственногоСобственникаНанимателя Тогда
		
		Значение = ТекущаяСтрокаЛицевогоСчета.ОтветственныйВладелецПредставление;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Фамилия
		Или Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Имя
		Или Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Отчество Тогда
		
		Значение = ПолучитьЗначениеФИО(ТекущаяСтрокаЛицевогоСчета, Параметр);
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Площадь
			И СтруктураОбщихТаблиц.ТаблицаПлощадей <> Неопределено
			И СтруктураОбщихТаблиц.ТаблицаПлощадей.Количество() > 0 Тогда
		
		НайденнаяПлощадьСтрокаТаблицы = СтруктураОбщихТаблиц.ТаблицаПлощадей.Найти(ТекущаяСтрокаЛицевогоСчета.ЛицевойСчет, "ЛицевойСчет");
		
		Если НайденнаяПлощадьСтрокаТаблицы = Неопределено Тогда
			
			Значение = 0;
			
		Иначе
			
			Значение = Окр(НайденнаяПлощадьСтрокаТаблицы.Площадь, 2);
			
		КонецЕсли;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Адрес Тогда
		
		АдресДома = Справочники.КВП_Здания.ПолучитьПредставлениеЗдания(ТекущаяСтрокаЛицевогоСчета.Здание);
		
		Значение = ПолучитьАдресПомещения(АдресДома, ТекущаяСтрокаЛицевогоСчета.Помещение);
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.СуммаДолга Тогда
		
		СуммаДолга = 0;
		
		Если СтруктураОбщихТаблиц.ТаблицаДолги <> Неопределено Тогда
			
			НайденнаяСтрокаСуммыДолга = СтруктураОбщихТаблиц.ТаблицаДолги.Найти(ТекущаяСтрокаЛицевогоСчета.ЛицевойСчет, "ЛицевойСчет");
			
			Если НайденнаяСтрокаСуммыДолга <> Неопределено Тогда
				СуммаДолга = НайденнаяСтрокаСуммыДолга.СуммаДолга;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СуммаДолга = 0 Тогда
			
			Если ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти = СтруктураЗначенийПолейФорматаВыгрузки.СпРазделителейДробнойЧасти[3].Значение Тогда // "Без разделителя"
				СуммаДолга = "0";
			Иначе
				СуммаДолга = "0" + ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти + "00";
			КонецЕсли;
			
		Иначе
			
			Если ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти = СтруктураЗначенийПолейФорматаВыгрузки.СпРазделителейДробнойЧасти[3].Значение Тогда // "Без разделителя"
				СуммаДолга = СтрЗаменить(Формат(СуммаДолга, "ЧДЦ=2; ЧГ=0"), ",", "");
			Иначе
				СуммаДолга = Формат(СуммаДолга, "ЧДЦ=2; ЧН=0,00; ЧГ=0; ЧРД=" + ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти + ";");
			КонецЕсли;
			
		КонецЕсли;
		
		Значение = СуммаДолга;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.НомерТелефона
		   И СтруктураОбщихТаблиц.ТаблицаКонтактныеДанные <> Неопределено Тогда
		
		НайденнаяСтрокаНомерТелефона = СтруктураОбщихТаблиц.ТаблицаКонтактныеДанные.Найти(ТекущаяСтрокаЛицевогоСчета.ЛицевойСчет, "ЛицевойСчет");
		
		Если НайденнаяСтрокаНомерТелефона = Неопределено Тогда
			Значение = "";
		Иначе
			Значение = СокрЛП(НайденнаяСтрокаНомерТелефона.НомерТелефона);
		КонецЕсли;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.СуммаНачислений Тогда
		
		СуммаНачисления = 0;
		
		Если СтруктураОбщихТаблиц.ТаблицаНачислений <> Неопределено Тогда
			
			НайденнаяСтрокаНачисления = СтруктураОбщихТаблиц.ТаблицаНачислений.Найти(ТекущаяСтрокаЛицевогоСчета.ЛицевойСчет, "ЛицевойСчет");
			
			Если НайденнаяСтрокаНачисления <> Неопределено Тогда
				СуммаНачисления = НайденнаяСтрокаНачисления.СуммаНачисления;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СуммаНачисления = 0 Тогда
			
			Если ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти = "Без разделителя" Тогда
				СуммаНачисления = "0";
			Иначе
				СуммаНачисления = "0" + ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти + "00";
			КонецЕсли;
			
		Иначе
			
			Если ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти = "Без разделителя" Тогда
				СуммаНачисления = СтрЗаменить(Формат(СуммаНачисления, "ЧДЦ=2; ЧГ=0"), ",", "");
			Иначе
				СуммаНачисления = Формат(СуммаНачисления, "ЧДЦ=2; ЧН=0,00; ЧГ=0; ЧРД=" + ТекущаяСтрокаПараметра.СимволРазделительДробнойЧасти + ";");
			КонецЕсли;
			
		КонецЕсли;
		
		Значение = СуммаНачисления;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.ПериодОплаты Тогда
		
		ПорядокМесяц = ТекущаяСтрокаПараметра.ПорядокМесяц;
		ПорядокГод   = ТекущаяСтрокаПараметра.ПорядокГод;
		
		СимволРазделительПериода = ТекущаяСтрокаПараметра.СимволРазделительПериода;
		Если СимволРазделительПериода = "Без разделителя" Тогда
			СимволРазделительПериода = "";
		КонецЕсли;
		
		Если ПорядокМесяц < ПорядокГод Тогда
			ПериодОплаты = Формат(ПараметрыВыгрузки.НачалоПериода, "ДФ=ММ")
						 + СимволРазделительПериода
						 + Формат(ПараметрыВыгрузки.НачалоПериода, "ДФ=гг");
		Иначе
			ПериодОплаты = Формат(ПараметрыВыгрузки.НачалоПериода, "ДФ=гг")
						 + СимволРазделительПериода
						 + Формат(ПараметрыВыгрузки.НачалоПериода, "ДФ=ММ");
		КонецЕсли;
		
		Значение = ПериодОплаты;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.ДатаОперации Тогда
		
		Значение = Формат(ТекущаяДата(), "ДФ=дд.MM.гггг");
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.РасчетныйСчетОрганизации Тогда
		
		Значение = Формат(ПараметрыВыгрузки.РасчетныйСчетОрганизации, "ДФ=дд.MM.гггг");
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.ОКТМО Тогда
		
		Если ЗначениеЗаполнено(ПараметрыВыгрузки)
		 И ПараметрыВыгрузки.Свойство("Организация")
		 И ЗначениеЗаполнено(ПараметрыВыгрузки.Организация)
		 И ЗначениеЗаполнено(ПараметрыВыгрузки.Организация.РегистрацияВНалоговомОргане) Тогда
			Значение = Строка(ПараметрыВыгрузки.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО);
		Иначе
			Значение = "";
		КонецЕсли;
		
	ИначеЕсли Параметр = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.КБК Тогда
		
		Значение = ?(ЗначениеЗаполнено(ТекущаяСтрокаПараметра.НалогКБК), ТекущаяСтрокаПараметра.НалогКБК.КодБК, "");
		//---
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Функция - Получить значение ФИО
//
// Параметры:
// ДанныеЛицевогоСчета	 - СтрокаТаблицыЗначений
// ПараметрВыгрузки	 - Перечисление	
// 
// Возвращаемое значение:
// Строка - Фамилия, Имя, Отчество
//
&НаСервереБезКонтекста
Функция ПолучитьЗначениеФИО(ДанныеЛицевогоСчета, ПараметрВыгрузки)
	
	Если ДанныеЛицевогоСчета.ЭтоЖилец Тогда
		
		Если ПараметрВыгрузки = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Фамилия Тогда
			СтрокаВыводаВМассив = ДанныеЛицевогоСчета.Фамилия;
		ИначеЕсли ПараметрВыгрузки = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Имя Тогда
			СтрокаВыводаВМассив = ДанныеЛицевогоСчета.Имя;
		ИначеЕсли ПараметрВыгрузки = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Отчество Тогда
			СтрокаВыводаВМассив = ДанныеЛицевогоСчета.Отчество;
		КонецЕсли;
		
	Иначе
		Если ДанныеЛицевогоСчета.РазбиватьНаименованиеКонтрагента Тогда
			
			МассивФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДанныеЛицевогоСчета.НаименованиеКонтрагента," ");
			
			Если ПараметрВыгрузки = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Фамилия Тогда
				
				Если МассивФИО.Количество() = 0 Тогда
					СтрокаВыводаВМассив = ДанныеЛицевогоСчета.НаименованиеКонтрагента;
				Иначе
					СтрокаВыводаВМассив = МассивФИО[0];
				КонецЕсли;
				
			ИначеЕсли ПараметрВыгрузки = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Имя Тогда
				
				Если МассивФИО.Количество() <= 1 Тогда
					СтрокаВыводаВМассив = "";
				Иначе
					СтрокаВыводаВМассив = МассивФИО[1];
				КонецЕсли;
				
			ИначеЕсли ПараметрВыгрузки = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Отчество Тогда
				
				Если МассивФИО.Количество() <= 2 Тогда
					СтрокаВыводаВМассив = "";
				Иначе
					СтрокаВыводаВМассив = МассивФИО[2];
				КонецЕсли;
				
			Иначе
				
				СтрокаВыводаВМассив = "";
				
			КонецЕсли;
			
		ИначеЕсли
			
			ПараметрВыгрузки = Перечисления.УПЖКХ_ПараметрыУниверсальнойВыгрузкиДанных.Фамилия Тогда
			СтрокаВыводаВМассив = ДанныеЛицевогоСчета.НаименованиеКонтрагента;
			
		Иначе
			
			СтрокаВыводаВМассив = "";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокаВыводаВМассив;
	
КонецФункции

// Процедура закрывает файл DBF.
//
// Параметры:
//  ФайлDBF	 - XBase	 - файл dbf.
//
&НаСервереБезКонтекста
Процедура ЗакрытьФайлDBF(ФайлDBF)
	
	Если ФайлDBF.Открыта() Тогда
		ФайлDBF.ЗакрытьФайл();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
// Записывает файл реестра во временный каталог.
//
// Параметры:
//  ФайлРеестра				 - 	ТекстовыйДокумент, ТабличныйДокумент - объект файла реестра.
//  ПараметрыВыгрузки		 - 	Стркутура - параметры выгрузки.
//  АдресХранилища			 - 	Строка	 - адрес хранилища.
//
Функция ЗаписатьФайлРеестра(ФайлРеестра, ПараметрыВыгрузки)
	
	Кодировка = ПараметрыВыгрузки.Кодировка;
	
	Если ПараметрыВыгрузки.ЭтоTXT Тогда
		
		ПутьКФайлу = ПолучитьИмяВременногоФайла(".txt");
		
		ФайлРеестра.Записать(ПутьКФайлу, ?(Кодировка = "DOS", КодировкаТекста.OEM, КодировкаТекста.ANSI));
		
	ИначеЕсли ПараметрыВыгрузки.ЭтоCSV Тогда
		
		ПутьКФайлу = ПолучитьИмяВременногоФайла(".csv");
		
		ФайлРеестра.Записать(ПутьКФайлу, ?(Кодировка = "DOS", КодировкаТекста.OEM, КодировкаТекста.ANSI));
		
	ИначеЕсли ПараметрыВыгрузки.ЭтоXLS Тогда
		
		ПутьКФайлу          = ПолучитьИмяВременногоФайла(".xlsx");
		СистемнаяИнформация = Новый СистемнаяИнформация;
		
		Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86
		 ИЛИ СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64 Тогда
			ТипФайла = ТипФайлаТабличногоДокумента.XLSX;
		Иначе
			ТипФайла = ТипФайлаТабличногоДокумента.XLS;
		КонецЕсли;
		
		ФайлРеестра.Записать(ПутьКФайлу, ТипФайла);
		
	ИначеЕсли ПараметрыВыгрузки.ЭтоDBF Тогда
		
		ПутьКФайлу = ФайлРеестра.ПолноеИмя;
		
		ЗакрытьФайлDBF(Новый XBase(ПутьКФайлу));
		
	КонецЕсли;
	
	Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПутьКФайлу), Новый УникальныйИдентификатор);
	
	УдалитьФайлы(ПутьКФайлу);
	
	Возврат Адрес;
	
КонецФункции

#КонецОбласти