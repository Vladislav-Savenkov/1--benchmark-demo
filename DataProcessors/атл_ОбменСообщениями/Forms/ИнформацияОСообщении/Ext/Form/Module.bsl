
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	мВыгрузка = ПолучитьИзВременногоХранилища(Параметры.АдресХранилища);
	Объект.ТаблицаСообщений.Загрузить(мВыгрузка);	
	МассивСообщений = Объект.ТаблицаСообщений.НайтиСтроки(Новый Структура("ИдентификаторСообщенияНаЭкране", Параметры.ИдентификаторСообщенияНаЭкране));
			
	Если МассивСообщений.Количество() > 0 Тогда		
		НадписьИнформация = СформироватьТекстНадписи(МассивСообщений[0]);
		ЗаполнитьРеквизитыФормы(МассивСообщений[0]);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция СформироватьТекстНадписи(СообщениеДляВывода)
	
	ТекстИнформации = "Дата создания: 	" + ?(ЗначениеЗаполнено(СообщениеДляВывода.ДатаСоздания), СообщениеДляВывода.ДатаСоздания, 		 " не указана...");

	Если Не СообщениеДляВывода.ОтправительМК Тогда
		ТекстИнформации = ТекстИнформации + "
		|Дата отправки: 	" + ?(ЗначениеЗаполнено(СообщениеДляВывода.ДатаОтправки), СообщениеДляВывода.ДатаОтправки, 		 " сообщение не отправлено");		
		
	КонецЕсли;
	ТекстИнформации = ТекстИнформации + "	
	|Дата доставки: 	" + ?(ЗначениеЗаполнено(СообщениеДляВывода.ДатаДоставки), СообщениеДляВывода.ДатаДоставки, 		 " сообщение не получено") + "
	|Дата прочтения: 	" + ?(ЗначениеЗаполнено(СообщениеДляВывода.ДатаПрочтения), СообщениеДляВывода.ДатаПрочтения, 	 " сообщение не прочитано");
	
	Если СообщениеДляВывода.ОтправительМК И СообщениеДляВывода.ПолучательМК Тогда
		ТекстИнформации = ТекстИнформации + "
		|Отправитель и получатель мобильные клиенты";
	ИначеЕсли СообщениеДляВывода.ОтправительМК И Не СообщениеДляВывода.ПолучательМК Тогда
		ТекстИнформации = ТекстИнформации + "
		|Отправитель мобильный клиент, получатель пользователь базы 1С";
		Элементы.ПоказатьНаКарте.Заголовок = "Место создания сообщения";
		Элементы.ПоказатьНаКарте.Видимость = Истина;
	ИначеЕсли Не СообщениеДляВывода.ОтправительМК И СообщениеДляВывода.ПолучательМК Тогда
		ТекстИнформации = ТекстИнформации + "
		|Отправитель пользователь базы 1С, получатель мобильный клиент";
		Элементы.ПоказатьНаКарте.Заголовок = "Место получения и прочтения сообщения";
		Элементы.ПоказатьНаКарте.Видимость = Истина;
	ИначеЕсли Не СообщениеДляВывода.ОтправительМК И Не СообщениеДляВывода.ПолучательМК Тогда
		ТекстИнформации = ТекстИнформации + "
		|Отправитель и получатель пользователи базы 1С";
		Элементы.ПоказатьНаКарте.Видимость = Ложь;
	КонецЕсли;
	
	Если СообщениеДляВывода.Административное Тогда
		ТекстИнформации = ТекстИнформации + "
		|Это административное сообщение";
	КонецЕсли;
		
	Возврат ТекстИнформации;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(СообщениеДляВывода)
		
	ДатаСоздания = СообщениеДляВывода.ДатаСоздания;
	Сообщение = СообщениеДляВывода.Сообщение;
	Пользователь = СообщениеДляВывода.Пользователь;
	
	//Заполним информацию по сообщению
	Если СообщениеДляВывода.Полученное Тогда
		Кому = ТекущийПользователь;
		ОтКого = СообщениеДляВывода.Пользователь;
	Иначе
		Кому = СообщениеДляВывода.Пользователь;
		ОтКого = ТекущийПользователь;
	КонецЕсли;
	
	ИдентификаторСообщения = СообщениеДляВывода.ИдентификаторСообщения;
	ИдентификаторИнформационнойБазы = СообщениеДляВывода.ИдентификаторИнформационнойБазы;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНаКарте(Команда)
	
	АдресТаблицы = ПолучитьАдресТаблицы();
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСпискаПараметровОткрытия", АдресТаблицы);
	ПараметрыФормы.Вставить("НеИнтерактивноеОткрытие", Истина);
	
	ОткрытьФорму("Обработка.атл_ОтображениеТочекСТрекомНаКарте.Форма.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицы()
	
	ТаблицаПараметровОткрытия = атл_УправлениеМобильнымиСотрудникамиСервер.СформироватьТаблицуСписокПараметровОткрытияДляОткрытияКарты();
	
	мНоваяСтрока = ТаблицаПараметровОткрытия.Добавить();
	мНоваяСтрока.Пользователь = Пользователь;
	мНоваяСтрока.ВидОбъекта = "Сообщение";
	мНоваяСтрока.Идентификатор = ИдентификаторСообщения;
	мНоваяСтрока.ИдентификаторИнформационнойБазы = ИдентификаторИнформационнойБазы;

	Возврат ПоместитьВоВременноеХранилище(ТаблицаПараметровОткрытия, УникальныйИдентификатор);
	
КонецФункции
