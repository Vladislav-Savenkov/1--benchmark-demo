

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДинамическийСписок.Параметры.УстановитьЗначениеПараметра("Пользователь", ПараметрыСеанса.ТекущийПользователь);		
	ДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ПустаяДата", Дата("00010101"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЧастотаОбновленияСписка = 0 Тогда
		ЧастотаОбновленияСписка = 60;
	КонецЕсли;
	
	Если МаксимальноеКоличествоВыводимыхСообщений = 0 Тогда
		МаксимальноеКоличествоВыводимыхСообщений = 200;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбновитьФормуНаКлиенте", ЧастотаОбновленияСписка);
	
	ОбновитьФормуНаКлиенте();
	
КонецПроцедуры
	
#КонецОбласти


#Область КомандыФормы

&НаКлиенте
Процедура НастройкаСписка(Команда)
	
	мПараметрыФормы = Новый Структура;	
	мПараметрыФормы.Вставить("ЧастотаОбновленияСписка", ЧастотаОбновленияСписка); 
	мПараметрыФормы.Вставить("НаправлениеСортировкиВозрастание", НаправлениеСортировкиВозрастание);
	мПараметрыФормы.Вставить("МаксимальноеКоличествоВыводимыхСообщений", МаксимальноеКоличествоВыводимыхСообщений);
	
	мОповещение = Новый ОписаниеОповещения("ОбновитьНастройкиСпискаОповещение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.атл_ПросмотрУведомлений.Форма.НастройкаСписка", мПараметрыФормы, ЭтаФорма,,,,мОповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьФормуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПрочитано(Команда)
	
	ВыделенныеСтроки = Элементы.ДинамическийСписок.ВыделенныеСтроки;
		
	Для Каждого текСтрока из ВыделенныеСтроки Цикл		
		мТекущиеДанные = Элементы.ДинамическийСписок.ДанныеСтроки(текСтрока);
		Если НЕ мТекущиеДанные = Неопределено И мТекущиеДанные.ДатаПрочтения = Дата("00010101") Тогда
			УстановитьОтметкуОПрочтениеУведомления(мТекущиеДанные.ИдентификаторУведомления);			
		КонецЕсли;
	КонецЦикла;

	ОбновитьФорму();	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСкрыть(Команда)
	
	ВыделенныеСтроки = Элементы.ДинамическийСписок.ВыделенныеСтроки;
		
	Для Каждого текСтрока из ВыделенныеСтроки Цикл		
		мТекущиеДанные = Элементы.ДинамическийСписок.ДанныеСтроки(текСтрока);
		Если НЕ мТекущиеДанные = Неопределено Тогда
			УстановитьОтметкуОСкрытииУведомления(мТекущиеДанные.ИдентификаторУведомления);					
		КонецЕсли;
	КонецЦикла;

	ОбновитьФорму();
	
КонецПроцедуры
	
#КонецОбласти



#Область ПрочиеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДатуНачалаПросмотраУведомлений(ТекущийПользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	атл_НастройкиУведомленийПользователей.Пользователь,
	|	атл_НастройкиУведомленийПользователей.ДатаНачалаИспользования,
	|	атл_НастройкиУведомленийПользователей.ФильтрПоЗадачами
	|ИЗ
	|	РегистрСведений.атл_НастройкиУведомленийПользователей КАК атл_НастройкиУведомленийПользователей
	|ГДЕ
	|	атл_НастройкиУведомленийПользователей.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		Возврат Выборка.ДатаНачалаИспользования;
	Иначе
		Возврат Дата("00010101");
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбновитьФорму(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено)

	мДатаНачалаПросмотраУведомлений = ПолучитьДатуНачалаПросмотраУведомлений(ПараметрыСеанса.ТекущийПользователь);
	ДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ДатаНачала", мДатаНачалаПросмотраУведомлений);
	
	Если МаксимальноеКоличествоВыводимыхСообщений <> 200 и МаксимальноеКоличествоВыводимыхСообщений <> 0 Тогда
		ДинамическийСписок.ТекстЗапроса = СтрЗаменить(ДинамическийСписок.ТекстЗапроса, "200", МаксимальноеКоличествоВыводимыхСообщений);
	КонецЕсли;
	
	Если НаправлениеСортировкиВозрастание Тогда
		ДинамическийСписок.ТекстЗапроса = СтрЗаменить(ДинамическийСписок.ТекстЗапроса, "УБЫВ", "ВОЗР");		
	Иначе
		ДинамическийСписок.ТекстЗапроса = СтрЗаменить(ДинамическийСписок.ТекстЗапроса, "ВОЗР", "УБЫВ");		
	КонецЕсли;
	
	Элементы.ДинамическийСписок.Обновить();
	
	мКоличество = ПолучитьКоличествоНепрочитанныхУведомлений(ДинамическийСписок.ТекстЗапроса, ДинамическийСписок.Параметры.Элементы);
	Если мКоличество = 0 Тогда
		ЭтаФорма.Заголовок = "Уведомления";
	Иначе
		ЭтаФорма.Заголовок = "Уведомления " + "(" + Строка(мКоличество) + ")";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуНаКлиенте() 
		
	ОбновитьФорму();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуНаКлиентеОповещение(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
		
	ОбновитьФорму();	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройкиСписка(РезультатЗакрытия = Неопределено)
	
	Если НЕ РезультатЗакрытия = Неопределено Тогда
		ЧастотаОбновленияСписка = РезультатЗакрытия.ЧастотаОбновленияСписка;
		МаксимальноеКоличествоВыводимыхСообщений = РезультатЗакрытия.МаксимальноеКоличествоВыводимыхСообщений;
		НаправлениеСортировкиВозрастание = РезультатЗакрытия.НаправлениеСортировкиВозрастание;
	КонецЕсли;
	
	ОбновитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиСпискаОповещение(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьНастройкиСписка(РезультатЗакрытия);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКоличествоНепрочитанныхУведомлений(ТекстЗапроса, Параметры)
	
	мЗапрос = Новый Запрос;
	мЗапрос.Текст = ТекстЗапроса;
	
	Если НЕ Параметры = Неопределено Тогда
		Для Каждого текСтрока Из Параметры Цикл
			мЗапрос.УстановитьПараметр(текСтрока.Параметр, текСтрока.Значение);
		КонецЦикла;
	КонецЕсли;
	
	мВыгрузка = мЗапрос.Выполнить().Выгрузить();
	мКоличество = мВыгрузка.Итог("КоличествоНепрочитанныхУведомлений");
	
	//для ускорения формирования выборки сместим граничную дату уведомлений в случае, если список получается пустой
	Если мВыгрузка.Количество() = 0 Тогда
		ЗаписьРегистра = РегистрыСведений.атл_НастройкиУведомленийПользователей.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		ЗаписьРегистра.Прочитать();
		
		Если НачалоДня(ЗаписьРегистра.ДатаНачалаИспользования) < НачалоДня(ТекущаяДата()) Тогда
			ТекстФильтрПоЗадачами = ЗаписьРегистра.ФильтрПоЗадачами;
			
			ЗаписьРегистра.Пользователь = ПараметрыСеанса.ТекущийПользователь;
			ЗаписьРегистра.ДатаНачалаИспользования = ТекущаяДата();
			ЗаписьРегистра.ФильтрПоЗадачами = ТекстФильтрПоЗадачами;
			ЗаписьРегистра.Записать();
		КонецЕсли;	
	КонецЕсли;
	
	Возврат мКоличество;
	
КонецФункции

&НаСервере
Процедура УстановитьОтметкуОПрочтениеУведомления(ИдентификаторУведомления)
	
	мТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	ЗаписьРегистраПрочтения = РегистрыСведений.атл_ПрочитанныеУведомленияПользователей.СоздатьМенеджерЗаписи();
	ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
	ЗаписьРегистраПрочтения.Пользователь = мТекущийПользователь;
	ЗаписьРегистраПрочтения.Прочитать();
	Если Не ЗаписьРегистраПрочтения.Выбран() Тогда
		ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
		ЗаписьРегистраПрочтения.Пользователь = мТекущийПользователь;
	КонецЕсли;
	
	ЗаписьРегистраПрочтения.ДатаПрочтения = ТекущаяДата();
	ЗаписьРегистраПрочтения.Записать();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметкуОСкрытииУведомления(ИдентификаторУведомления)
	
	мТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	ЗаписьРегистраПрочтения = РегистрыСведений.атл_ПрочитанныеУведомленияПользователей.СоздатьМенеджерЗаписи();
	ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
	ЗаписьРегистраПрочтения.Пользователь = мТекущийПользователь;
	ЗаписьРегистраПрочтения.Прочитать();
	Если Не ЗаписьРегистраПрочтения.Выбран() Тогда
		ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
		ЗаписьРегистраПрочтения.Пользователь = мТекущийПользователь;
	КонецЕсли;
	
	Если ЗаписьРегистраПрочтения.ДатаПрочтения = Дата("00010101") Тогда
		ЗаписьРегистраПрочтения.ДатаПрочтения = ТекущаяДата();
	КонецЕсли;
		
	ЗаписьРегистраПрочтения.ДатаСкрытия = ТекущаяДата();
	ЗаписьРегистраПрочтения.Записать();
		
КонецПроцедуры

&НаКлиенте
Процедура ДинамическийСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
			
	мТекущиеДанные = Элементы.ДинамическийСписок.ДанныеСтроки(ВыбраннаяСтрока);
	Если НЕ мТекущиеДанные = Неопределено Тогда
		
		мПараметрыФормы = Новый Структура;
		мПараметрыФормы.Вставить("ДатаСоздания", мТекущиеДанные.ДатаСоздания); 
		мПараметрыФормы.Вставить("ЗадачаМобильномуСотруднику", мТекущиеДанные.ЗадачаМобильномуСотруднику);
		мПараметрыФормы.Вставить("ИдентификаторУведомления", мТекущиеДанные.ИдентификаторУведомления);
		мПараметрыФормы.Вставить("ТекстУведомления", мТекущиеДанные.ТекстУведомления);
		
		мОповещение = Новый ОписаниеОповещения("ОбновитьФормуНаКлиентеОповещение", ЭтотОбъект);
		ОткрытьФорму("Обработка.атл_ПросмотрУведомлений.Форма.ФормаУведомленияПоЗадачеУМС",мПараметрыФормы, ЭтаФорма,,,,мОповещение);		

	КонецЕсли;
	
КонецПроцедуры

	
#КонецОбласти


