
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаСоздания = Параметры.ДатаСоздания;
	ЗадачаМобильномуСотруднику = Параметры.ЗадачаМобильномуСотруднику;
	ИдентификаторУведомления = Параметры.ИдентификаторУведомления;
	ТекстУведомления = Параметры.ТекстУведомления;	
	
	//регистрируем прочтение если необходимо
	ЗаписьРегистраПрочтения = РегистрыСведений.атл_ПрочитанныеУведомленияПользователей.СоздатьМенеджерЗаписи();
	ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
	ЗаписьРегистраПрочтения.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	ЗаписьРегистраПрочтения.Прочитать();
	
	Если ЗаписьРегистраПрочтения.ДатаПрочтения = Дата("00010101") Тогда
		Если Не ЗаписьРегистраПрочтения.Выбран() Тогда
			ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
			ЗаписьРегистраПрочтения.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		КонецЕсли;
		ЗаписьРегистраПрочтения.ДатаПрочтения = ТекущаяДата();
		ЗаписьРегистраПрочтения.Записать();				
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗадачаМобильномуСотруднику) Тогда
		Если ДатаСоздания = Дата("00010101") Тогда
			ЭтаФорма.Заголовок = "Уведомление";
		Иначе
			ЭтаФорма.Заголовок = "Уведомление от " +  ДатаСоздания;
		КонецЕсли;
		ДокументЗанятостиРесурса = ЗадачаМобильномуСотруднику.ДокументЗанятостиРесурса;
		ЗадачаИсполнителя = ЗадачаМобильномуСотруднику.ЗадачаИсполнителя;
		Если ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.НомерМК) Тогда
			Элементы.НадписьЗадачаМобильномуСотруднику.Заголовок = "Задача мобильному сотруднику №МК " + ЗадачаМобильномуСотруднику.НомерМК + " от " + ЗадачаМобильномуСотруднику.Дата;
		Иначе
			Элементы.НадписьЗадачаМобильномуСотруднику.Заголовок = "Задача мобильному сотруднику № " + ЗадачаМобильномуСотруднику.Номер + " от " + ЗадачаМобильномуСотруднику.Дата;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


#Область КомандыФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Скрыть(Команда)
	
	УстановитьОтметкуОСкрытииУведомления();	
	Закрыть();

КонецПроцедуры
	
#КонецОбласти



#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура НадписьЗадачаМобильномуСотрудникуНажатие(Элемент)
	
	СтандартнаяОбработка = Ложь;
	
	мПараметрыФормы = Новый Структура;
	мПараметрыФормы.Вставить("Ключ", ЗадачаМобильномуСотруднику); 
	ОткрытьФорму("Документ.атл_ЗадачаМобильномуСотруднику.ФормаОбъекта", мПараметрыФормы);

КонецПроцедуры

&НаСервере
Процедура УстановитьОтметкуОСкрытииУведомления()
	
	ЗаписьРегистраПрочтения = РегистрыСведений.атл_ПрочитанныеУведомленияПользователей.СоздатьМенеджерЗаписи();
	ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
	ЗаписьРегистраПрочтения.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	ЗаписьРегистраПрочтения.Прочитать();
	
	ЗаписыватьЗаписьРегистра = Ложь;
	
	Если Не ЗаписьРегистраПрочтения.Выбран() Тогда
		ЗаписьРегистраПрочтения.ИдентификаторУведомления = ИдентификаторУведомления;
		ЗаписьРегистраПрочтения.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		ЗаписыватьЗаписьРегистра = Истина;
	КонецЕсли;
	
	Если ЗаписьРегистраПрочтения.ДатаПрочтения = Дата("00010101") Тогда		
		ЗаписьРегистраПрочтения.ДатаПрочтения = ТекущаяДата();
		ЗаписыватьЗаписьРегистра = Истина;
	КонецЕсли;
	
	Если ЗаписьРегистраПрочтения.ДатаСкрытия = Дата("00010101") Тогда
		ЗаписьРегистраПрочтения.ДатаСкрытия = ТекущаяДата();
		ЗаписыватьЗаписьРегистра = Истина;
	КонецЕсли;
		
	Если ЗаписыватьЗаписьРегистра Тогда
		ЗаписьРегистраПрочтения.Записать();		
	КонецЕсли;	
	
КонецПроцедуры
	
#КонецОбласти

