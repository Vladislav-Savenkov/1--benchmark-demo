
#Область СобытияЭлементовФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	мСписокВыбора = Элементы.ФильтрКоординат.СписокВыбора;
	мСписокВыбора.Добавить("Все");
	мСписокВыбора.Добавить("Передаются");
	мСписокВыбора.Добавить("Не передаются");

	ФильтрКоординат = "Все";
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(Команда)
	
	ПеременнаяТипаСтандартныйПериод = Новый СтандартныйПериод; 

	Диалог = Новый ДиалогРедактированияСтандартногоПериода(); 
	Диалог.Период.ДатаНачала = ДатаНачала; 
	Диалог.Период.ДатаОкончания = ДатаОкончания; 
	Если Диалог.Редактировать() Тогда 
		ПеременнаяТипаСтандартныйПериод = Диалог.Период;
		ДатаНачала = ПеременнаяТипаСтандартныйПериод.ДатаНачала;
		ДатаОкончания = ПеременнаяТипаСтандартныйПериод.ДатаОкончания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	Если ПроверитьЗаполнениеФормы() Тогда
		ОбновитьТабДок();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда		
		СтандартнаяОбработка = Ложь;
		
		Если Расшифровка.Координаты Тогда		
			мПараметры = Новый Структура;
			мПараметры.Вставить("НеИнтерактивноеОткрытие", Истина);
			мПараметры.Вставить("ПоказыватьТрек", Истина);
			мПараметры.Вставить("ДатаНачала", Расшифровка.НачалоПериода);
			мПараметры.Вставить("ДатаОкончания", Расшифровка.ОкончаниеПериода);
			// будет пустая ссылка
			//мПараметры.Вставить("ВидТрека", Перечисления.атл_ВидыИсточниковКоординат.ПустаяСсылка());
			мПараметры.Вставить("АдресСпискаПараметровОткрытия", ПолучитьАдресТаблицыДляКарты());
			
			ОткрытьФорму("Обработка.атл_ОтображениеТочекСТрекомНаКарте.Форма.Форма", мПараметры);

		Иначе
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "За выбранный период координаты не передавалась. Не возможно отобразить трек пользователя!";
			Сообщение.Сообщить();			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область ПрочиеПроцедурыИФункции 

&НаКлиенте
Функция ПроверитьЗаполнениеФормы()
	
	фл = Истина;
	
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		фл = Ложь;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбран пользователь!";
		Сообщение.Поле = "Пользователь";
		Сообщение.Сообщить();		
	КонецЕсли;
	
	Если ДатаНачала = Дата("00010101") Тогда
		фл = Ложь;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрано начало периода!";
		Сообщение.Поле = "ДатаНачала";
		Сообщение.Сообщить();		
	КонецЕсли;
	
	Если ДатаОкончания = Дата("00010101") Тогда
		фл = Ложь;		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрана окончание периода!";
		Сообщение.Поле = "ДатаОкончания";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Возврат фл;
	
КонецФункции

&НаСервере
Процедура ОбновитьТабДок()
			
	ТабДок.Очистить();
	мМакет = Обработки.атл_СводныйОтчетПоКоординатамТрека.ПолучитьМакет("Макет");
	
	ОбластьШапка = мМакет.ПолучитьОбласть("ОбластьШапка");
	ТабДок.Вывести(ОбластьШапка);
	
	мЗапрос = Новый Запрос;
	мЗапрос.Текст = "ВЫБРАТЬ
	                |	атл_КоординатыТрека.Период,
	                |	атл_КоординатыТрека.Широта,
	                |	атл_КоординатыТрека.Долгота,
	                |	атл_КоординатыТрека.ЗарядБатареи,
	                |	ВЫБОР
	                |		КОГДА атл_КоординатыТрека.Широта = """"
	                |				ИЛИ атл_КоординатыТрека.Долгота = """"
	                |			ТОГДА ЛОЖЬ
	                |		ИНАЧЕ ИСТИНА
	                |	КОНЕЦ КАК ЗаписаныКоординаты
	                |ПОМЕСТИТЬ ТаблицаИтого
	                |ИЗ
	                |	РегистрСведений.атл_КоординатыТрека КАК атл_КоординатыТрека
	                |ГДЕ
	                |	атл_КоординатыТрека.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	                |	И атл_КоординатыТрека.Пользователь = &Пользователь
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ТаблицаИтого.Период,
	                |	ТаблицаИтого.Широта,
	                |	ТаблицаИтого.Долгота,
	                |	ТаблицаИтого.ЗарядБатареи,
	                |	ТаблицаИтого.ЗаписаныКоординаты
	                |ИЗ
	                |	ТаблицаИтого КАК ТаблицаИтого";
	мЗапрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	мЗапрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	мЗапрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Если ФильтрКоординат = "Передаются" Тогда
		мЗапрос.Текст = мЗапрос.Текст + "	ГДЕ
										|	ТаблицаИтого.ЗаписаныКоординаты = ИСТИНА";
	ИначеЕсли ФильтрКоординат = "Не передаются" Тогда 
		мЗапрос.Текст = мЗапрос.Текст + "	ГДЕ
										|	ТаблицаИтого.ЗаписаныКоординаты = ЛОЖЬ";									
	КонецЕсли;
					
	мВыгрузка = мЗапрос.Выполнить().Выгрузить();	
	
	Если мВыгрузка.Количество() > 0 Тогда
		Сч = 0;		
		НачПериода = мВыгрузка[0].Период;
		НачБатарейка = мВыгрузка[0].ЗарядБатареи;
		ТекДата = НачалоДня(мВыгрузка[0].Период);		
		флКоординаты = мВыгрузка[0].ЗаписаныКоординаты;
		ВывестиЗаголовокДаты(мМакет, ТекДата);
		
		Для Сч = 0 По мВыгрузка.Количество() -2 Цикл
			текСтрока = мВыгрузка[Сч];
			следСтрока = мВыгрузка[Сч + 1];
			флКоординаты = текСтрока.ЗаписаныКоординаты;
			
			Если НЕ ТекДата = НачалоДня(следСтрока.Период) Тогда
				ВывестиСтрокуМакета(мМакет, НачПериода, текСтрока.Период, НачБатарейка, текСтрока.ЗарядБатареи, флКоординаты); 						
				
				ВывестиЗаголовокДаты(мМакет, следСтрока.Период);	
				ТекДата = НачалоДня(следСтрока.Период);
				НачПериода = следСтрока.Период;
				НачБатарейка = следСтрока.ЗарядБатареи;
				
			Иначе
				Если НЕ текСтрока.ЗаписаныКоординаты = следСтрока.ЗаписаныКоординаты Тогда				
					ВывестиСтрокуМакета(мМакет, НачПериода, текСтрока.Период, НачБатарейка, текСтрока.ЗарядБатареи, флКоординаты); 						
					
					НачПериода = следСтрока.Период;
					НачБатарейка = следСтрока.ЗарядБатареи;
				КонецЕсли;

			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ НачПериода = мВыгрузка[Сч].Период Тогда
			ВывестиСтрокуМакета(мМакет, НачПериода, мВыгрузка[Сч].Период, НачБатарейка, мВыгрузка[Сч].ЗарядБатареи, флКоординаты); 												
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ВывестиЗаголовокДаты(Макет, ДатаПериода)
	
	Область = Макет.ПолучитьОбласть("ОбластьДата");
	Область.Параметры.Период = Формат(ДатаПериода, "ДЛФ = Д");
	ТабДок.Вывести(Область);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиСтрокуМакета(Макет, НачПериода, КонПериода, НачБатарейка, КонБатарейка, Координаты)
	
	Область = Макет.ПолучитьОбласть("ОбластьСтрока");
	Область.Параметры.ПериодРасшифровка = Новый Структура("НачалоПериода, ОкончаниеПериода, Координаты", НачПериода, КонПериода, Координаты);
	Область.Параметры.Период = Формат(НачПериода, "ДЛФ=В") + " - " + Формат(КонПериода, "ДЛФ=В");
	Область.Параметры.НачБатарейка = Строка(НачБатарейка) + "%";
	Область.Параметры.КонБатарейка = Строка(КонБатарейка) + "%";
	Область.Параметры.Координаты = ?(Координаты, "Передаются", "Не передаются");
	ТабДок.Вывести(Область);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицыДляКарты()
	
	мТаблица = Новый ТаблицаЗначений;
	мТаблица.Колонки.Добавить("Пользователь");
	мТаблица.Колонки.Добавить("ВидОбъекта");
	мТаблица.Колонки.Добавить("Объект");
	
	мНоваяСтрока = мТаблица.Добавить();
	мНоваяСтрока.Пользователь = Пользователь;
	
	Возврат ПоместитьВоВременноеХранилище(мТаблица, УникальныйИдентификатор);

КонецФункции

#КонецОбласти

