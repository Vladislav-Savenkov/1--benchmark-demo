
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	мТекущийКомментарий = РегистрыСведений.атл_КомментарииКОбъектамУМС.СоздатьМенеджерЗаписи();
	мТекущийКомментарий.ИдентификаторИнформационнойБазы = Параметры.ИдентификаторИнформационнойБазы;
	мТекущийКомментарий.ИдентификаторКомментария = Параметры.ИдентификаторКомментария;
	мТекущийКомментарий.Объект = Параметры.Ссылка;
	мТекущийКомментарий.Прочитать();	
	
	НадписьИнформация = СформироватьТекстИнфНадписи(мТекущийКомментарий);
	      
	ЗаполнитьРеквизитыФормы(мТекущийКомментарий);
	
КонецПроцедуры

&НаСервере
Функция СформироватьТекстИнфНадписи(КомментарийДляВывода)
	
	ТекстИнформации = "Дата создания: 	" + ?(ЗначениеЗаполнено(КомментарийДляВывода.ДатаСоздания), КомментарийДляВывода.ДатаСоздания, 		 " не указана...");
	
	Если КомментарийДляВывода.ИдентификаторИнформационнойБазы = 0 Тогда
		ТекстИнформации = ТекстИнформации + "	
		|Дата доставки: 	" + ?(ЗначениеЗаполнено(КомментарийДляВывода.ПолучилДата), КомментарийДляВывода.ПолучилДата, 	 " комментарий не получен") + "
		|Дата прочтения: 	" + ?(ЗначениеЗаполнено(КомментарийДляВывода.ПрочиталДата), КомментарийДляВывода.ПрочиталДата, 	 " комментарий не прочитан");
		
		ТекстИнформации = ТекстИнформации + "
		|Отправитель пользователь базы 1С, получатель мобильный клиент";		
		Элементы.ПоказатьНаКарте.Заголовок = "Место получения и прочтения комментария";
	Иначе
		ТекстИнформации = ТекстИнформации + "	
		|Дата доставки: 	" + ?(ЗначениеЗаполнено(КомментарийДляВывода.ПолучилДата), КомментарийДляВывода.ПолучилДата, 	 " комментарий не получен");
		
		ТекстИнформации = ТекстИнформации + "
		|Отправитель мобильный клиент, получатель пользователь базы 1С";
		Элементы.ПоказатьНаКарте.Заголовок = "Место отправки комментария";
	КонецЕсли;
	
	Возврат ТекстИнформации;	
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(КомментарийДляВывода)
		
	Широта = КомментарийДляВывода.ПрочиталШирота;
	Долгота = КомментарийДляВывода.ПрочиталДолгота;
	
	Время = КомментарийДляВывода.ПрочиталДата;
	Текст = КомментарийДляВывода.Текст;
	
	Пользователь = КомментарийДляВывода.Автор;
	СсылкаНаОбъект = КомментарийДляВывода.Объект;
	ИдентификаторКомментария = КомментарийДляВывода.ИдентификаторКомментария;
	ИдентификаторИнформационнойБазы = КомментарийДляВывода.ИдентификаторИнформационнойБазы;
	
КонецПроцедуры


&НаКлиенте
Процедура ПоказатьНаКарте(Команда)
	
	АдресТаблицы = ПолучитьАдресТаблицы();
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСпискаПараметровОткрытия", АдресТаблицы);
	ПараметрыФормы.Вставить("НеИнтерактивноеОткрытие", Истина);
	
	ОткрытьФорму("Обработка.атл_ОтображениеТочекСТрекомНаКарте.Форма.Форма", ПараметрыФормы);

КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицы()
	
	ТаблицаПараметровОткрытия = атл_УправлениеМобильнымиСотрудникамиСервер.СформироватьТаблицуСписокПараметровОткрытияДляОткрытияКарты();
	
	мНоваяСтрока = ТаблицаПараметровОткрытия.Добавить();
	мНоваяСтрока.Пользователь = Пользователь;
	мНоваяСтрока.ВидОбъекта = "Комментарий";
	мНоваяСтрока.Объект = СсылкаНаОбъект;
	мНоваяСтрока.Идентификатор = ИдентификаторКомментария;
	мНоваяСтрока.ИдентификаторИнформационнойБазы = ИдентификаторИнформационнойБазы;

	Возврат ПоместитьВоВременноеХранилище(ТаблицаПараметровОткрытия, УникальныйИдентификатор);
	
КонецФункции
