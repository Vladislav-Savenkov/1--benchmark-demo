
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗадачаМобильномуСотруднику = Параметры.Ссылка;
	
	Если ЗначениеЗаполнено(Параметры.ЗначениеДопРеквизита) Тогда
		СтруктураРеквизита = атл_УправлениеМобильнымиСотрудникамиСервер.ПреобразованиеДанныхОКоорднатеВСтруктуру(Параметры.ЗначениеДопРеквизита);
		
		КоличествоПопыток = СтруктураРеквизита.КоличествоПопыток;
		
		Если СтруктураРеквизита.Свойство("АдресТаблицыОшибок") Тогда
			Элементы.ГруппаТаблица.Видимость = Истина;
			ТаблицаОшибок.Загрузить(ПолучитьИзВременногоХранилища(СтруктураРеквизита.АдресТаблицыОшибок));
			Для Каждого текСтрока Из ТаблицаОшибок Цикл
				текСтрока.КодОшибки = ПолучитьРасшифровкуОшибки(текСтрока.КодОшибки);
			КонецЦикла;
			
		Иначе
			Долгота = СтруктураРеквизита.Долгота;
			Широта = СтруктураРеквизита.Широта;
			МоментЗаписиКоординат = СтруктураРеквизита.МоментЗаписиКоординат;
			ИсточникКоординат = СтруктураРеквизита.ИсточникКоординат;
			Точность = СтруктураРеквизита.Точность; 
			
			стрИнфНадпись = "Дата получения: " + МоментЗаписиКоординат + Символы.ПС
							+ "Долгота: " + Долгота + Символы.ПС
							+ "Широта: " + Широта + Символы.ПС
							+ "Точность, м.: " + Точность + Символы.ПС
							+ "Источник координат: " + ИсточникКоординат;
							
			Элементы.ГруппаКоординат.Видимость = Истина;			
			НадписьИнформация = стрИнфНадпись;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область РаботаСКартой

&НаКлиенте
Процедура ПоказатьНаКарте(Команда)
		
	мПараметры = Новый Структура;
	мПараметры.Вставить("НеИнтерактивноеОткрытие", Истина);
	мПараметры.Вставить("АдресСпискаПараметровОткрытия", ПолучитьАдресТаблицыДляКарты());	
	
	ОткрытьФорму("Обработка.атл_ОтображениеТочекСТрекомНаКарте.Форма.Форма", мПараметры,,,ВариантОткрытияОкна.ОтдельноеОкно);
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицыДляКарты()
		
	ТаблицаПараметровОткрытия = атл_УправлениеМобильнымиСотрудникамиСервер.СформироватьТаблицуСписокПараметровОткрытияДляОткрытияКарты();
	                             	
	мНоваяСтрока = ТаблицаПараметровОткрытия.Добавить();
	мНоваяСтрока.Пользователь = ЗадачаМобильномуСотруднику.Исполнитель;
	мНоваяСтрока.ВидОбъекта = "КоординатаДопРеквизита";
	мНоваяСтрока.Объект = ЗадачаМобильномуСотруднику;	
	мНоваяСтрока.Широта = Широта;
	мНоваяСтрока.Долгота = Долгота;
	мНоваяСтрока.ДатаТочки = МоментЗаписиКоординат;
	мНоваяСтрока.ИсточникКоординат = ИсточникКоординат;
	мНоваяСтрока.Точность = Точность;

	Возврат ПоместитьВоВременноеХранилище(ТаблицаПараметровОткрытия, УникальныйИдентификатор);

КонецФункции

#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаСервере
Функция ПолучитьРасшифровкуОшибки(стрКодОшибки)
	
	Если СокрЛП(стрКодОшибки) = "1"	Тогда
		Возврат "Не удалось определить координату за отведенное время";
	ИначеЕсли СокрЛП(стрКодОшибки) = "2" Тогда
		Возврат "В настройках Android отключена возможность определения координат с помощью GPS";		
	ИначеЕсли СокрЛП(стрКодОшибки) = "3" Тогда
		Возврат "Определение координат прервано пользователем";
	КонецЕсли;			
	
	Возврат стрКодОшибки;
	
КонецФункции
	
#КонецОбласти

