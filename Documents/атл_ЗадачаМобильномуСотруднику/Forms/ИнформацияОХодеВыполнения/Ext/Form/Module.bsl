
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗадачаМобильномуСотруднику = Параметры.Ссылка;	
	
	НадписьИнформация = СформироватьТекстИнфНадписи();
	НадписьСтатистика = СформироватьТекстСтатистики();
	
	ПоказыватьКомментарии = Истина;
	ПоказыватьТрек = Истина;
	ПоказыватьСтатусы = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКартой

&НаКлиенте
Процедура ПоказатьНаКарте(Команда)
		
	мПараметры = Новый Структура;
	мПараметры.Вставить("НеИнтерактивноеОткрытие", Истина);
	мПараметры.Вставить("ПоказыватьСтатусы", Истина);
	мПараметры.Вставить("ПоказыватьТрек", Истина);
	мПараметры.Вставить("ПоказыватьКомментарии", Истина);
	мПараметры.Вставить("АдресСпискаПараметровОткрытия", ПолучитьАдресТаблицыДляКарты());
	мПараметры.Вставить("ЗаголовокФормы", "События по задаче");
	
	ОткрытьФорму("Обработка.атл_ОтображениеТочекСТрекомНаКарте.Форма.Форма", мПараметры,,,ВариантОткрытияОкна.ОтдельноеОкно);
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицыДляКарты()
		
	ТаблицаПараметровОткрытия = атл_УправлениеМобильнымиСотрудникамиСервер.СформироватьТаблицуСписокПараметровОткрытияДляОткрытияКарты();
	
	мНоваяСтрока = ТаблицаПараметровОткрытия.Добавить();
	мНоваяСтрока.Пользователь = ЗадачаМобильномуСотруднику.Исполнитель;
	мНоваяСтрока.ВидОбъекта = "ЗадачаМобильномуСотруднику";
	мНоваяСтрока.Объект = ЗадачаМобильномуСотруднику;

	Возврат ПоместитьВоВременноеХранилище(ТаблицаПараметровОткрытия, УникальныйИдентификатор);

КонецФункции

#КонецОбласти


#Область ОтображениеЭлементовФормы

&НаСервере
Функция СформироватьТекстИнфНадписи()	
	
	ТекстИнформации =  "Дата отправки:      " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ОтправленаДата), ЗадачаМобильномуСотруднику.ОтправленаДата, "");
	
	ТекстИнформации = ТекстИнформации + "	
	|Дата доставки:      " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПолучилДата), ЗадачаМобильномуСотруднику.ПолучилДата, "") + "
	|Дата прочтения:     " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПрочиталДата), ЗадачаМобильномуСотруднику.ПрочиталДата, "");
	
	мЗадачаПринята = Ложь;
	
	Если ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПринялОтклонилДата) Тогда
		Если ЗадачаМобильномуСотруднику.Принял Тогда 
			ТекстИнформации = ТекстИнформации + "	
			|Задача принята:     " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПринялОтклонилДата), ЗадачаМобильномуСотруднику.ПринялОтклонилДата, "");
			мЗадачаПринята = Истина;			
		Иначе
			ТекстИнформации = ТекстИнформации + "	
			|Задача отклонена:   " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПринялОтклонилДата), ЗадачаМобильномуСотруднику.ПринялОтклонилДата, "");			
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ЗавершилДата) Тогда
		Если ЗадачаМобильномуСотруднику.ЗавершилДиспетчер Тогда  //досрочное завершение
			Если ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ВыехалДата) Тогда
				ТекстИнформации = ТекстИнформации + "	
				|Выехал:             " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ВыехалДата),ЗадачаМобильномуСотруднику.ВыехалДата,"");				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПриехалДата) Тогда
				ТекстИнформации = ТекстИнформации + "	
				|Приехал:            " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПриехалДата),ЗадачаМобильномуСотруднику.ПриехалДата,"");
			КонецЕсли;
			
			ТекстИнформации = ТекстИнформации + "	
			|Завершил диспетчер: " + ЗадачаМобильномуСотруднику.ЗавершилДата;
			
		Иначе
			ТекстИнформации = ТекстИнформации + "	
			|Выехал:             " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ВыехалДата), ЗадачаМобильномуСотруднику.ВыехалДата, "") + "
			|Приехал:            " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПриехалДата), ЗадачаМобильномуСотруднику.ПриехалДата, "");
			
			ТекстИнформации = ТекстИнформации + "	
			|Завершил:           " + ЗадачаМобильномуСотруднику.ЗавершилДата;			
		КонецЕсли;
	Иначе
		Если мЗадачаПринята Тогда
			ТекстИнформации = ТекстИнформации + "	
			|Выехал:             " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ВыехалДата), ЗадачаМобильномуСотруднику.ВыехалДата, "") + "
			|Приехал: 	         " + ?(ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.ПриехалДата), ЗадачаМобильномуСотруднику.ПриехалДата, "");			
		КонецЕсли;
	КонецЕсли;	
	
	Возврат ТекстИнформации;	
	
КонецФункции

&НаСервере
Функция СформироватьТекстСтатистики()
	
	мПустаяДата = Дата("00010101");
	
	Если ЗадачаМобильномуСотруднику.Принял Тогда
		
		ТекстСтатистики = "Время реакции сотрудника: ";	
		ВремяРеакции = Число(ЗадачаМобильномуСотруднику.ПринялОтклонилДата - ЗадачаМобильномуСотруднику.ПолучилДата);
		ТекстСтатистики = ТекстСтатистики + ПолучитьПредставлениеВремени(ВремяРеакции);
						
		Если НЕ ЗадачаМобильномуСотруднику.ВыехалДата = мПустаяДата И НЕ ЗадачаМобильномуСотруднику.ПриехалДата = мПустаяДата Тогда
			Если ЗадачаМобильномуСотруднику.СпособУказанияДатыНазначения = Перечисления.атл_СпособУказанияДатыНазначения.КоВремени
						ИЛИ НЕ ЗначениеЗаполнено(ЗадачаМобильномуСотруднику.СпособУказанияДатыНазначения) Тогда
				ВремяОпоздания = Число(ЗадачаМобильномуСотруднику.ПланируемаяДатаВыполненияРабот - ЗадачаМобильномуСотруднику.ПриехалДата);
				Если ВремяОпоздания < 0 Тогда
					ТекстСтатистики = ТекстСтатистики + Символы.ПС + "Опоздал на ";
					ТекстСтатистики = ТекстСтатистики + ПолучитьПредставлениеВремениБезСекунд(ВремяОпоздания);
				Иначе
					ТекстСтатистики = ТекстСтатистики + Символы.ПС + "Приехал за ";
					ТекстСтатистики = ТекстСтатистики + ПолучитьПредставлениеВремениБезСекунд(ВремяОпоздания);		
				КонецЕсли;
			Иначе				
				Если ЗадачаМобильномуСотруднику.ПланируемаяДатаВыполненияРабот <= ЗадачаМобильномуСотруднику.ПриехалДата 
							И ЗадачаМобильномуСотруднику.ПриехалДата <= ЗадачаМобильномуСотруднику.ПланируемаяДатаОкончанияРабот Тогда
					ТекстСтатистики = ТекстСтатистики + Символы.ПС + "Уложился в интервал";
				Иначе
					ТекстСтатистики = ТекстСтатистики + Символы.ПС + "Не уложился в интервал";
				КонецЕсли;
			КонецЕсли;
			
			ТекстСтатистики = ТекстСтатистики + Символы.ПС + "Время в пути: ";
			ВремяВПути = Число(ЗадачаМобильномуСотруднику.ПриехалДата - ЗадачаМобильномуСотруднику.ВыехалДата);
			ТекстСтатистики = ТекстСтатистики + ПолучитьПредставлениеВремени(ВремяВПути);	
		КонецЕсли;
		
		Если НЕ ЗадачаМобильномуСотруднику.ПриехалДата = мПустаяДата И НЕ ЗадачаМобильномуСотруднику.ЗавершилДата = мПустаяДата Тогда
			ТекстСтатистики = ТекстСтатистики + Символы.ПС + "Время выполнения: ";
			ВремяВыполнения = Число(ЗадачаМобильномуСотруднику.ЗавершилДата - ЗадачаМобильномуСотруднику.ПриехалДата);
			ТекстСтатистики = ТекстСтатистики + ПолучитьПредставлениеВремени(ВремяВыполнения);	
		КонецЕсли;
				
	Иначе		
		Если ЗадачаМобильномуСотруднику.ПринялОтклонилДата = мПустаяДата Тогда
			ТекстСтатистики = "Нет данных";
		Иначе
			ТекстСтатистики = "Сотрудник отклонил задачу";
		КонецЕсли;
	КонецЕСли;
	
	Возврат ТекстСтатистики;
	
КонецФункции

&НаСервере
Функция ПолучитьПредставлениеВремени(текВремя)
	
	стрТекст = "";
	мПустаяДата = Дата("00010101");
	
	Если текВремя > 86400 Тогда // больше дня
		КолвоДней = Цел(текВремя / 86400);
		текДата = мПустаяДата + текВремя;
		стрТекст = Строка(КолвоДней) + "д. " + Формат(текДата, "ДФ=""ЧЧ 'ч.' мм 'мин.' сс 'сек.'""");
	ИначеЕсли текВремя  > 3600 Тогда // больше часа
		текДата = мПустаяДата + текВремя;
		стрТекст = Формат(текДата, "ДФ=""ЧЧ 'ч.' мм 'мин.' сс 'сек.'""");
	ИначеЕсли текВремя > 60 Тогда // больше минуты
		текДата = мПустаяДата + текВремя;
		стрТекст = Формат(текДата, "ДФ=""мм 'мин.' сс 'сек.'""");
	Иначе // несколько секунд
		стрТекст = Строка(текВремя) + " сек.";
	КонецЕсли;
	
	Возврат стрТекст;
		
КонецФункции

&НаСервере
Функция ПолучитьПредставлениеВремениБезСекунд(текВремя)
	
	стрТекст = "";
	мПустаяДата = Дата("00010101");
	
	Если текВремя < 0 Тогда
		текВремя = 0 - текВремя;
	КонецЕсли;
	
	Если текВремя > 86400 Тогда // больше дня
		КолвоДней = Цел(текВремя / 86400);
		текДата = мПустаяДата + текВремя;
		стрТекст = Строка(КолвоДней) + "д. " + Формат(текДата, "ДФ=""ЧЧ 'ч.' мм 'мин.'""");
	ИначеЕсли текВремя  > 3600 Тогда // больше часа
		текДата = мПустаяДата + текВремя;
		стрТекст = Формат(текДата, "ДФ=""ЧЧ 'ч.' мм 'мин.'""");
	Иначе
		текДата = мПустаяДата + текВремя;
		стрТекст = Формат(текДата, "ДФ=""мм 'мин.'""");
	КонецЕсли;
	
	Возврат стрТекст;
		
КонецФункции

#КонецОбласти
