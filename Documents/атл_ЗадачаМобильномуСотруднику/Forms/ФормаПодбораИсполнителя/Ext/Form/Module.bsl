
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Контрагент = Параметры.Контрагент;
	ШиротаАдреса = Параметры.ШиротаАдреса;
	ДолготаАдреса = Параметры.ДолготаАдреса;
	
	ДеревоПользователей.Параметры.УстановитьЗначениеПараметра("ДатаДопустимойПоследнейАктивности", ТекущаяДата() - 300);		
	ДеревоПользователей.Параметры.УстановитьЗначениеПараметра("ПустаяДата", Дата("00010101"));
	ДеревоПользователей.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь",ПараметрыСеанса.ТекущийПользователь);	
	ДеревоПользователей.Параметры.УстановитьЗначениеПараметра("ТолькоАктивные",ТолькоАктивные);	

	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтметкиКнопокПослеВосстановленияНастроек();
	
	ОбновитьФормуНаКлиенте();
	
КонецПроцедуры


#Область КомандыФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьФормуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПоследнейАктивности(Команда)
	
	Кнопка = Элементы.ДатаПоследнейАктивности;
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	ДатаПоследнейАктивности = Кнопка.Пометка;
	
	УстановитьВидимостьКолонкиПоследнейАктивности(Кнопка);
	ОбновитьФормуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНаКарте(Команда)
	
	АдресХранилища = ПолучитьСписокПользователейМК(УникальныйИдентификатор);
	ОтобразитьПользователейНаКарте(АдресХранилища);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоАктивные(Команда)
	
	Кнопка = Элементы.ТолькоАктивные;
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	ТолькоАктивные = Кнопка.Пометка;
	
	УстановитьКартинкуКнопкиАктивности(Кнопка);
	ОбновитьФормуНаКлиенте();
	
КонецПроцедуры

#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьФормуНаКлиенте()
	
	ДеревоПользователей.Параметры.УстановитьЗначениеПараметра("ДатаДопустимойПоследнейАктивности", ТекущаяДата() - 300);
	ДеревоПользователей.Параметры.УстановитьЗначениеПараметра("ТолькоАктивные",ТолькоАктивные);	
	Элементы.ДеревоПользователей.Обновить();	
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметкиКнопокПослеВосстановленияНастроек()
	
	Кнопка = Элементы.ТолькоАктивные;
	Кнопка.Пометка = ТолькоАктивные;
	УстановитьКартинкуКнопкиАктивности(Кнопка);
	
	Кнопка = Элементы.ДатаПоследнейАктивности;
	Кнопка.Пометка = ДатаПоследнейАктивности;	
	УстановитьВидимостьКолонкиПоследнейАктивности(Кнопка);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКартинкуКнопкиАктивности(Кнопка)
	
	Если Кнопка.Пометка Тогда
		Кнопка.Картинка = БиблиотекаКартинок.атл_СтатусыАктивностиАктивен;
	Иначе
		Кнопка.Картинка = БиблиотекаКартинок.атл_СтатусыАктивностиУжатые;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьКолонкиПоследнейАктивности(Кнопка)
	
	Элементы.ДеревоПользователейДатаПоследнейАктивности.Видимость = Кнопка.Пометка;
	Элементы.ДеревоПользователей.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокПользователейМК(УникальныйИдентификатор)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	атл_НастройкиПодключенияМобильныхСотрудников.Пользователь КАК Пользователь,
	               	|	атл_НастройкиПодключенияМобильныхСотрудников.ЯвляетсяПользователемМК КАК ЯвляетсяПользователемМК
	               	|ИЗ
	               	|	РегистрСведений.атл_НастройкиПодключенияМобильныхСотрудников КАК атл_НастройкиПодключенияМобильныхСотрудников
	               	|ГДЕ
	               	|	атл_НастройкиПодключенияМобильныхСотрудников.ЯвляетсяПользователемМК = ИСТИНА";
	Выгрузка = Запрос.Выполнить().Выгрузить();
	мСписок = Выгрузка.ВыгрузитьКолонку("Пользователь");
	
	АдресХранилища = ПоместитьВоВременноеХранилище(мСписок, УникальныйИдентификатор);
	Возврат АдресХранилища;
	
КонецФункции

&НаКлиенте
Процедура ОтобразитьПользователейНаКарте(АдресСписокаПользователейВХранилище)
	
	АдресТаблицы = ПолучитьАдресТаблицыСписокПараметровОткрытия(АдресСписокаПользователейВХранилище);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСпискаПараметровОткрытия", АдресТаблицы);
	ПараметрыФормы.Вставить("НеИнтерактивноеОткрытие", Истина);
	ПараметрыФормы.Вставить("ПоказыватьТекущееМестонахождение", Истина);
	ПараметрыФормы.Вставить("ПодборСотрудника", Истина);
	
	ОповещениеФормы = Новый ОписаниеОповещения("ОбработкаПодбораСотрудника", ЭтотОбъект);

	ОткрытьФорму("Обработка.атл_ОтображениеТочекСТрекомНаКарте.Форма.Форма", ПараметрыФормы, ЭтаФорма,,,, ОповещениеФормы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицыСписокПараметровОткрытия(АдресСписокаПользователейВХранилище)
	
	ТаблицаПараметровОткрытия = атл_УправлениеМобильнымиСотрудникамиСервер.СформироватьТаблицуСписокПараметровОткрытияДляОткрытияКарты();
	
	СписокПользователей = ПолучитьИзВременногоХранилища(АдресСписокаПользователейВХранилище);
	
	Для Каждого текПользователь Из СписокПользователей Цикл
		мНоваяСтрока = ТаблицаПараметровОткрытия.Добавить();
		мНоваяСтрока.Пользователь = текПользователь;
	КонецЦикла;

	Если ЗначениеЗаполнено(ШиротаАдреса) И ЗначениеЗаполнено(ДолготаАдреса) Тогда
		мНоваяСтрока = ТаблицаПараметровОткрытия.Добавить();
		мНоваяСтрока.ВидОбъекта = "ТочкаНаКарте";
		мНоваяСтрока.Объект = Контрагент;
		мНоваяСтрока.Широта = ШиротаАдреса;
		мНоваяСтрока.Долгота = ДолготаАдреса;
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаПараметровОткрытия, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаПодбораСотрудника(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатЗакрытия = Неопределено И ТипЗнч(РезультатЗакрытия) = Тип("СправочникСсылка.Пользователи") Тогда
		Закрыть(РезультатЗакрытия);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	мТекущиеДанные = Элементы.ДеревоПользователей.ТекущиеДанные;
	Если НЕ мТекущиеДанные = Неопределено Тогда
		Закрыть(мТекущиеДанные.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти





