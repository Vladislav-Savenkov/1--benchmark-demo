
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПервыйЗапуск = Параметры.ПервыйЗапуск;
	
	мНастройкиПодсистемыУМС = РегистрыСведений.атл_НастройкиПодсистемыУМС.СоздатьМенеджерЗаписи();
	мНастройкиПодсистемыУМС.Прочитать();
	
	ЗаполнитьЗначенияСвойств(Запись, мНастройкиПодсистемыУМС);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИЗакрытьНаСервере();
	ОбновитьПовторноИспользуемыеЗначения();
	Закрыть();
	
	Если ПервыйЗапуск Тогда
		ОткрытьФорму("Обработка.атл_НастройкаОбменаДаннымиУМС.Форма.Форма");	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьИЗакрытьНаСервере()
	
	мНастройкиПодсистемыУМС = РегистрыСведений.атл_НастройкиПодсистемыУМС.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(мНастройкиПодсистемыУМС, ЭтаФорма.Запись);
	
	мНастройкиПодсистемыУМС.Записать();	
	
КонецПроцедуры


&НаКлиенте
Процедура Местонахождение(Команда)
	
	АдресТаблицы = ПолучитьАдресТаблицы();
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСпискаПараметровОткрытия", АдресТаблицы);
	ПараметрыФормы.Вставить("НеИнтерактивноеОткрытие", Истина);
	ПараметрыФормы.Вставить("ВводКоординаты", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы", "Адрес организации");
	
	ОповещениеФормы = Новый ОписаниеОповещения("ОбработкаВводаКоординат", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.атл_ОтображениеТочекСТрекомНаКарте.Форма.Форма", ПараметрыФормы, ЭтаФорма,,,, ОповещениеФормы);

КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицы()
	
	ТаблицаПараметровОткрытия = атл_УправлениеМобильнымиСотрудникамиСервер.СформироватьТаблицуСписокПараметровОткрытияДляОткрытияКарты();
	
	мНоваяСтрока = ТаблицаПараметровОткрытия.Добавить();
	мНоваяСтрока.Пользователь = ПараметрыСеанса.ТекущийПользователь;	
	Если ЗначениеЗаполнено(Запись.ШиротаАдресОрганизации) И ЗначениеЗаполнено(Запись.ДолготаАдресОрганизации) Тогда
		мНоваяСтрока.ВидОбъекта = "ТочкаНаКарте";	
		мНоваяСтрока.Широта = Запись.ШиротаАдресОрганизации;
		мНоваяСтрока.Долгота = Запись.ДолготаАдресОрганизации;
	Иначе
		мНоваяСтрока.ВидОбъекта = "АдресДляПоискаНаКарте";	
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаПараметровОткрытия, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВводаКоординат(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатЗакрытия = Неопределено И ТипЗнч(РезультатЗакрытия) = Тип("Строка") И НЕ РезультатЗакрытия = "" тогда
		НайденныйРазделитель = Найти(РезультатЗакрытия, ";");
		Если НайденныйРазделитель > 0 Тогда
			Запись.ДолготаАдресОрганизации = Сред(РезультатЗакрытия, 1, НайденныйРазделитель - 1);
			Запись.ШиротаАдресОрганизации = Сред(РезультатЗакрытия, НайденныйРазделитель + 1);			
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры
